<?xml version="1.0" encoding="utf-8"?>
<mx:Text xmlns:mx="http://www.adobe.com/2006/mxml"
		 implements="mx.controls.listClasses.IDropInListItemRenderer"
		 toolTip="click to Editâ€¦" click="callLater(openPopup)" doubleClickEnabled="true"
		 text="{txt}">
	
	<mx:Script>
		<![CDATA[
			import hci.flex.renderers.RendererFactory;
			
			import mx.controls.AdvancedDataGrid;
			import mx.controls.DataGrid;
			import mx.controls.dataGridClasses.DataGridListData;
			import mx.controls.listClasses.BaseListData;
			import mx.controls.listClasses.ListData;
			import mx.core.IFactory;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			
			private var _listData:DataGridListData;
			
			[Bindable]
			public var txt:String;
			
			public var isEditable:Boolean = true;
			public var isRequired:Boolean = false;
			public var addOptionOnFly:Boolean = false;
			
			private var pop:MultiselectPopup;
			private var idProperty:String;
			
			public static function create(isEditable:Boolean=true,isRequired:Boolean=false,addOptionOnFly:Boolean=false):IFactory {
				return RendererFactory.create(MultiselectRenderer, {isEditable:isEditable, isRequired:isRequired, addOptionOnFly:addOptionOnFly});
			}		
			override public function set data(value:Object):void {
				super.data = value;
				if (_listData == null) {
					return;
				}
				
				txt = "";
				if (data == null || _listData == null || !data.hasOwnProperty(_listData.dataField) || _listData.dataField == null || _listData.dataField == '') {
					return;
				}
	
				idProperty = _listData.dataField.substr(6);
				
				var values:Array = new Array();
				values = data[_listData.dataField].split(",");
				var idx:Object = null;
				var allOptions:XMLList = getOptions();
					
				var option:XML = null;
				for each(option in allOptions) {
					var theOption:XML = option.copy();
					var isSelected:Boolean = false;
					if (values != null) {
						for (idx in values) {
							if (values[idx] != null && theOption.@idPropertyOption == values[idx]) {
								isSelected = true;
								break;
							}
						}
					}
					if (isSelected) {
						if (txt.length > 0) {
							txt += ",";
						}
						txt += option.@option;
					}
				}
			}
			
			override public function get data():Object {
				return super.data;
			}
			
			override public function get listData():BaseListData
			{
				return _listData;
			}
			
			override public function set listData(value:BaseListData):void
			{
				_listData = DataGridListData(value);
			}
			
			private function openPopup():void{
				if (!isEditable) {
					return;
				}
				if (_listData == null || _listData.dataField == null || !(data is XML)) {
					return;
				}
				idProperty = _listData.dataField.substr(6);
				pop= MultiselectPopup(PopUpManager.createPopUp(this, MultiselectPopup,true));
				pop.opener=this;
				var propertyName:String = parentApplication.getSampleProperty(idProperty).@name;
				pop.initializeData(propertyName, this.txt, addOptionOnFly, idProperty);
			}
			
			public function getOptions():XMLList {
				// Use parent document to get options because then we will know if inactive options are included
				return parentDocument.getPropertyOptions(idProperty);
			}
			
			public function updateDP(str:String):void{
				if (!isEditable) {
					return;
				}
				if (_listData == null) {
					return;
				}
				
				this.data[_listData.dataField]=str;
				
				if (_listData.owner is DataGrid) {
					var dg:DataGrid=_listData.owner as DataGrid;
					dg.dataProvider.itemUpdated(data);					
				} else if (_listData.owner is AdvancedDataGrid) {
					var adg:AdvancedDataGrid=_listData.owner as AdvancedDataGrid;
					adg.dataProvider.itemUpdated(data);					
				} 
				
				this.invalidateDisplayList();
				this.validateNow();
			}
			
			override protected function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void
			{
				super.updateDisplayList(unscaledWidth,unscaledHeight);
				
				if (data == null || !(data is XML)) {
					return;
				}
				
				if (!isEditable) {
					return;
				}
				
				var g:Graphics = graphics;
				g.clear();
				
				if (this.text == null || this.text == '') {
					if ( !isRequired ) {
						g.beginFill(RendererFactory.DEFAULT_MISSING_FIELD_BACKGROUND);
						g.lineStyle(RendererFactory.DEFAULT_MISSING_REQUIRED_FIELD_BORDER_THICKNESS,
							RendererFactory.DEFAULT_MISSING_FIELD_BACKGROUND);          	
						g.drawRect(0,0,unscaledWidth,unscaledHeight);
						g.endFill();
					} else {
						g.beginFill(RendererFactory.DEFAULT_MISSING_REQUIRED_FIELD_BACKGROUND);
						g.lineStyle(RendererFactory.DEFAULT_MISSING_REQUIRED_FIELD_BORDER_THICKNESS,
							RendererFactory.DEFAULT_MISSING_REQUIRED_FIELD_BORDER);          	
						g.drawRect(0,0,unscaledWidth,unscaledHeight);
						g.endFill();
					}
				} 
				
			}

			
		]]>
	</mx:Script>
	
</mx:Text>


