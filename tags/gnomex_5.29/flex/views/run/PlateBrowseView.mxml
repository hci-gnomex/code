<?xml version="1.0" encoding="utf-8"?>
<mx:VBox styleName="panelBox"  xmlns:mx="http://www.adobe.com/2006/mxml" 
		 width="100%" height="100%" backgroundAlpha="0"
		 xmlns:util="views.util.*" xmlns:run="views.run.*"  show="init()"  verticalGap="0">
	
	<mx:HTTPService 
		id="getPlateList" 
		url="GetPlateList.gx"
		resultFormat="e4x"
		result="onGetPlateList(event)"
		showBusyCursor="true"
		fault="parentApplication.onFailHttpRequest('Unable to get plate list', event)"
		method="POST"
		useProxy="false">
	</mx:HTTPService >
	
	<mx:HTTPService 
		id="deletePlates" 
		url="DeletePlates.gx"
		resultFormat="e4x"
		result="onDeletePlates(event)"
		showBusyCursor="true"
		fault="parentApplication.onFailHttpRequest('Unable to delete plates', event)"
		method="POST"
		useProxy="false">
	</mx:HTTPService >
	
	<mx:Script> 
		<![CDATA[
			import hci.flex.controls.DropdownLabel;
			
			import mx.controls.Alert;
			import mx.core.ScrollPolicy;
			import mx.events.CloseEvent;
			import mx.events.ListEvent;
			import mx.events.PropertyChangeEvent;
			import mx.managers.PopUpManager;
			import mx.rpc.events.ResultEvent;
			
			import views.util.EmailWindowForPlates;
			
			// Warning Icon
			[Embed( source = '../../assets/error.png' )]
			[Bindable]
			public var iconWarning:Class;
			
			[Bindable]
			public var parentView:NavRunView;
			
			public function init():void {
				getList();
			}
						
			private function gridItemClick(event:ListEvent):void {
				if ( plateGrid.selectedItem.@codeReactionType == "CHERRY" || plateGrid.selectedItem.@codePlateType == "SOURCE" ) {
					return;
				}
				if ( event.columnIndex == 2 ) {
					parentApplication.showPlate(plateGrid.selectedItem);
				}
				if ( event.columnIndex != 0 && event.columnIndex != 2 ) {
					plateGrid.selectedIndex = -1;
				}
				if ( event.columnIndex == 0 && plateGrid.selectedItems.length > 1 ) {
					for each ( var obj1:Object in plateGrid.selectedItems ) {
						obj1.@isSelected = true;
					}
				}
				selectedPlates.refresh();
			}
			public function getList():void {
				getPlateList.send(getPlateParameters());
			}
			
			private function onGetPlateList(event:ResultEvent):void{
				if ( getPlateList.lastResult.name() == "PlateList"){
					selectedPlates.refresh();
				}
				else{
					Alert.show( event.result..ERROR.@message.toString(), "Error getting plate list" );
				}
				
			}
			
			private function promptToDelete():void {
				
				Alert.show( "Are you sure you want to delete these plates?", "Confirm Delete", ( Alert.YES | Alert.NO ), this, onPromptToDelete, iconWarning );
			}
			
			private function onPromptToDelete( event:CloseEvent ):void {
				
				if ( event.detail == Alert.YES ) {
					deleteSelectedPlates();
				}
			}
			
			private function deleteSelectedPlates():void {
				var params:Object = new Object;
				var platesToDelete:String = "<Plates>";
				
				for each(var node:XML in selectedPlates){
					platesToDelete += "<idPlate>" + node[0].@idPlate + "</idPlate>";
				} 
				platesToDelete += "</Plates>";
				params.platesToDeleteXMLString = new XML(platesToDelete);
				deletePlates.send(params);
				
			}
			
			private function onDeletePlates(event:ResultEvent):void{
				if ( event.result.name() == "SUCCESS" ) {
					getList();
					return;
				}
				Alert.show( event.result..ERROR.@message.toString(), "Error deleting plates" );
				
			}
			
			private function getPlateParameters():Object {
				var params:Object = new Object;

				
				if ( parentDocument.allCheckbox.selected ) {
					params.getAll = "Y";
					return params;
				}
				
				params.plateName = parentDocument.lookupPlateNameField.text != null ? parentDocument.lookupPlateNameField.text : '' ;
				if (parentView.pickCreateDateRangeButton.fromDate != null && parentView.pickCreateDateRangeButton.toDate != null) {
					params.createDateFrom = parentView.pickCreateDateRangeButton.dateFormatter.format(parentView.pickCreateDateRangeButton.fromDate);
					params.createDateTo = parentView.pickCreateDateRangeButton.dateFormatter.format(parentView.pickCreateDateRangeButton.toDate);
				}
				
				if (parentDocument.reactionCheckbox.selected){
					params.plateType = "REACTION"; 
				}
				else if (parentDocument.sourceCheckbox.selected){
					params.plateType = "SOURCE";
				}
				
				params.codeReactionType = 'SEQ';
				
				return params;
			}
			
			private function checkEmptyFilter():Boolean {
				
				if (parentView.pickCreateDateRangeButton.fromDate != null && parentView.pickCreateDateRangeButton.toDate != null) {
					return false;
				}
				if (parentView.pickRunDateRangeButton.fromDate != null && parentView.pickRunDateRangeButton.toDate != null) {
					return false;
				}
				if ( parentDocument.pendingCheckbox.selected ) {
					return false ;
				}
				if ( parentDocument.runningCheckbox.selected ) {
					return false ;
				}
				if ( parentDocument.completeCheckbox.selected ) {
					return false ;
				}
				if ( parentDocument.failedCheckbox.selected ) {
					return false ;
				}
				
				return true;
			}
			
			private function selectedFilterFunc(item:XML):Boolean {
				return item.@isSelected == true;
			} 	
			
			public function createSampleSheetFile(event:MouseEvent):void {
				var idPlate:int = (int)(plateList[plateGrid.selectedIndex].@idPlate);
			
				var url:URLRequest = new URLRequest('DownloadPlateSampleSheetFileServlet.gx' +
					'?idPlate=' + idPlate);
				navigateToURL( url, '_blank' );
				
			}
			
			private function showEmailWindow():void{
				var emailWindow:EmailWindowForPlates = EmailWindowForPlates(PopUpManager.createPopUp(this, EmailWindowForPlates, true));
				emailWindow.selectedPlates = this.selectedPlates;
				PopUpManager.centerPopUp(emailWindow);
				this.setFocus();
				
			}
			
		]]>
	</mx:Script>    	
	
	<mx:XMLListCollection id = "plateList" source = "{getPlateList.lastResult..Plate}" />
	<mx:XMLListCollection id = "selectedPlates" source="{getPlateList.lastResult..Plate}" filterFunction="selectedFilterFunc"/>
	
	<mx:VDividedBox width = "100%"
					height = "100%"
					paddingBottom = "4"
					paddingLeft = "4"
					paddingRight = "4"
					paddingTop = "4">
		
		<mx:VBox width = "100%" height = "100%" verticalGap = "1">
			
			<mx:HBox width = "100%" verticalAlign = "bottom">
				<mx:Spacer width = "100%" />
				
			</mx:HBox>
			
			<mx:DataGrid id = "plateGrid" minWidth="0" horizontalScrollPolicy="{ScrollPolicy.AUTO}"
						 allowMultipleSelection="true"
						 click="{selectedPlates.refresh()}"
						 itemClick="gridItemClick(event)"
						 width = "100%"
						 height = "100%"
						 dataProvider = "{plateList}"
						 sortArrowSkin="mx.skins.ProgrammaticSkin" fontSize="11">
				<mx:columns>
					
					<mx:DataGridColumn id = "checkBoxColumn"
									   textAlign = "center"
									   width = "30"
									   resizable = "false"
									   sortable = "false"
									   editable = "true"
									   rendererIsEditor = "true"
									   dataField = "@isSelected"
									   editorDataField = "selected"
									   itemRenderer = "views.renderers.CheckBoxIsSelectedToggle"
									   headerRenderer = "views.renderers.CheckBoxHeader"
									   draggable="false"/>
					
					<mx:DataGridColumn   minWidth="28" width="50" headerText="#">
						<mx:itemRenderer>
							<mx:Component >
								<mx:HBox paddingLeft="1" horizontalGap="0"   horizontalScrollPolicy="off">
									<mx:Image id="experimentImage"  
											  source="{data.@icon}" >
									</mx:Image> 
									<mx:Label id="requestNumber" text="{data.@idPlate}">
									</mx:Label>
								</mx:HBox>
							</mx:Component>
						</mx:itemRenderer>
					</mx:DataGridColumn> 
					
					
					<mx:DataGridColumn dataField = "@label"
									   headerText = "Label"
									   width = "175"
									   textDecoration = "underline"
									   color = "#2017D8"
									   draggable="false">
						<mx:headerRenderer>
							<mx:Component>
								<mx:Label text = "Label" textDecoration = "none" color = "#000000" />
								
							</mx:Component>
							
						</mx:headerRenderer>
						
					</mx:DataGridColumn>
					
					<mx:DataGridColumn dataField="@codePlateType"
									   headerText= "Plate Type"
									   width="100" itemRenderer="{hci.flex.controls.DropdownLabel.getFactory(parentApplication.dictionaryManager.xml.Dictionary.(@className=='hci.gnomex.model.PlateType').DictionaryEntry, '@display', '@value', '@codePlateType')}"/>
					
					<mx:DataGridColumn dataField = "@creator" headerText = "Created by" width = "95" />
					
					<mx:DataGridColumn dataField = "@createDate" headerText = "Created on" width = "80" />
					
					<mx:DataGridColumn headerText="Export Samples" headerWordWrap="true" editable="false" width="110">
						<mx:itemRenderer>
							<mx:Component>
								<mx:LinkButton label="Export Samples" click="outerDocument.createSampleSheetFile(event)" color="#0000FF" textDecoration = "underline" textAlign="left"/>
							</mx:Component>
						</mx:itemRenderer>
					</mx:DataGridColumn>
					
					<mx:DataGridColumn dataField = "@codeReactionType" 
									   headerText = "Reaction Type" width = "120"
									   itemRenderer="{hci.flex.controls.DropdownLabel.getFactory(parentApplication.dictionaryManager.xml.Dictionary.(@className=='hci.gnomex.model.ReactionType').DictionaryEntry, '@display', '@value', '@codeReactionType')}"/>
					
					<mx:DataGridColumn dataField = "@codeSealType" 
									   headerText = "Seal" width = "80" 
									   itemRenderer="{hci.flex.controls.DropdownLabel.getFactory(parentApplication.dictionaryManager.xml.Dictionary.(@className=='hci.gnomex.model.SealType').DictionaryEntry, '@display', '@value', '@codeSealType')}"/>
					
					<mx:DataGridColumn dataField = "@comments" headerText = "Comments"  />
					
				</mx:columns>
				
			</mx:DataGrid>
			
		</mx:VBox>
		
	</mx:VDividedBox>
	
	<mx:ControlBar horizontalAlign = "left" width = "100%" paddingTop="2" paddingBottom="2">
		<mx:Label id = "nSelectedRuns" text="{selectedPlates.length + ' selected'}" />
		
		<mx:LinkButton textDecoration="underline"
					   label="Delete"
					   icon="@Embed('../../assets/delete.png')"
					   toolTip="Delete selected runs"
					   click="{promptToDelete()}" 
					   disabledIcon="@Embed('../../assets/delete_disable.png')"
					   enabled="{selectedPlates.length>0}"/>
		
		<mx:LinkButton textDecoration="underline"
					   label="Email"
					   icon="@Embed('../../assets/email_go.png')"
					   toolTip="Email GNomEx users associated with selected plates"
					   click="{showEmailWindow()}" 
					   disabledIcon="@Embed('../../assets/email_go_disable.png')"
					   enabled="{selectedPlates.length>0}"/>
		
		<mx:Spacer width="100%" height="100%" />
		<mx:Text id="messageLabel" text="{'(' + getPlateList.lastResult.@message + ')'}" visible="{getPlateList.lastResult.@message != '' &amp;&amp; plateList.length > 0}" width="100%" fontStyle="normal" fontWeight="normal" fontSize="10" textAlign="right"/>
		<mx:Label text = "{'(' + plateList.length + ' plates)'}" fontWeight = "normal" textAlign = "right"  fontStyle="italic"/>
		
		
	</mx:ControlBar>
	
</mx:VBox>
