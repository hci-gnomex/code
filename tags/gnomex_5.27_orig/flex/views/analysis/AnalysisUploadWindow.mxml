<?xml version="1.0" encoding="utf-8"?>
<util:ResizableTitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" 
				layout="vertical" width="824" 
				xmlns:util="views.util.*"
				height="408" creationPolicy="all"
				creationComplete="centerMe()"
				showCloseButton="true"
				close="xClose(event);" 
				title="{'Upload Files for Analysis ' + this.analysis.@number}"  xmlns:controls="hci.flex.controls.*">
	
	<mx:HTTPService  
		id="getAnalysisUploadURL" 
		url="UploadAnalysisURLServlet.gx"
		resultFormat="e4x"
		showBusyCursor="true" 
		result="onGetAnalysisUploadURL(event)" 
		fault="parentApplication.onFailHttpRequest('Failed to get analysis upload URL', event)"
		method="POST"
		useProxy="false">
	</mx:HTTPService>
	
	<mx:HTTPService  
		id="fdtUploadStart" 
		url="FastDataTransferUploadStart.gx"
		resultFormat="e4x"
		showBusyCursor="true" 
		result="onFdtUploadStart(event)" 
		fault="parentApplication.onFailHttpRequest('Failed to start FDT upload', event)"
		method="POST"
		useProxy="false">
	</mx:HTTPService>
	
	<mx:HTTPService  
		id="getAnalysis" 
		url="GetAnalysis.gx"
		resultFormat="e4x"
		showBusyCursor="true" 
		result="onGetAnalysis(event)" 
		fault="parentApplication.onFailHttpRequest('Failed to get Analysis', event)"
		method="POST"
		useProxy="false">
	</mx:HTTPService>
	
	<mx:HTTPService  
		id="getAnalysisDownloadList" 
		url="GetAnalysisDownloadList.gx"
		destination="getAnalysisDownloadList"
		resultFormat="e4x"
		showBusyCursor="true"
		result="onGetAnalysisDownloadList(event)"
		fault="parentApplication.onFailHttpRequest('Failed to get analysis download list', event)"
		method="POST"
		useProxy="false">
	</mx:HTTPService >
	
	<mx:HTTPService  
		id="organizeAnalysisUploadFiles" 
		url="OrganizeAnalysisUploadFiles.gx"
		resultFormat="e4x"
		showBusyCursor="true"
		result="onOrganizeAnalysisUploadFiles(event)"
		fault="parentApplication.onFailHttpRequest('Failed to organize analysis upload files', event)"
		method="POST"
		useProxy="false">
	</mx:HTTPService >
	
	
	<mx:Script>
		<![CDATA[
      import flash.net.sendToURL;
      
      import mx.controls.Alert;
      import mx.core.DragSource;
      import mx.events.CloseEvent;
      import mx.events.DragEvent;
      import mx.events.ListEvent;
      import mx.managers.DragManager;
      import mx.managers.PopUpManager;
      import mx.rpc.events.ResultEvent;
      
      import views.util.MultiFileUpload;
      import views.util.ResizableTitleWindow;
	  import views.util.GNomExStringUtil;
			
			[Bindable]
			private var analysis:Object;
			[Bindable]
			private var idAnalysis:Object;
			[Bindable]
			private var analysisNumber:Object;
			[Bindable]
			public var parentView:AnalysisDownloadView;
			
			private var multiFileUpload:MultiFileUpload;
			
			private var downloadDraggedItems:Array = null;
			
			private var analysisEditView:AnalysisEditView;
			private var analysisDetailView:AnalysisDetailView;
			
			private var isInitialized:Boolean = false;
			
			// ResizableTitleWindow needs to be posistioned after it is created
			public function centerMe():void
			{
				PopUpManager.centerPopUp(this);
			}
			
			public function init(analysis:Object):void {
				this.analysis = analysis;
				this.idAnalysis = analysis.@idAnalysis as Object;
				this.analysisNumber = analysis.@analysisNumber as Object;
				
				isInitialized = true;
				
				fdtStartButton.enabled = true;
				saveUUID.text = "";
				
				this.getAnalysisUploadURL.send();
				
				var params:Object = new Object();
				params.idAnalysis = idAnalysis.toString();
				params.includeUploadStagingDir = 'N';
				getAnalysisDownloadList.send(params);
        
        		refreshUploadFiles();
				this.addEventListener("refreshUploadFiles", onUploadFinished);
			}
			
			public function refreshFiles():void {
				filesToRemove.removeAll();
				this.dirty.resetDirty();
				
				refreshUploadFiles();
				
				var params:Object = new Object();
				params.idAnalysis = idAnalysis.toString(); 
				params.includeUploadStagingDir = 'N';
				getAnalysisDownloadList.send(params);
        
			}
			
			public function refreshAnalysis(updAnalysis:Object):void {
				this.analysis = updAnalysis;
				this.idAnalysis = analysis.@idAnalysis as Object;
				this.analysisNumber = analysis.@analysisNumber as Object;
				refreshFiles();
			}
			
			private function onGetAnalysisUploadURL(event:ResultEvent):void {
				if (getAnalysisUploadURL.lastResult.name() == "UploadAnalysisURL") {
					var url:String = getAnalysisUploadURL.lastResult.@url;
					initializeFileUploadControl(url);
				}
			}
			
			private function onFdtUploadStart(event:ResultEvent):void {
				if (fdtUploadStart.lastResult.name() == "FDTUploadUuid") {
					var uuid:String = fdtUploadStart.lastResult.@uuid;
					saveUUID.text = uuid;
					fdtStartButton.enabled = false;
					
					var url:URLRequest = new URLRequest('FastDataTransferUploadGetJnlpServlet.gx');
					var uv:URLVariables = new URLVariables();
					uv.uuid = uuid;
					url.data = uv;
					url.method = URLRequestMethod.POST;
					navigateToURL(url, '_self');	
          
				}
			}
			
			public function initializeFileUploadControl(uploadServletURL:String):void{
				
				if (multiFileUpload == null) {
					var postVariables:URLVariables = new URLVariables;
					postVariables.idAnalysis = idAnalysis;
					postVariables.analysisNumber = analysisNumber; 
					
					multiFileUpload = new MultiFileUpload(
						this.uploadFilesGrid,
						this.uploadBrowseButton,
						this.uploadClearButton,
						this.uploadDeleteButton,
						this.uploadButton,
						this.uploadProgressBar,
						uploadServletURL,
						postVariables,
						0,
						null
					);
					
					multiFileUpload.addEventListener(Event.COMPLETE, onUploadFinished);
					multiFileUpload.addEventListener(Event.OPEN, onChooseFileToUpload);
					
					this.uploadBrowseButton.enabled = true;
					this.uploadClearButton.enabled = true;
					this.uploadDeleteButton.enabled = true;
				} 
				
			}
			
			private function refreshUploadFiles():void {
				if (idAnalysis != null) {  
					var params:Object = new Object();
					params.idAnalysis = idAnalysis.toString();
					params.showUploads = "Y";
					getAnalysis.send(params); 
				}      	
			}
			
			public function onUploadFinished(event:Event):void{
				this.theTab.selectedIndex = 1;
				refreshUploadFiles();	
			}
			
			private function onGetAnalysis(event:ResultEvent): void {
				if (getAnalysis.lastResult.name() == "OpenAnalysisList") {
					newFolderButton.enabled = true; 
				}
			}
			
			public function onChooseFileToUpload(event:Event):void {
			}
			
			private function onGetAnalysisDownloadList(event:ResultEvent): void {
				if (getAnalysisDownloadList.lastResult.name() != "AnalysisDownloadList") {
					mx.controls.Alert.show(getAnalysisDownloadList.lastResult..ACTMESSAGE.@TEXT);
					return;
				} else {
					newFolderButton.enabled = true; 
					callLater(expandDownloadTree);
				}  	 
			}
			
			private function expandDownloadTree():void {
		        var maxFiles:int = 60;
		        var count:int = 0;
				if (downloadTree != null) {
		          	downloadTree.expandItem(downloadList.getItemAt(0),true); 
					for each(var a:Object in getAnalysisDownloadList.lastResult..AnalysisFileDescriptor) {
		    	        downloadTree.expandItem(a,true);
		        	    count++;
		            	if (count>=maxFiles){
		              	break;
          		 		}
					}
				}
			}
			
			private function collapseDownloadTree():void {
				if (downloadTree != null) {
					downloadTree.expandItem(downloadList.getItemAt(0),true); 
					for each(var a:Object in getAnalysisDownloadList.lastResult..AnalysisFileDescriptor) {
						downloadTree.expandItem(a,false);
						
					}
				}
			}
			
			private function save():void {
				var params:Object = new Object();
				params.idAnalysis = idAnalysis.toString();
				//*****(10/17/2012)getDirtyAnalysisFiles needs to be debugged. It doesn't properly get dirty analysis file.  
				//Workaround for now since release is imminent is to just send the entire download list to the server.*****
				//params.filesXMLString = getDirtyAnalysisFiles().toXMLString();  
				params.filesXMLString = this.downloadList.toXMLString();
				if (filesToRemove.length > 0) {
					params.filesToRemoveXMLString = filesToRemove.toXMLString();			
				}
				organizeAnalysisUploadFiles.send(params);
			}
			
			private function onOrganizeAnalysisUploadFiles(event:ResultEvent): void {
				if (organizeAnalysisUploadFiles.lastResult.name() != "SUCCESS") {
					mx.controls.Alert.show(organizeAnalysisUploadFiles.lastResult..ACTMESSAGE.@TEXT);
				} else {
					refreshFiles();
				} 	 
			}
			private function getFilesGridRowNumber(item:Object,col:int):String
			{
				var x:int = filesGrid.dataProvider.getItemIndex(item) + 1;
				return String(x);
			} 		
			
			private function getDownloadName(item:Object):String {
				return item.@displayName;
			}
			
			public function showAddFolderWindow():void {
				if (this.downloadTree.selectedItem == null) {
					this.downloadTree.selectedIndex = 0;
				}
				
				var parent:XML = XML(downloadTree.selectedItem);
				if (this.downloadTree.selectedItem.@type != 'dir' && parent.name() != "Analysis") {
					parent = XML(downloadTree.selectedItem).parent();
				} 
								
				var addFolderWindow:FolderAddWindow = FolderAddWindow(PopUpManager.createPopUp(DisplayObject(parentApplication.theBody), FolderAddWindow, true));
				PopUpManager.centerPopUp(addFolderWindow);
				addFolderWindow.uploadWindow = this;
				addFolderWindow.folderName.setFocus();
			}
			
			public function getDirtyAnalysisFiles():XML{
				var topNodeShallow:XML = new XML("<Analysis/>");
				shallowCopy(downloadList[0] as XML, topNodeShallow);
				recurseGetDirty(downloadList[0], topNodeShallow);
				return topNodeShallow;
			}
			
			private function recurseGetDirty(node:XML, nodeShallow:XML):void {
				for each(var childNode:XML in node.children()) {
					var keep:Boolean = false;
					if (childNode.@type == 'dir') {
						if (childNode.@dirty == 'Y') {
							keep = true;
						} else {
							var dirtyNodes:XMLList = childNode..AnalysisFileDescriptor.(@dirty == 'Y');
							if (dirtyNodes.length() > 0) {
								keep = true;
							}
						}
					} else if (childNode.@dirty == 'Y') {
						keep = true;
					}
					if (keep) {
						var childNodeShallow:XML = new XML("<AnalysisFileDescriptor/>");
						shallowCopy(childNode, childNodeShallow);
						nodeShallow.appendChild(childNodeShallow);
					}
					recurseGetDirty(childNode, childNodeShallow);
				}
			}
			
			private function shallowCopy(source:XML, destination:XML):void {
				for each(var attr:XML in source.attributes()) {
					destination[attr.name()] = attr.toString();
				}
			}

			
			public function addDownloadFolder(folderName:String):void {
				var parent:XML = XML(downloadTree.selectedItem);
				if (this.downloadTree.selectedItem.@type != 'dir' && parent.name() != "Analysis") {
					parent = XML(downloadTree.selectedItem).parent();
				} 
				var folder:XML;
				
				folder = new XML("<AnalysisFileDescriptor isNew='Y' " +
					"dirty='Y' " + 
					"key='X-X-" + GNomExStringUtil.makeXMLSafe(folderName) + "' " +
					"displayName='" + GNomExStringUtil.makeXMLSafe(folderName) + "' " +
					"type='dir'" +
					"qualifiedFilePath='' " +
					"isEmpty='Y'" +
					"info='' />");
				
				if (parent.children().length() > 0) {
					var firstChild:XML = parent.children()[0];
					parent.insertChildBefore(firstChild, folder);	 		    	
				} else {
					parent.appendChild(folder);
				}	    	                         
				this.downloadTree.expandItem(parent, true);
				this.downloadTree.selectedItem = null;
				this.downloadTree.selectedItem = folder;
				this.downloadTree.scrollToIndex(this.downloadTree.selectedIndex); 
				this.downloadTree.selectedItem = null;
			}
			
			private function removeAnalysisFile():void  {
				
				if ((this.filesGrid.selectedItems == null || this.filesGrid.selectedItems.length == 0) &&
				    (this.downloadTree.selectedItems == null || this.downloadTree.selectedItems.length == 0) ) {
					Alert.show("Please select the files to delete.");
					return;
				} else if (this.filesGrid.selectedItems.length == 1 || this.downloadTree.selectedItems.length == 1) {
					if ( dataTrackFileSelected() ) {
						Alert.show("This file is linked to a data track.  Do you want to remove the file and delete the associated data track?",
							"Confirm",
							(Alert.YES | Alert.NO), this, 
							onPromptToRemoveFile,						
							parentApplication.iconWarning);
					}  else {
						removeAnalysisFileImpl();
					} 
				} else if (this.filesGrid.selectedItems.length > 1 || this.downloadTree.selectedItems.length > 1) {
					if ( dataTrackFileSelected() ) {
						Alert.show("At lease one selected file is linked to a data track.  Do you want to remove the files and delete any associated data tracks?",
							"Confirm",
							(Alert.YES | Alert.NO), this, 
							onPromptToRemoveFile,						
							parentApplication.iconWarning);
					}  else {
						removeAnalysisFileImpl();
					} 
				}
			}
			
			private function onPromptToRemoveFile(event:CloseEvent):void {
				if (event.detail == Alert.YES) {
					removeAnalysisFileImpl();
				}
			}
			
			
			private function removeAnalysisFileImpl():void {
				if (this.filesGrid.selectedItems != null && this.filesGrid.selectedItems.length > 0) {
					for each(var uploadFile:XML in this.filesGrid.selectedItems) {
						this.filesToRemove.addItem(uploadFile);
						this.uploadedFiles.removeItemAt(uploadedFiles.getItemIndex(uploadFile));
					}
					this.dirty.setDirty();
				} else if (this.downloadTree.selectedItems != null && this.downloadTree.selectedItems.length > 0) {
					for each(var downloadFile:XML in this.downloadTree.selectedItems) {
						// Only add to remove list if this file exists
						if(downloadFile.@type == "dir"){
							this.filesToRemove.addItem(downloadFile);
							checkXMLChildren(downloadFile);
						}
						else if (downloadFile.hasOwnProperty("@fileName")) {
							this.filesToRemove.addItem(downloadFile);						
						}
						// Delete from XML hierarchy
						this.deleteNode(downloadFile);
					}
					this.dirty.setDirty();
				}
			}
			
			
			
			private function dataTrackFileSelected():Boolean {
				var hasDataTrack:Boolean = false;
				for each(var f:Object in this.filesGrid.selectedItems){		
					if (f.@hasDataTrack == 'Y') {
						hasDataTrack = true;
						break;
					}
				}
				for each(var f1:Object in this.downloadTree.selectedItems){		
					if (f1.@hasDataTrack == 'Y') {
						hasDataTrack = true;
						break;
					}
				}
				return hasDataTrack;
			}
			
			private function checkXMLChildren(downloadFile:XML):void{
				for each(var childFile:XML in downloadFile.children()){
					if(childFile.hasOwnProperty("@fileName") && childFile.@type == "dir"){
						this.filesToRemove.addItem(childFile);
						this.deleteNode(childFile);
						checkXMLChildren(childFile);
					}
					else{
						this.filesToRemove.addItem(childFile);
						this.deleteNode(childFile);
					}
					
				}
				
				
			}
			
			private function onTabChange():void {
				if (isInitialized && theTab.selectedIndex == 1) {
					refreshFiles();
        		}
			}
      
		    private function sendUploadsToDB():void{
		        refreshUploadFiles();
		        var params:Object = new Object();
		        params.idAnalysis = idAnalysis.toString();
		        var fileNode:XML = new XML("<AnalysisFileDescriptor type='dir' displayName='" + this.analysisNumber + "/upload_staging'>" + uploadedFiles.toXMLString() + "</AnalysisFileDescriptor>");
		        params.filesXMLString = fileNode.toXMLString();
		        
		        organizeAnalysisUploadFiles.send(params);
		    }
			
			private function onDragDropFile(event:DragEvent) : void {

				
				// determine where in the grid the drop occurs and select that node by the index; 
				// followed by retrieving the node itself. 
				var dropNode:XML = null;
				var dropFolderNode:XML = null;
				var siblingDropNode:XML = null;
				//var r:int = downloadTree.calculateDropIndex(event);
				//if (r > 0) { 
				//	r--;
				//}
				//downloadTree.selectedIndex = r;
				dropNode = downloadTree.selectedItem as XML;
				
				// Don't allow drag operation if nothing no drop node
				// found
				if (dropNode == null) {
					event.preventDefault();
					event.target.hideDropFeedback(event);
					DragManager.showFeedback(DragManager.NONE);
					return;
				}
				
				dropFolderNode = dropNode;
				if (dropNode.name() == "AnalysisFileDescriptor" && dropNode.@type != "dir" ) {
						dropFolderNode = dropNode.parent();
						siblingDropNode = dropNode;
					
				} 
				
				// Don't allow drag operation if nothing no drop folder node
				// found
				if (dropFolderNode == null) {
					event.preventDefault();
					event.target.hideDropFeedback(event);
					DragManager.showFeedback(DragManager.NONE);
					return;
				}
				
				// get the drag format (its always items in our case
				// Get the dragged items
				var draggedItems:Array = null;		    
				if (event.dragInitiator.name == "downloadTree") {
					// For some reason, if we are dragging within the tree, the dragged
					// items has to be detected on the dragEnter event.  By the time
					// with get to the dragDrop event, the event.draggedItems contains
					// the drop target, not the dragged items.
					draggedItems = this.downloadDraggedItems;	    
				} else {
					var draggedFormat:String = event.dragSource.formats[0];
					draggedItems = event.dragSource.dataForFormat(draggedFormat) as Array;	
				}
				for(var i:int = 0; i < draggedItems.length; i++) {
					var dragItem:XML = XML(draggedItems[i]);
					if (dragItem.name() == "AnalysisFileDescriptor") {
						
						var fileNode:XML = new XML("<AnalysisFileDescriptor " +
							" dirty='Y' " +
							" displayName='" +  GNomExStringUtil.makeXMLSafe(dragItem.@displayName) + "'" + 
							" fileName='" +  GNomExStringUtil.makeXMLSafe(dragItem.@fileName) + "'" + 
							" type='" + dragItem.@type + "'" +
							" number='" + dragItem.@number + "'" +
							" qualifiedFilePath='" + GNomExStringUtil.makeXMLSafe(dragItem.@qualifiedFilePath) + "'" +
							" baseFilePath='" + GNomExStringUtil.makeXMLSafe(dragItem.@baseFilePath) + "'" +
							" idAnalysisFileString='" + GNomExStringUtil.makeXMLSafe(dragItem.@idAnalysisFileString) + "'" +
							" idAnalysis='" + GNomExStringUtil.makeXMLSafe(idAnalysis.toString()) + "'/>");
						
						for each ( var child:Object in dragItem.children() ) {
							fileNode.appendChild(child);	
						}
						
						// Don't allow dropping on yourself (causes a cyclical error)
						if ( fileNode.@fileName == dropFolderNode.@fileName ){
							return;
						}
						
						if (siblingDropNode != null) {
							dropFolderNode.insertChildAfter(siblingDropNode, fileNode);	       				
						} else {
							dropFolderNode.appendChild(fileNode);
						}
						
						// We have to manually remove the file when we are
						// dragging within the download tree.
						if (event.dragInitiator.name == "downloadTree") {
							deleteNode(dragItem);
							downloadTree.selectedItem = null;
						}
						
						this.dirty.setDirty();
					} 
				}
			}
			
			private function deleteNode(xmlToDelete:XML):Boolean {
				var cn:XMLList = XMLList(xmlToDelete.parent()).children();
				for ( var i:Number = 0 ; i < cn.length() ; i++ )
				{
					if ( cn[i] == xmlToDelete ) {
						delete cn[i];       
						return true;
					}
				}    
				return false;
			}
			
			private function doDragEnter(event:DragEvent):void
			{
				// Keep track of what was dragged.
				var draggedFormat:String = event.dragSource.formats[0];
				downloadDraggedItems = event.dragSource.dataForFormat(draggedFormat) as Array;
				
				// Cancel default behaviour
				event.preventDefault();
				// Tell the DragManager that the Tree will accent the DragDrop
				DragManager.acceptDragDrop(Tree(event.target));
				
			}
			
			private function doDragOver(event:DragEvent):void
			{
				// Show the default "drop line" in the Tree control
				//downloadTree.showDropFeedback(event);
				// Cancel default behaviors
				
				var r:int = downloadTree.calculateDropIndex(event);
				if (r > 0) { 
					r--;
        		}
		        downloadTree.selectedIndex = r;
		        var dropNode:XML = downloadTree.selectedItem as XML;
		        var dropFolderNode:XML = dropNode;
		        if (dropNode.name() == "AnalysisFileDescriptor" && dropNode.@type != "dir" ) {
		          dropFolderNode = dropNode.parent();
		        } 
		        if (dropFolderNode != null) {
		          downloadTree.selectedItem = dropFolderNode;
		        }
        
				event.preventDefault();
			}
			
			private function doDragExit(event:DragEvent):void
			{
				// hide the "drop line" that is shown in Tree control
				// when dropping in a Tree
				downloadTree.hideDropFeedback(event);
			}
			
			private function doDragComplete(event:DragEvent):void
			{
				// hide the "drop line" that is shown in Tree control
				// when dropping in a Tree
				downloadTree.hideDropFeedback(event);
			}	
			
			public function showHTMLUploadForm():void {
				var url:URLRequest = new URLRequest('uploadAnalysisFile.jsp?analysisNumber=' + this.analysisNumber);
				navigateToURL(url, '_blank');			
				this.theTab.selectedIndex = 1;
			}
			
			public function startFDTUpload():void {			
				var params:Object = new Object();		
				params.idAnalysis = idAnalysis.toString();
				
				fdtUploadStart.send(params);
			}
			
			private function xClose(event:CloseEvent):void {
		        if (uploadFilesStack.selectedIndex == 1){
		          refreshUploadFiles();
		          sendUploadsToDB();
		        }
						event.detail = idAnalysis.toString();
						PopUpManager.removePopUp(this);
			}
			
			private function renameFile(event:MouseEvent):void{
				if(this.dirty.isDirty()){
					Alert.show("Please save changes before attempting to change the names of any files");
					return;
				}
				if((downloadTree.selectedItem == null) || downloadTree.selectedIndex == 0){
					return;
				}	
				else{
					var renameWindow:RenameAnalysisWindow = RenameAnalysisWindow(mx.managers.PopUpManager.createPopUp(this, RenameAnalysisWindow, true));
				}
				
				renameWindow.oldExperimentName = downloadTree.selectedItem.@displayName;	
				renameWindow.fileList = this.downloadList;
				renameWindow.idAnalysis = this.idAnalysis;
				renameWindow.parentView = this;
				mx.managers.PopUpManager.centerPopUp(renameWindow);
				renameWindow.nameExperiment.setFocus();
			}
			
			public function closeWindow():void{
				PopUpManager.removePopUp(this);
				var e:CloseEvent = new CloseEvent(CloseEvent.CLOSE, true);
				e.detail = idAnalysis.toString();
				var dis:Boolean = dispatchEvent(e);
			}
			
			public function deleteToolTip(obj:Object):void{
				if(filesGrid.toolTip == null){
					downloadTree.toolTip = null;
				}
				else{
					filesGrid.toolTip = null;
				}
			}
		]]>
	</mx:Script>	
	
	<mx:XMLListCollection id="downloadList" source="{getAnalysisDownloadList.lastResult..Analysis}"/> <!--.Analysis on exp-->
	<mx:XMLListCollection id="files"/>
	<mx:XMLListCollection id="uploadedFiles" source="{getAnalysis.lastResult..AnalysisUpload.AnalysisFileDescriptor}"/>
	<mx:XMLListCollection id="filesToRemove"/>
	
	<mx:Canvas width="100%" height="100%">
		<mx:TabNavigator id="theTab" change="onTabChange()" width="100%" paddingTop="0" height="100%" styleName="form">
			<mx:VBox label="Upload files" width="100%" height="100%" verticalGap="2" paddingBottom="4" paddingLeft="4" paddingRight="4" paddingTop="4">
				<mx:ViewStack id="uploadFilesStack" width="100%" height="100%">
					<mx:VBox width="100%" height="100%">
						<mx:HBox height="20" width="100%">						
							<mx:LinkButton id="uploadBrowseButton" label="Choose files" icon="@Embed(source='../../assets/add.png')" disabledIcon="@Embed(source='../../assets/add_disable.png')"  toolTip="Choose file(s) to upload"  textDecoration="underline" enabled="false"/>
							<mx:LinkButton id="uploadDeleteButton" label="Remove" disabledIcon="@Embed(source='../../assets/delete_disable.png')" icon="@Embed(source='../../assets/delete.png')" toolTip="Remove file"  textDecoration="underline" enabled="false"/>
							<mx:LinkButton label="Clear all" toolTip="Clear all files" id="uploadClearButton" icon="@Embed(source='../../assets/cross.png')" disabledIcon="@Embed(source='../../assets/cross_disable.png')" textDecoration="underline" enabled="false"/>
							<mx:HBox width="100%"  horizontalAlign="right">
								<mx:VRule height="20"/>
								<mx:LinkButton visible="{parentApplication.isFDTSupported ? false : true}" label="Upload problems?  Try this." click="showHTMLUploadForm()" textDecoration="underline" icon="@Embed(source='../../assets/application_get.png')"/>
								
								<mx:LinkButton label="Upload and Download Help" 
											   click="parentApplication.showUploadDownloadHints()" 
											   id="buttondownloadhint"  icon="@Embed(source='../../assets/information.png')"/>
							</mx:HBox>			          
						</mx:HBox>
						<mx:DataGrid id="uploadFilesGrid" width="100%" height="100%">				
						</mx:DataGrid>	
					</mx:VBox>
					<mx:VBox width="100%" height="100%" horizontalScrollPolicy="off" verticalScrollPolicy="off">
						<mx:TextArea styleName="instructionBox" width="100%" wordWrap="true" height="100%" id="textarea1" fontSize="12">
							<mx:htmlText><![CDATA[This method of uploading files uses a Fast Data Transfer (FDT) server. To upload files:<br><br>     1. Select the "Start" button below.<br><br>     2. This will download a special "jnlp" file that can be opened using Java Web Start (either directly or after first saving<br>         the file to your desktop).<br><br>     3. Use the window launched by Java Web Start to select and upload the files to the server.<br><br>     4. After all of the files have been uploaded (% Completed column indicates 100 for all files), click the<br>        "Organize files" link on Experiment Detail to move the uploaded files to the appropriate folders.<br>         button.]]></mx:htmlText>
						</mx:TextArea>							
					</mx:VBox>
				</mx:ViewStack>
			</mx:VBox>
			<mx:VBox width="100%" height="100%" label="Organize files"  paddingBottom="4" paddingLeft="4" paddingRight="4" paddingTop="4" verticalGap="2">
				<mx:HBox width="100%" horizontalGap="2" paddingLeft="0" verticalAlign="middle">
					<mx:LinkButton id="newFolderButton" label="New folder"  enabled="false" disabledIcon="@Embed(source='../../assets/folder_add_disable.png')" icon="@Embed(source='../../assets/folder_add.png')" toolTip="Create folder"  textDecoration="underline" click="showAddFolderWindow()" paddingLeft="0"/>
					<mx:LinkButton id="removeFileButton" label="Remove (from file system)"  enabled="{filesGrid.selectedItem != null || (this.downloadTree.selectedItem != null &amp;&amp; this.downloadTree.selectedIndex != 0)}" disabledIcon="@Embed(source='../../assets/delete_disable.png')" icon="@Embed(source='../../assets/delete.png')" toolTip="Remove file"  textDecoration="underline" click="removeAnalysisFile()" paddingLeft="0"/>
					<mx:LinkButton id="renameFileButton" label="Rename file" textDecoration="underline" click="renameFile(event)" enabled="{this.downloadTree.selectedItem != null &amp;&amp; this.downloadTree.selectedIndex != 0 &amp;&amp; this.downloadTree.selectedItems.length == 1}" toolTip="Rename selected file or folder" icon="@Embed(source='../../assets/segment_import.png')"/>
					<mx:LinkButton id="refresh" label="Refresh" icon="@Embed(source='../../assets/refresh.png')" toolTip="Refresh files list"  textDecoration="underline" click="refreshFiles()"/>
					<mx:Spacer width="100%"/>
					<mx:Image source="assets/information.png" 
							  click="{Alert.show('Drag uploaded file into one of the folders on the right.  To drag multiple files, hold CTRL or SHIFT key while selecting.')}"
							  toolTip="Drag uploaded file into one of the folders on the right.  To drag multiple files, hold CTRL or SHIFT key while selecting." />
					
				</mx:HBox>
				
				<mx:HDividedBox width="100%" height="100%">
					<mx:VBox width="300" height="100%" verticalGap="0" styleName="form" >
						<mx:HBox width="100%"  backgroundColor="#C9BEC0" backgroundAlpha="0.22" >
							<mx:Label text="Uploaded Files"/>
						</mx:HBox>
						<mx:DataGrid id="filesGrid"  
									 dragEnabled="true"
									 showHeaders="false"
									 allowMultipleSelection="true"
									 width="100%" height="100%"
									 dataProvider="{uploadedFiles}" dragMoveEnabled="true" itemClick="{this.downloadTree.selectedItem = null; this.renameFileButton.enabled = false;}">
							<mx:columns>
								<mx:DataGridColumn headerText="Uploaded file"  dataField="@displayName"/>
							</mx:columns>
						</mx:DataGrid>
					</mx:VBox>
					<mx:VBox width="50%" height="100%" verticalGap="0" styleName="form">
						<mx:HBox width="100%"  backgroundColor="#C9BEC0" backgroundAlpha="0.22" >
							<mx:Label text="Analysis Folders"/>
						</mx:HBox>
						<mx:Tree id="downloadTree"
								 width="100%" height="100%"
								 iconFunction="parentApplication.getTreeIcon"
								 dataProvider="{downloadList}"
								 borderStyle="solid" 
								 labelFunction="{getDownloadName}"
								 dropEnabled="true"
								 doubleClickEnabled="false"
								 dragEnabled="true"
								 dragMoveEnabled="true" 
								 allowMultipleSelection="true" 
								 dragDrop="onDragDropFile(event)"
								 dragOver="doDragOver(event)"
								 dragEnter="doDragEnter(event)"
								 dragExit="doDragExit(event)"
								 itemClick="{this.filesGrid.selectedItem = null;}">						
						</mx:Tree>	
						<mx:HBox>
							<mx:LinkButton label="Expand Folders" click="{this.expandDownloadTree()}" textDecoration="underline" />
							<mx:LinkButton label="Collapse Folders" click="{this.collapseDownloadTree()}" textDecoration="underline" />
						</mx:HBox>
					</mx:VBox>
				</mx:HDividedBox>
			</mx:VBox>
		</mx:TabNavigator>
	</mx:Canvas>
	<mx:ControlBar width="100%">
		<mx:ViewStack id="buttonStack" width="100%" selectedIndex="{theTab.selectedIndex}">
			<mx:HBox width="100%">
				<mx:ViewStack id="buttonStack_1" width="100%" selectedIndex="{uploadFilesStack.selectedIndex}">
					<mx:HBox height="100%" width="100%">
						<mx:ProgressBar id="uploadProgressBar"  width="100%" labelPlacement="center" trackHeight="15" height="20" fontWeight="normal" themeColor="#EAED9A"/>
						<mx:Button label="Upload files" id="uploadButton"  enabled="false" visible="true" disabledIcon="@Embed(source='../../assets/upload_disable.png')" icon="@Embed(source='../../assets/upload.png')"/>	
					</mx:HBox>				 
					<mx:HBox height="100%" width="100%">
						<mx:Spacer  width="100%" height="10"/>	
						<mx:Label id="saveUUID" text="" visible="false"/>
						<mx:Button id="fdtStartButton" label="Start" click="startFDTUpload();"  enabled="true" visible="true"/>	      	
					</mx:HBox>		  			 
				</mx:ViewStack>
			</mx:HBox>
			<mx:HBox width="100%">
				<mx:Spacer  width="100%" height="10"/>		
				<util:DirtyNote id="dirty"/>
				<mx:Button label="Save" id="saveButton"  enabled="true" click="save()" visible="true" icon="@Embed(source='../../assets/save.png')"/>	      	
				<mx:Button label="Close" enabled="true" click="closeWindow()" visible="true" />	      	
			</mx:HBox>  	
		</mx:ViewStack>
	</mx:ControlBar>
	
</util:ResizableTitleWindow>
