<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml"  
				layout="vertical" title="Send Issue to GNomEx Team"
				width="652" height="430"
				xmlns:util="views.util.*"
				initialize="init()"
				defaultButton="{sendEmailButton}"
				showCloseButton="true"  
				close="{mx.managers.PopUpManager.removePopUp(this)}"
				titleIcon="@Embed(source='./assets/email_go.png')">
	<mx:HTTPService
		id="ReportIssueGetURLService"
		url="ReportIssueServletGetURL.gx"
		resultFormat="e4x"
		showBusyCursor="true"
		result="onReportIssueGetURLService(event)"
		fault="parentApplication.onFailHttpRequest('Unable to get upload URL', event)"
		method="GET"
		useProxy="false"/>	
	
	<mx:HTTPService
		id="ReportIssueService"
		url="ReportIssueServlet.gx"
		resultFormat="e4x"
		showBusyCursor="true"
		result="onReportIssue(event)"
		fault="this.faultHandlerReportIssue(event)"
		method="POST"
		useProxy="false"/>
	
	<mx:Script>
		<![CDATA[
			import UploadPostHelper;
			
			import flash.net.FileReference;
			
			import mx.controls.Alert;
			import mx.managers.PopUpManager;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			
			private var fileRef:FileReference = null;

			public var imageByteArray:ByteArray = null;			
			
			private var reportIssueURL:String = null;
			 
			private function init():void {
				ReportIssueGetURLService.send();				
				fromAddress.text= parentApplication.getUserEmail();
			}
			
			private function onReportIssueGetURLService(event:ResultEvent):void {
				if(ReportIssueGetURLService.lastResult.name() == "ReportIssueServlet") {
					reportIssueURL = ReportIssueGetURLService.lastResult.@url; // this tells it which servlet to call when recieved
				}
			}
			public function onIOError(ioError:IOErrorEvent):void {
				Alert.show(ioError.text + "\r" + ioError.toString(), "IOError");
			}
			
			private function reportIssue():void {		
				var parameters:Object = new Object();
				parameters.fromAddress = fromAddress.text;
				parameters.body = feedbackTextArea.text;
				
				var urlRequest:URLRequest = new URLRequest();
				urlRequest.url = reportIssueURL;
				urlRequest.contentType = 'multipart/form-data; boundary=' + UploadPostHelper.getBoundary();
				urlRequest.method = URLRequestMethod.POST;
				urlRequest.data = UploadPostHelper.getPostData("testfile.png", imageByteArray, parameters);
				urlRequest.requestHeaders.push( new URLRequestHeader( 'Cache-Control', 'no-cache' ));
							
				var urlLoader:URLLoader = new URLLoader();
				urlLoader.dataFormat = URLLoaderDataFormat.BINARY;
				urlLoader.addEventListener(Event.COMPLETE, onReportIssue);
				urlLoader.addEventListener(IOErrorEvent.IO_ERROR, onIOError);
				urlLoader.load(urlRequest);

			}
			
			private function onReportIssue(event:Event):void {			
					Alert.show("Issue has been submitted. Thank you.");
					PopUpManager.removePopUp(this);			
			}			
			
			public function faultHandlerReportIssue(event:FaultEvent):void {
				if (event.message.body is String) {
					var startPos:int = event.message.body.indexOf("H3");
					var endPos:int   = event.message.body.indexOf("</H3>");
					if (startPos != -1 && endPos != -1) {
						Alert.show(event.message.body.toString().substring(startPos + 3, endPos), "Fault Event, Error Status Code: " + event.statusCode);    		
						PopUpManager.removePopUp(this);        		
					} else {
						if(event.message.body == null){
							Alert.show("On web browsers, when a service returns any status code other than 200, Adobe Flash Player cannot read the body of the response. If the status code is 500 and the body contains a fault, there is no way to get to the fault." , "Fault Event, Error Status Code: " + event.statusCode );
						} else {							
							Alert.show(event.message.body.toString(), "Fault Event, Error Status Code: " + event.statusCode);
						}
					}        
					
				} else {
					Alert.show(event.fault.faultCode + "\n\n" + event.fault.faultString + "\n\n" + event.fault.faultDetail, title);
				}
			}
		]]>
	</mx:Script>
	<mx:VBox id="vbox" width="100%" verticalGap="2" backgroundColor="#FFFFFF" borderColor="#000000" >
		<mx:HBox>
			<mx:Label id="textAreaLabel" text="Please enter your feedback in the text box" width="80%"/>
			<mx:Button id="sendEmailButton" label="Submit" width="20%" click="reportIssue()"/>	
		</mx:HBox>		
		<mx:TextArea id="feedbackTextArea" 
					 width="100%" 
					 backgroundColor="#FFFFFF" 
					 editable = "true" 
					 borderColor="#FF0000"
					 text="Feedback" 
					 click="feedbackTextArea.text=='Feedback'?feedbackTextArea.text='':''"
					 />
		<mx:TextArea id="fromAddress"/>
					 
	</mx:VBox>
	
	<mx:HBox>		
		<mx:SWFLoader id="swfLoader">
			<mx:filters>
				<mx:DropShadowFilter />
			</mx:filters>
		</mx:SWFLoader>
	</mx:HBox>
	
</mx:TitleWindow>