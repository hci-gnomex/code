<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml"
	title="{windowTitle}"
	showCloseButton="true" 
	width="440" 
	height="162"
	verticalGap="0"
	close="closeWindow()"
	titleIcon="{parentApplication.iconTopic}"
	layout="vertical" backgroundAlpha="100" defaultButton="{saveButton}">

	<mx:HTTPService             
		id="saveTopic" 
		url="SaveTopic.gx"
		resultFormat="e4x"
		showBusyCursor="true"
		result="onSaveTopic(event)"
		fault="parentApplication.onFailHttpRequest('Failed to update topic', event)"
		method="POST"
		useProxy="false">
		<mx:request>
		</mx:request>
	</mx:HTTPService>	   
       
    <mx:Script>
	<![CDATA[
	
	import mx.controls.Alert;
	import mx.managers.PopUpManager;
	import mx.rpc.events.ResultEvent;
	
	import views.util.StringUtil;

	
	[Bindable]
	public var idParentTopic:Object = null;
	[Bindable]
	public var windowTitle:Object = null;
		
	private var parentView:DisplayObject;
	private var parentTopic:Object;

	public function init(parentTopic:Object, parentView:DisplayObject):void {
		var item:Object;
		
		this.parentView = parentView;
		this.parentTopic = parentTopic;
		
		var isAdmin:Boolean = false;
		if(parentApplication.isSuperAdmin || parentApplication.hasPermission(parentApplication.CAN_ADMINISTER_ALL_CORE_FACILITIES)) {
			isAdmin = true;
		}
			
		// Default owner to current user
		for each (item in ownerCombo.dataProvider) {
			if (item.@idAppUser == parentApplication.idAppUser) {
				ownerCombo.selectedItem = item;
				break;
			}
		}	
		
		if(parentTopic == null) {
			// If top level topic
			this.idParentTopic = null;
			windowTitle = "Add new top level topic";
		} else {
			// If subtopic
			if(isAdmin) {
				// If admin, changed default owner to parent owner
				for each (item in ownerCombo.dataProvider) {
					if (item.@idAppUser == parentTopic.@idAppUser) {
						ownerCombo.selectedItem = item;
						break;
					}
				}
			}				
			
			this.idParentTopic = parentTopic.@idTopic;
			windowTitle = "Add subtopic of " + parentTopic.@label;
			if(parentTopic.hasOwnProperty("@idLab") && parentTopic.@idLab != null && parentTopic.@idLab != "") {
				for each (item in labCombo.dataProvider) {
					if (item.@idLab == parentTopic.@idLab) {
						labCombo.selectedItem = item;
						break;
					}
				}				
			}
		}
	}

   	private function onSaveTopic(event:ResultEvent):void {
		if (saveTopic.lastResult.name() == "SUCCESS") {			
			closeWindow();
			var itemToSelect:XML = new XML(
			   "<Topic " +
			     " idLab='" + (labCombo.selectedItem != null ? labCombo.selectedItem.@idLab : '') + "'" +
			     " idTopic='" + saveTopic.lastResult.@idTopic + "'" +
			    "/>");
			if (parentView is NavTopicsView) {
				NavTopicsView(parentView).refreshTopicsAndSelect(itemToSelect);
			}
		} else {
			Alert.show(saveTopic.lastResult..ACTMESSAGE.@TEXT);
		}	
	}

	private function save():void {
		if (labCombo.selectedItem == null || labCombo.selectedIndex == 0) {
			labCombo.setFocus();
			Alert.show("Please select a Lab.");
			return;        		
		} 
		if (ownerCombo.selectedItem == null || ownerCombo.selectedIndex == 0) {
			ownerCombo.setFocus();
			Alert.show("Please select select the topic owner.");
			return;        		
		} 
		var selectedIdLab:String = labCombo.selectedItem != null ? labCombo.selectedItem.@idLab : "";
		if(parentTopic != null && parentTopic.@idLab != selectedIdLab) {
			ownerCombo.setFocus();
			Alert.show("Lab for child topic must be the same as its parent (" + parentTopic.@lab + ")");
			return;  			
		}
		var params:Object = new Object();
		params.idParentTopic      	  = idParentTopic;
		params.name                   = this.topicName.text;
		params.description            = "";
		params.idLab                  = selectedIdLab;
		params.idAppUser              = ownerCombo.selectedItem != null ? ownerCombo.selectedItem.@idAppUser : "";
		
		saveTopic.send(params);
	}   
		
    private function closeWindow():void {
		PopUpManager.removePopUp(this);		
	}

	]]>
    </mx:Script>
    <mx:VBox width="100%" height="100%" styleName="form" verticalGap="10">
		<mx:HBox width="100%" >
			<mx:Label text="Name:" width="105"/>
			<mx:TextInput id="topicName" width="308" maxChars="2000"/>
		</mx:HBox>		
		<mx:HBox width="100%" >
			<mx:Label text="Lab:" width="105"/>
			<mx:ComboBox id="labCombo" dataProvider="{parentApplication.promptedLabList}" width="308" labelField="@name"></mx:ComboBox>
		</mx:HBox>		
		<mx:HBox width="100%" >
			<mx:Label text="Owner:" width="105"/>
			<mx:ComboBox width="308" id="ownerCombo" labelField="@displayName" dataProvider="{parentApplication.promptedAppUserList}"></mx:ComboBox>							         			
		</mx:HBox>		

    </mx:VBox>

	<mx:ControlBar horizontalAlign="right" height="29">
		<mx:Button id="saveButton"  
			enabled="{(topicName.text != null &amp;&amp; topicName.text.length > 0) ? true : false}" 
			icon="{parentApplication.iconSave}" 
			disabledIcon="{parentApplication.iconSaveDisabled}" 
			label="Save"
			click="{save();}"/>
		<mx:Button id="cancelButton" label="Cancel" click="closeWindow()"/>
	</mx:ControlBar>
</mx:TitleWindow>
