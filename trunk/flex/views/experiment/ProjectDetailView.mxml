<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" >

<mx:HTTPService  
    id="saveProject" 
    url="SaveProject.gx"
    destination="saveProject"
    resultFormat="e4x"
    showBusyCursor="true"
    result="onSaveProject(event)"
	fault="parentApplication.onFailHttpRequest('Failed to save project', event)"
    method="POST"
    useProxy="false">
</mx:HTTPService>

<mx:HTTPService  
    id="deleteProject" 
    url="DeleteProject.gx"
    destination="deleteProject"
    resultFormat="e4x"
    showBusyCursor="true"
    result="onDeleteProject(event)"
 	fault="parentApplication.onFailHttpRequest('Failed to delete project', event)"
    method="POST"
    useProxy="false">
</mx:HTTPService>
  
<mx:Script>
	<![CDATA[
		import mx.events.CollectionEvent;
		import mx.events.CollectionEventKind;
		import mx.rpc.events.FaultEvent;
		import mx.rpc.events.ResultEvent;
		import mx.controls.Alert;
		import mx.events.CloseEvent;
		import mx.events.ListEvent;
		import hci.flex.components.DictionaryEditor;
		import hci.flex.events.DictionaryEvent;
		import mx.managers.PopUpManager;
		
		private var closeWindowAfterSave:Boolean = false;
		public var fromSubmitRequestWindow:Boolean = false;
		private var dictionaryEditor:DictionaryEditor;
		
		
		public function checkSecurity():void {
			if (parentApplication.navExperimentView.selectedProject == null) {
				return;
			}
			
			
			if (parentApplication.navExperimentView.selectedProject.@canUpdate != "Y") {
				projectName.editable = false;
				projectName.enabled = false;
				projectDescription.editable = false;
				projectDescription.enabled = false;
			} else {
				projectName.editable = true;
				projectName.enabled = true;
				projectDescription.editable = true;
				projectDescription.enabled = true;
			}
			if (parentApplication.navExperimentView.selectedProject.@canUpdate == "Y") {
				parentDocument.saveProjectButton.enabled = true;
				editExperimentDesignButton.enabled = true;
				editExperimentFactorButton.enabled = true;
			} else{
				parentDocument.saveProjectButton.enabled = false;
				editExperimentDesignButton.enabled = false;
				editExperimentFactorButton.enabled = false;
			}
			
			if (parentApplication.navExperimentView.selectedProject.@canDelete == "Y") {
				parentApplication.navExperimentView.deleteProjectButton.enabled = true;
			} else{
				parentApplication.navExperimentView.deleteProjectButton.enabled = false;
			}
		}
		
		public function setupDataListeners():void {
			experimentDesignEntries.addEventListener(CollectionEvent.COLLECTION_CHANGE, underlyingExperimentDesignDataChange);
			experimentFactorEntries.addEventListener( CollectionEvent.COLLECTION_CHANGE, underlyingExperimentFactorDataChange);			
		}
		
		private function underlyingExperimentDesignDataChange(event:CollectionEvent):void {
			if (event.kind == CollectionEventKind.RESET || event.kind == CollectionEventKind.REFRESH) {
				// ignore refresh and resets 
				return;
			}
			setDirty();
		}

		
		private function underlyingExperimentFactorDataChange(event:CollectionEvent):void {
			if (event.kind == CollectionEventKind.RESET || event.kind == CollectionEventKind.REFRESH) {
				// ignore refresh and resets 
				return;
			}
			setDirty();
		}
		
		public function addProject():void {
			closeWindowAfterSave = true;
			parentApplication.navExperimentView.selectedProject.@idLab = parentDocument.labCombo.selectedItem.@idLab;
			save();
		}
		
		public function save():void {
			parentApplication.navExperimentView.selectedProject.@name = projectName.text;
			parentApplication.navExperimentView.selectedProject.@description = projectDescription.text;
			
			var params:Object = new Object();
			params.projectXMLString = parentApplication.navExperimentView.selectedProject.toXMLString();
			params.parseEntries = 'Y';
			saveProject.send(params);
		}
		
		public function promptToDeleteProject():void {
			Alert.show("Delete project " + parentApplication.navExperimentView.selectedProject.@name + "?", 
						null, 
						(Alert.YES | Alert.NO), this, 
						onPromptToDeleteProject);
			
		}
		
		private function onPromptToDeleteProject(event:CloseEvent):void {
            if (event.detail==Alert.YES) {
            	removeProject();
            }
        }

		
		private function removeProject():void {
			var params:Object = new Object();
			params.idProject = parentApplication.navExperimentView.selectedProject.@idProject;
			deleteProject.send(params);
			
		} 
		
		private function setDirty():void {
			if (parentDocument is ProjectDetailWindow) {
				parentDocument.dirtyProject.setDirty();
			} else {
				parentApplication.navExperimentView.browseView.dirtyProject.setDirty();
			}
		}
		
		private function resetDirty():void {
			if (parentDocument is ProjectDetailWindow) {
				parentDocument.dirtyProject.resetDirty();
			} else {
				parentApplication.navExperimentView.browseView.dirtyProject.resetDirty();
			}
		}
		
		private function isDirty():Boolean {
			if (parentDocument is ProjectDetailWindow) {
				return parentDocument.dirtyProject.isDirty();
			} else {
				return parentApplication.navExperimentView.browseView.dirtyProject.isDirty();
			}
		}
		
		private function onSaveProject(event:ResultEvent):void {
        	if (saveProject.lastResult.name() == "SUCCESS") {
        		resetDirty();
				parentApplication.navExperimentView.selectedIdProject = saveProject.lastResult.@idProject;
				if (fromSubmitRequestWindow) {
					parentApplication.submitRequestView.setupView.onProjectAdded(saveProject.lastResult.@idProject);
				}else {
					parentApplication.navExperimentView.getProject.send();
					parentApplication.navExperimentView.refreshExperiments();
				}
        		
        		if (closeWindowAfterSave) {
        			parentDocument.closeWindow();
        			closeWindowAfterSave = false;
        		}
        	} else {
        		Alert.show(saveProject.lastResult..ACTMESSAGE.@TEXT);
        	}
			
		}
		
		private function onDeleteProject(event:ResultEvent):void {
        	if (deleteProject.lastResult.name() == "SUCCESS") {
        		parentApplication.navExperimentView.selectedProject = null;
        		parentApplication.navExperimentView.selectedIdProject = null;
        		parentApplication.navExperimentView.refreshExperiments();
        		parentApplication.navExperimentView.theViews.visible = false;
        		parentApplication.navExperimentView.browseView.buttonStack.selectedChild = parentDocument.browseButtonView;
        	} else {
        		Alert.show(deleteProject.lastResult..ACTMESSAGE.@TEXT);
        	}
		}
		public function sortExperimentDesigns(obj1:Object, obj2:Object, fields:Array=null):int {
			if (obj1 == null && obj2 == null) {
				return 0;
			} else if (obj1 == null) {
				return 1;
			} else if (obj2 == null) {
				return -1;
			} else {
				var ed1:String = obj1.@experimentDesign;
				var ed2:String = obj2.@experimentDesign;
				
				if (ed1 == 'Other') {
					return 1;
				} else if (ed2 == 'Other') {
					return  -1;
				} else {
					if (ed1 < ed2) {
						return -1;
					} else if (ed1 > ed2) {
						return 1;
					} else {
						return 0;
					}
				}			
				
			}
		}
		public function sortExperimentFactors(obj1:Object, obj2:Object, fields:Array=null):int {
			if (obj1 == null && obj2 == null) {
				return 0;
			} else if (obj1 == null) {
				return 1;
			} else if (obj2 == null) {
				return -1;
			} else {
				var ef1:String = obj1.@experimentFactor;
				var ef2:String = obj2.@experimentFactor;
				if (ef1 == 'Other') {
					return 1;
				} else if (ef2 == 'Other') {
					return  -1;
				} else  {
					if (ef1 < ef2) {
						return -1;
					} else if (ef1 > ef2) {
						return 1;
					} else {
						return 0;
					}
				}				
				
			}
		}
		
		private function toggleExperimentDesignCheck(event:ListEvent):void {
   			if (event.columnIndex == 0) {
	   			return;
   			}
   			if (experimentDesignEntryGrid.selectedItem.@isSelected == "true") {
				experimentDesignEntryGrid.selectedItem.@isSelected = "false";
			} else {
				experimentDesignEntryGrid.selectedItem.@isSelected = "true";
			}
		}
		private function toggleExperimentFactorCheck(event:ListEvent):void {
   			if (event.columnIndex == 0) {
	   			return;
   			}
   			if (experimentFactorEntryGrid.selectedItem.@isSelected == "true") {
				experimentFactorEntryGrid.selectedItem.@isSelected = "false";
			} else {
				experimentFactorEntryGrid.selectedItem.@isSelected = "true";
			}
		}
		
		//
        // Edit experiment design dictionary
        //
		private function editExperimentDesign():void {
			// Don't allow edit dictionary if window is dirty.  Warn user that project 
			// should be saved
			if (isDirty()) {
				Alert.show("Please save your project changes before proceeding to edit Experiment Designs.", "Unsaved changes");
				return;
			}

			dictionaryEditor = DictionaryEditor(PopUpManager.createPopUp(parentApplication.navExperimentView, DictionaryEditor, true));
			dictionaryEditor.addEventListener(DictionaryEvent.DICTIONARY_WINDOW_INITIALIZED,   
			                          onDictionaryExperimentDesignInitialized);
			parentApplication.addEventListener(DictionaryEvent.DICTIONARY_LOADED,   
			                                   onDictionaryExperimentDesignRefreshed);
			PopUpManager.centerPopUp(dictionaryEditor);
		}
       	private function onDictionaryExperimentDesignInitialized(event:Event):void{
			dictionaryEditor.selectDictionary("hci.gnomex.model.ExperimentDesign");
        }          
       	private function onDictionaryExperimentDesignRefreshed(event:Event):void{
			parentApplication.navExperimentView.getProject.send();
        }	

		
		//
        // Edit experiment factor dictionary
        //
		private function editExperimentFactor():void {
			if (isDirty()) {
				Alert.show("Please save your project changes before proceeding to edit Experimental Factors.", "Unsaved changes");
				return;
			}
			dictionaryEditor = DictionaryEditor(PopUpManager.createPopUp(parentApplication.navExperimentView, DictionaryEditor, true));
			dictionaryEditor.addEventListener(DictionaryEvent.DICTIONARY_WINDOW_INITIALIZED,   
			                          onDictionaryExperimentFactorInitialized);
			parentApplication.addEventListener(DictionaryEvent.DICTIONARY_LOADED,   
			                                   onDictionaryExperimentFactorRefreshed);
			PopUpManager.centerPopUp(dictionaryEditor);
		}
       	private function onDictionaryExperimentFactorInitialized(event:Event):void{
			dictionaryEditor.selectDictionary("hci.gnomex.model.ExperimentFactor");
        }          
       	private function onDictionaryExperimentFactorRefreshed(event:Event):void{
			parentApplication.navExperimentView.getProject.send();
        }	
		

	]]>
</mx:Script>

	  
	  <mx:XMLListCollection id="experimentDesignEntries" source="{parentApplication.navExperimentView.selectedProject.ExperimentDesignEntries.ExperimentDesignEntry}" 
	  	 sort="{experimentDesignSorter}"/>
	  <mx:Sort id="experimentDesignSorter" compareFunction="sortExperimentDesigns"
	  unique="true"/>
	  
	  <mx:XMLListCollection id="experimentFactorEntries" source="{parentApplication.navExperimentView.selectedProject.ExperimentFactorEntries.ExperimentFactorEntry}" 
	  	 sort="{experimentFactorSorter}"/>
	  <mx:Sort id="experimentFactorSorter" compareFunction="sortExperimentFactors"
	  unique="true"/>
 

	<mx:HDividedBox width="100%" height="100%" horizontalAlign="left" paddingTop="4" paddingRight="4" paddingLeft="4" paddingBottom="4">
		<mx:VBox width="33%" height="100%" verticalGap="10"  >
		    <mx:VBox width="100%" verticalGap="0">
				<mx:Label text="Project name:"/>
				<mx:TextInput id="projectName" width="100%"  
					text="{parentApplication.navExperimentView.selectedProject.@name}">
					<mx:change>setDirty()</mx:change>
					<mx:toolTip>(Suggested naming convention: Lab-Species-Experiment title)</mx:toolTip>
				</mx:TextInput>			    	
		    </mx:VBox>
			
			<mx:VBox width="100%" verticalGap="0" height="100%">
				<mx:Label text="Project description:"/>
				<mx:TextArea height="100%" id="projectDescription" width="100%" 
					text="{parentApplication.navExperimentView.selectedProject.@description}">
					<mx:change>setDirty()</mx:change>
				</mx:TextArea>			    	
		    </mx:VBox>
		</mx:VBox>

		<mx:VBox width="30%" height="100%" verticalGap="0">
	      <mx:DataGrid id="experimentDesignEntryGrid" width="100%" height="100%" 
	    	dataProvider="{experimentDesignEntries}" 
	    	selectionColor="#FFFFFF"
 			showHeaders="true" headerHeight="20" variableRowHeight="true" rowHeight="20" itemClick="toggleExperimentDesignCheck(event)">
	        <mx:columns>
	            <mx:DataGridColumn headerText=" " 
	            	width="17" 
	            	editable="true"                        	
	            	dataField="@isSelected" 
	            	editorDataField="selected"
	            	itemRenderer="hci.flex.renderers.CheckBox" >
	            </mx:DataGridColumn>
	            <mx:DataGridColumn  
	            	headerText="Experiment Design (general intent)"
	            	editable="false" editorDataField="selected" 
	            	dataField="@experimentDesign">
	                <mx:itemRenderer>
	                    <mx:Component >
	                         <mx:HBox>
	                           <mx:Label text="{data.@experimentDesign}">			                           	
	                           </mx:Label>
	                           <mx:TextInput id="edOtherLabel" fontSize="9"
	                           	text="{data.@otherLabel}"
	                           	change="{data.@otherLabel = edOtherLabel.text}" 
	                           	width="{data.@experimentDesign == 'Other' ? 130 : 0}"  
	                           	height="{data.@experimentDesign == 'Other' ? 20 : 0}"  
	                           	visible="{data.@experimentDesign == 'Other' ? true : false}" 
	                           	editable="{data.@experimentDesign == 'Other' ? true : false}">			                           
	                           </mx:TextInput>
	                         	
	                         </mx:HBox>
	                    </mx:Component>
	                </mx:itemRenderer>
                </mx:DataGridColumn>
            </mx:columns>
  	      </mx:DataGrid>
		  <mx:LinkButton label="New/Edit..." textDecoration="underline" styleName="blueLinkButton" click="editExperimentDesign()" id="editExperimentDesignButton">		  	
		  </mx:LinkButton>			
		</mx:VBox>

		<mx:VBox width="30%" height="100%" verticalGap="0">
	      <mx:DataGrid id="experimentFactorEntryGrid" width="100%" height="100%" dataProvider="{experimentFactorEntries}" selectionColor="#FFFFFF" showHeaders="true" headerHeight="20" variableRowHeight="true" rowHeight="20" itemClick="toggleExperimentFactorCheck(event)">
	        <mx:columns>
	            <mx:DataGridColumn headerText=" " 
	            	width="17" 
	            	editable="true"                        	
	            	dataField="@isSelected" 
	            	editorDataField="selected" 
	            	itemRenderer="hci.flex.renderers.CheckBox">
	            </mx:DataGridColumn>
	            <mx:DataGridColumn  
	            	headerText="Factors (differences between samples)" 
	            	editable="false" editorDataField="selected" 
	            	dataField="@experimentFactor">
	                <mx:itemRenderer>
	                    <mx:Component >
	                         <mx:HBox horizontalGap="1">
	                           <mx:Label text="{data.@experimentFactor}">			                           	
	                           </mx:Label>
	                           <mx:TextInput id="efOtherLabel" fontSize="9"
	                           	text="{data.@otherLabel}"
	                           	change="{data.@otherLabel = efOtherLabel.text}" 
	                           	width="{data.@experimentFactor == 'Other' ? 130 : 0}"  
	                           	height="{data.@experimentFactor == 'Other' ? 20 : 0}"  
	                           	visible="{data.@experimentFactor == 'Other' ? true : false}" 
	                           	editable="{data.@experimentFactor == 'Other' ? true : false}">			                           
	                           </mx:TextInput>
	                         	
	                         </mx:HBox>
	                    </mx:Component>
	                </mx:itemRenderer>
                </mx:DataGridColumn>
	        </mx:columns>
  	      </mx:DataGrid>
		  <mx:LinkButton label="New/Edit..." textDecoration="underline" styleName="blueLinkButton" click="editExperimentFactor()" id="editExperimentFactorButton">		  	
		  </mx:LinkButton>			
		</mx:VBox>


       
   </mx:HDividedBox>
   

	
</mx:Canvas>
