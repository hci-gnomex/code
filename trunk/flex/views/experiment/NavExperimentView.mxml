<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml"     
	label="Experiments" width="100%" height="100%" 
	xmlns:views="views.*" xmlns:experiment="views.experiment.*"
	creationPolicy="all"  show="init()"
	cornerRadius="10"  >
     

  <mx:HTTPService  
    id="getProjectRequestList" 
    url="GetProjectRequestList.gx"
    destination="getProjectRequestList"
    resultFormat="e4x"
    showBusyCursor="true"
    result="onGetProjectRequestList(event)"
 	fault="parentApplication.onFailHttpRequest('Failed to get project request list', event)"
    method="POST" 
    useProxy="false">
    <mx:request>
    </mx:request>
   </mx:HTTPService>
   
   <mx:HTTPService  
    id="getRequest" 
    url="GetRequest.gx"
    destination="getRequest"
    resultFormat="e4x"
    showBusyCursor="false"
    result="onGetRequest(event)"
 	fault="parentApplication.onFailHttpRequest('Failed to get request', event)"
    method="POST"
    useProxy="false">
  </mx:HTTPService>

   <mx:HTTPService  
    id="getRequestFromNav" 
    url="GetRequest.gx"
    destination="getRequest"
    resultFormat="e4x"
    showBusyCursor="false"
    result="onGetRequestFromNav(event)"
 	fault="parentApplication.onFailHttpRequest('Failed to get request', event)"
    method="POST"
    useProxy="false">
  </mx:HTTPService>
   
   <mx:HTTPService  
    id="getRequestForDownload" 
    url="GetRequest.gx"
    destination="getRequestForDownload"
    resultFormat="e4x"
    showBusyCursor="true"
    result="onGetRequestForDownload(event)"
 	fault="parentApplication.onFailHttpRequest('Failed to get request for download', event)"
    method="POST"
    useProxy="false">
  </mx:HTTPService>
   
   <mx:HTTPService  
    id="deleteRequest" 
    url="DeleteRequest.gx"
    destination="deleteRequest"
    resultFormat="e4x"
    showBusyCursor="true"
    result="onDeleteRequest(event)"
 	fault="parentApplication.onFailHttpRequest('Failed to delete request', event)"
    method="POST"
    useProxy="false">
  </mx:HTTPService>

   
   <mx:HTTPService  
    id="getProject" 
    url="GetProject.gx"
    destination="getProject"
    resultFormat="e4x"
    showBusyCursor="true"
    result="onGetProject(event)"
    method="POST"
 	fault="parentApplication.onFailHttpRequest('Failed to get project', event)"
    useProxy="false">
    <mx:request>
      <idProject>{selectedIdProject}</idProject>
    </mx:request>
  </mx:HTTPService>
  
  <mx:HTTPService  
    id="saveRequestProject" 
    url="SaveRequestProject.gx"
    destination="saveRequestProject"
    resultFormat="e4x"
    showBusyCursor="true"
    result="onSaveRequestProject(event)"
 	fault="parentApplication.onFailHttpRequest('Failed to reassign experiment project', event)"
    method="POST"
    useProxy="false">    
  </mx:HTTPService>

   <mx:HTTPService  
    id="getLab" 
    url="GetLab.gx" 
    destination="getLab" 
    resultFormat="e4x"
    showBusyCursor="true"
    result="onGetLab(event)"
    fault="parentApplication.onFailHttpRequest('Failed to get group', event)"
    method="POST"
    useProxy="false">
    <mx:request>
    	<idLab>{this.browseLabCombo.selectedItem.@idLab}</idLab>
    </mx:request>
   </mx:HTTPService>

 
	   <mx:Script> 
        <![CDATA[
        import mx.controls.Alert;
        import mx.managers.DragManager;
        import mx.core.DragSource;
        import mx.events.DragEvent;
        import mx.events.CloseEvent;
        import mx.controls.Menu;
        import mx.collections.ArrayCollection;

        import mx.rpc.events.ResultEvent;   
        import mx.collections.XMLListCollection;
        
        public var selectedIdRequest:Object;
        public var selectedRequestNumber:Object;
        
        [Bindable]

		public var showEditView:Boolean = false;

        public var selectedExperiment:Object;
        
        [Bindable]
        public var selectedProject:Object;
        
        public var selectedIdLab:String;
        
        public var selectedNode:Object;
        
       [Bindable]
        private var appUsers:XMLListCollection;
        private var appUserSort:Sort;

        private var dropProject:Object;
        private var dragRequest:Object;
        
        [Bindable]
        public var selectedIdProject:String;
        
        [Bindable]
    	public var rowCountMessage:String = new String();
    	

    	private var showDownloads:Boolean = false;
    

	 	public static const millisecondsPerDay:int = 1000 * 60 * 60 * 24;
	 	public static const millisecondsPerWeek:Number = 1000 * 60 * 60 * 24 * 7;
	 	public static const millisecondsPerMonth:Number = 1000 * 60 * 60 * 24 * 30;
	 	public static const millisecondsPerYear:Number = 1000 * 60 * 60 * 24 * 365;

		public function checkDirty():void {
		}
		
		private function init():void {
			checkSecurity();
			
			// Only initialize experiments if the user clicked on
			// Experiments link in top nav bar. (Bypass
			// if looking up experiment based on its number.)
			if (selectedRequestNumber == null) {
            	initializeExperiments();
   			} 
		}
		
		public function checkSecurity():void {
			this.browseView.checkSecurity();
			if (parentApplication.hasPermission("canAccessAnyObject")) {
				currentState = "AdminState";
			} else if (parentApplication.isGuestMode()) {
				currentState = "GuestState"; 
			} else {
				currentState = "";
			}
		}

        private function getBrowseTreeLabel(item:Object):String {
        	var label:String = item.@label;
        	
        	if (item.name() == "Request") {
        		label = item.@requestNumber;
        	}
        	return label;
        }
        
       
        
        private function onGetRequestDownloadList(event:ResultEvent): void {        	
        	setResultsCount();
        }
        
        private function onGetProjectRequestList(event:ResultEvent): void {
        	if (getProjectRequestList.lastResult.name() != "ProjectRequestList") {
        		Alert.show(getProjectRequestList.lastResult..ACTMESSAGE.@TEXT);
        		browseTree.visible = false;
        		this.treeExpansionStack.visible = false;
        		return;
        	}
        	  
        	if (getProjectRequestList.lastResult.Lab.length() == 0) {
        		browseTree.visible = false;
        		this.treeExpansionStack.visible = false;
        	} else {
        		browseTree.visible = true;
        		this.treeExpansionStack.visible = true;
        	}
        	
        	var call:Object = event.token;
        	if (call.marker == "showDownloads") {
        		showDownloads = true;
        	} else {
        		showDownloads = false;
        	}

        	
        	
        	callLater(expandAndReselectTree);    	
        	
	    }

		private function expandAndReselectTree():void {
			
        	expandAllNodes();
			this.treeExpansionStack.selectedIndex = 1;        		
        	
        	callLater(reselectTreeItem);
		}
	    
	    
	    private function reselectTreeItem():void {
			selectTreeNode();
	    	
	    }
	    
	    //private function selectTreeNode():void {
	    //	browseTree.selectedItem = selectedNode;
	    //	onBrowseTree();
	    //}
	    private function selectTreeNode():void {
	    	var idx:int = -1; 
    		var pos:int = 0;
    		var lab:Object;
    		var project:Object;
    		var request:Object; 
    		// If actionscript sets selectedItem (rather than selectedIndex) on tree,
    		// there is a flex bug that causes an extra node to be inserted under
    		// selected item.  To work around this problem, I'm iterating through
    		// open items to determine the selected index.  This is clumsy code,
    		// but it woirks!
    		if (selectedRequestNumber != null) {
		    	for each (lab in getProjectRequestList.lastResult..Lab) {
		    		pos++;	    	
		    		for each(project in lab.Project) {
		    			pos++;
		    			for each( request in project.Request) {
		    				pos++;
		    				if (request.@requestNumber == String(selectedRequestNumber).toUpperCase()) {
		    					idx = --pos;
		    					
		    					// We came from the main gnomex app, so select the lab from the lab combo
		    					if (this.currentState == "AdminState") {
		    						for each(var l:Object in  parentApplication.promptedLabList) {
		    							if (l.@idLab == request.@idLab) {
		    								this.browseLabCombo.selectedItem = l;
		    								break;	
		    							}
		    						}
		    					}
		    					break;
		    				} 
		    			}
		    			if (idx != -1) {
		    				break;
		    			} 
		    		}
		    		if (idx != -1) {
		    			break;
		    		}
    			}    			
    		} else if (selectedIdProject != null) {
		    	for each (lab in getProjectRequestList.lastResult..Lab) {	   
		    		pos++; 	
		    		for each(project in lab.Project) {
		    			pos++;
		    			if (project.@idProject == selectedIdProject) {
		    				idx = --pos;
		    				break;
		    			}
		    			for each( request in project.Request) {
		    				pos++;
		    			}
			    		if (idx != -1) {
			    			break;
			    		}
		    		}
		    		if (idx != -1) {
		    			break;
		    		}
    			}    			
    		} else if (selectedIdRequest != null) {
		    	for each (lab in getProjectRequestList.lastResult..Lab) {
		    		pos++;	    	
		    		for each(project in lab.Project) {
		    			pos++;
		    			for each( request in project.Request) {
		    				pos++;
		    				if (request.@idRequest == selectedIdRequest) {
		    					idx = --pos;
		    					break;
		    				} 
		    			}
		    			if (idx != -1) {
		    				break;
		    			} 
		    		}
		    		if (idx != -1) {
		    			break;
		    		}
    			}    			
    		} else if (selectedIdLab != null) {
    			for each (lab in getProjectRequestList.lastResult..Lab) {	    	
		    		pos++;
    				if (selectedIdLab == lab.@idLab) {
    					idx = --pos;
    					break;
    				}
		    		for each(project in lab.Project) {
		    			pos++;
		    			for each(request in project.Request) {
		    				pos++;
		    			}
			    		if (idx != -1) {
			    			break;
			    		}
		    		}
		    		if (idx != -1) {
		    			break;
		    		}
    			}    			
    		}
	    	
	    	if (idx != -1) {
	    		// Select the tree item
	    		browseTree.selectedIndex = idx;
	    	} else {
	    		// Otherwise, just select first lab
	    		browseTree.selectedIndex = 0;
	    	}
			onBrowseTree();
	    	
	    }	    
	    
        
        
	    private function expandLabNodes():void
  		{
  			browseTree.expandItem(getProjectRequestList.lastResult, true);
  			for each(var lab:Object in getProjectRequestList.lastResult..Lab) {
  				browseTree.expandItem(lab, true);
  			}
  			browseTree.validateNow();
  			treeContainer.validateNow();
  		}
  		
  		private function collapseProjectNodes():void {
  			for each(var project:Object in getProjectRequestList.lastResult..Project) {
  				browseTree.expandItem(project, false);
  			}
  			browseTree.validateNow();
  			treeContainer.validateNow();
  			
  		}

	    public function expandAllNodes():void
  		{
  			browseTree.expandItem(getProjectRequestList.lastResult, true);
  			for each(var lab:Object in getProjectRequestList.lastResult..Lab) {
  				browseTree.expandItem(lab, true);
  			}
  			for each(var project:Object in getProjectRequestList.lastResult..Project) {
  				browseTree.expandItem(project, true);
  			}
  			browseTree.validateNow();
  			treeContainer.validateNow();
  		}
  		
  		
  		private function collapseAllBrowseNodes():void {
  			if (browseTree == null || browseTree.openItems == null) {
  				return;
  			}
  			
  			browseTree.openItems = [];
  			browseTree.validateNow();
  		}
  		
	    public function setResultsCount():void {
	    	if (browseTree.selectedItem == null) {
	    		rowCountMessage = "";
	    	} else if (browseTree.selectedItem..Request.length() == 1) {
		    	rowCountMessage = "(" + browseTree.selectedItem..Request.length() + " experiment)";
	    	} else {
			   	rowCountMessage = "(" + browseTree.selectedItem..Request.length() + " experiments)";
	    	}
	    }
    

		private function onBrowseTree():void {
			setResultsCount();
			
			browseView.dirtyProject.resetDirty();
			browseView.dirtyVisibility.resetDirty();

			selectedProject = null;
			selectedIdProject = null;
			selectedIdLab = null;
			
			deleteProjectButton.enabled = false;
			newProjectButton.enabled = false;
			
			if (browseTree.selectedItem == null) {
				return; 
			} 
			
			if (browseTree.selectedItem.name() == 'Request') {
				detailView.title = "Experiment " + browseTree.selectedItem.@requestNumber;
				editView.title = "Edit Experiment " + browseTree.selectedItem.@requestNumber;
				selectedIdRequest = browseTree.selectedItem.@idRequest;
				var parms:Object = new Object();
				parms.idRequest = selectedIdRequest;
				getRequest.send(parms);
				selectedIdLab = null;
				selectedIdProject = null;
				selectedRequestNumber = null;
				
			} else {												
				selectedIdLab = browseTree.selectedItem.@idLab.toString();
				
				if (browseTree.selectedItem.name() == 'Project') {
					browseView.title = browseTree.selectedItem.@projectName + "\t" + rowCountMessage;
					selectedIdProject = browseTree.selectedItem.@idProject.toString();
					if (!browseView.theViews.contains(browseView.projectDetailView)) {
						browseView.theViews.addChild(browseView.projectDetailView);
					}
					getProject.send();
					
				} else {
					browseView.title =  browseTree.selectedItem.@label + "\t" + rowCountMessage;;
					selectedIdProject = null;
					if (browseView.theViews.contains(browseView.projectDetailView)) {
						browseView.theViews.removeChild(browseView.projectDetailView);
					}
					browseView.buttonStack.selectedChild = browseView.browseButtonView;
					browseView.refreshButtonStack();
					
					browseView.refreshDownloadList();
					browseView.refreshTrackList();
					browseView.refreshVisibilityList();

					
				}
				
				theViews.selectedIndex=0;
			} 
			
			browseView.requestNumberCombo.selectedIndex = -1;
			browseView.browseList.refresh();
			refreshRequestNumbers(browseView.browseList);
			browseView.refreshVisibilityList();
			theViews.visible = true;
			
			
		}
		
		public function showExperimentDownloads():void {
			
			selectedIdProject = browseTree.selectedItem.parent().@idProject;
			selectTreeNode();
			
			for each (var item:Object in requestNumbers) {
				if (item.data == selectedExperiment.@number) {
					browseView.requestNumberCombo.selectedItem = item;
					break;
				}
			}
			
			
			theViews.selectedChild = browseView;
			browseView.theViews.selectedChild = browseView.downloadView;
			browseView.downloadView.theTab.selectedChild = browseView.downloadView.folderView;
			browseView.refreshButtonStack();
			

		}
		
		public function refreshRequestNumbers(experimentList:XMLListCollection):void {
	    	requestNumbers.removeAll();
	    	browseView.requestNumberCombo.selectedIndex = -1;
	
			var blankItem:Object = new Object();
			blankItem.data = "";
			blankItem.label = "";
	    	requestNumbers.addItem(blankItem);
	    	
	    	for(var x:Number = 0; x < experimentList.length; x++) {
	    		var item:Object = experimentList.getItemAt(x, 0);
	    		
	    		var found:Boolean = false;
	    		for(var i:Number = 0; i < requestNumbers.length; i++) {
	    			if (requestNumbers.getItemAt(i).label == item.@requestNumber) {
	    				found = true;
	    				break;
	    			}
	    		}
	    		if (found == false) {
	    			var newItem:Object = new Object();
	    			newItem.data = item.@requestNumber;
	    			newItem.label = item.@requestNumber;
	    			requestNumbers.addItem(newItem);
	    		}
	    	}
	    	requestNumbers.refresh();
	    }		
		
		public function sortRequestNumbers(obj1:Object, obj2:Object, fields:Array=null):int {
			if (obj1 == null && obj2 == null) {
				return 0;
			} else if (obj1 == null) {
				return 1;
			} else if (obj2 == null) {
				return -1;
			} else {
				var num1:String = obj1.label;
				var num2:String = obj2.label;
				
				if (num1 == 'Other') {
					return 1;
				} else if (num2 == 'Other') {
					return  -1;
				} else {
					if (num1 < num2) {
						return -1;
					} else if (num1 > num2) {
						return 1;
					} else {
						return 0;
					}
				}			
				
			}
		}

		public function filterExperimentList(item:Object):Boolean {
			var experimentDate:Date = new Date(item.@createDate.toString());
			
        	var keep:Boolean = true;
        	if (browseView.requestNumberCombo.selectedItem != null && browseView.requestNumberCombo.selectedItem.label != "") {
        		keep = false; 
        		if (item.@requestNumber == browseView.requestNumberCombo.selectedItem.label) {
        			keep = true;
        		}
        	}   
        	return keep;
        }
        
 
          
		  
		private function onGetRequest(event:ResultEvent):void {
			if (getRequest.lastResult.name() == "SUCCESS") {
				Alert.show("Experiment not found");
				parentApplication.theViews.selectedIndex = 0;
        		return;
        	}
			if (getRequest.lastResult.name() != "OpenRequestList") {
        		Alert.show(getRequest.lastResult..ACTMESSAGE.@TEXT);
        		return;
        	}
        	

        	
		    if (this.showDownloads) {
		    	this.showExperimentDownloads();
		    	theViews.visible = true;
		    	this.showDownloads = false;
		    	return;
		    }

			if (selectedIdRequest != null) {
				selectedExperiment = getRequest.lastResult.Request.(@idRequest == selectedIdRequest);			
			} else {
				selectedExperiment = getRequest.lastResult.Request.(@number == selectedRequestNumber);
				
				var params:Object = new Object();
				params.idProject = selectedExperiment.@idProject;
				params.showSamples = "N";
				params.showCategory = "N";
				params.searchPublicProjects = 'N';
				getProjectRequestList.send(params);
				
			    detailView.title = "Experiment " + selectedRequestNumber;
			    
			    
			}
			
			if (showEditView) {
				theViews.selectedChild = editView;
				editView.removeDataListeners();
				editView.setupEditForm(this.selectedExperiment);
				callLater(editView.setupDataListeners);
				showEditView = false;						
			} else {
			    theViews.selectedChild = detailView;
				detailView.setupForm(this.selectedExperiment);
				
			}

		    theViews.visible = true;
		}

		private function onGetRequestForDownload(event:ResultEvent):void {
			if (getRequestForDownload.lastResult.name() != "OpenRequestList") {
        		Alert.show(getRequestForDownload.lastResult..ACTMESSAGE.@TEXT);
        		return;
        	}			
			selectedExperiment = getRequestForDownload.lastResult.Request.(@number == selectedRequestNumber);
			
			var params:Object = new Object();
			params.idProject = selectedExperiment.@idProject;
			params.showSamples = "N";
			params.showCategory = "N";
			var call:Object = getProjectRequestList.send(params);
			call.marker = "showDownloads";
		    theViews.visible = true;
		}
		
		private function onGetRequestFromNav(event:ResultEvent):void {
			if (getRequestFromNav.lastResult.name() == "SUCCESS") {
				Alert.show("Experiment not found");
				parentApplication.theViews.selectedIndex = 0;
        		return;
        	}
			if (getRequestFromNav.lastResult.name() != "OpenRequestList") {
        		Alert.show(getRequestFromNav.lastResult..ACTMESSAGE.@TEXT);
        		return;
        	}
        	

			if (selectedIdRequest != null) {
				selectedExperiment = getRequestFromNav.lastResult.Request.(@idRequest == selectedIdRequest);			
			} else {
				selectedExperiment = getRequestFromNav.lastResult.Request.(@number == String(selectedRequestNumber).toUpperCase());
			}
				
			var params:Object = new Object();
			params.idProject = selectedExperiment.@idProject;
			params.showSamples = "N";
			params.showCategory = "N";
			params.searchPublicProjects = 'N';
			getProjectRequestList.send(params);
		}
		
		private function onGetProject(event:ResultEvent):void {
			selectedProject = getProject.lastResult.Project.(@idProject == selectedIdProject);
			browseView.refreshDownloadList();
			browseView.refreshTrackList();
			browseView.refreshVisibilityList();
			
			browseView.refreshButtonStack();
			if (!browseView.theViews.contains(browseView.projectDetailView)) {
				browseView.theViews.addChild(browseView.projectDetailView);
			}
			browseView.projectDetailView.checkSecurity();
			browseView.projectDetailView.setupDataListeners();

		}

		public function getTreeIcon(item:Object):Class {
				if (item.name() == "Lab") {
					return parentApplication.iconGroup;
				} else if (item.name() == "Project") {
					return parentApplication.iconProject;
				} else if (item.name() == "RequestCategory") {
					return parentApplication.iconCategory;
				} else if (item.name() == "Request" && item.@codeRequestCategory == "QC") {
					return parentApplication.iconQC;
				} else if (item.name() == "Request" && item.@codeRequestCategory == "SOLEXA") {
					return parentApplication.iconSolexa;
				} else if (item.name() == "Request" && item.@codeRequestCategory == "AFFY") {
					return parentApplication.iconMicroarrayAffy;
				}else if (item.name() == "Request" && item.@codeRequestCategory == "AGIL") {
					return parentApplication.iconMicroarray;
				} else if (item.name() == "Request" && item.@codeRequestCategory == "AGIL1") {
					return parentApplication.iconMicroarraySingleColor;
				} else {
					return parentApplication.iconMicroarrayOther;
				}
		}

		public function getTreeLabel(item:Object):String {
				if (item.name() == "Request") {
					return item.@requestNumber;
				} else {
					return item.@label;
				}
		}
		
		public function refreshExperimentsAfterDrag():void {
			this.browseTree.selectedItem = null;
			selectedIdProject = null;
			selectedIdLab = null;
			if (this.selectedRequestNumber != null) {
				var params:Object = new Object();
				params.idProject = selectedExperiment.@idProject;
				params.showSamples = "N";
				params.showCategory = "N";
				params.publicExperimentsInOtherGroups = 'N';
				getProjectRequestList.send(params);
				
			} else {
				
				getProjectRequestList.send(getBrowseParameters());
			}

		}
		
		public function getBrowseParameters():Object {
			var params:Object = new Object();
			if (currentState == 'AdminState') {
			    if (browseLabCombo.selectedItem != null) {
					params.idLab = browseLabCombo.selectedItem.@idLab;
				}
				if (browseUserCombo.selectedItem != null) {
					params.idAppUser = browseUserCombo.selectedItem.@idAppUser;
				}
			} else if (currentState != "GuestState") {
				if (myExperimentsCheckbox.selected) {
					params.idAppUser = parentApplication.getIdAppUser();
				}				
			}
			if (lastWeekCheckbox.selected) { 
				params.lastWeek = 'Y'; 
			} else if (lastMonthCheckbox.selected) {
				params.lastMonth = 'Y';
			} else if (lastYearCheckbox.selected) {
				params.lastYear = 'Y';
			}
			if (publicExperimentsOtherGroupsCheckbox.selected) {
				params.publicExperimentsInOtherGroups = 'Y';
			} else {
				params.publicExperimentsInOtherGroups = 'N';
			}
			
			params.showSamples = 'N';
			params.showCategory = 'N';
			if (currentState == 'AdminState' || 
			    publicExperimentsOtherGroupsCheckbox.selected || 
			    myExperimentsCheckbox.selected || 
			    lastWeekCheckbox.selected || 
			    lastMonthCheckbox.selected || 
			    lastYearCheckbox.selected ||
			    this.MicroarrayCheckbox.selected ||
			    this.SolexaCheckbox.selected ||
			    this.BioanalyzerCheckbox.selected ) {
				params.showMyLabsAlways = 'N'; 
			} else {
				params.showMyLabsAlways = 'Y';
			}
			
			params.isMicroarray  = this.MicroarrayCheckbox.selected ? "Y" : "N";
			params.isSolexa      = this.SolexaCheckbox.selected ? "Y" : "N";
			params.isBioanalyzer = this.BioanalyzerCheckbox.selected ? "Y" : "N";
			
			return params;
		}
		 
		public function resetSearchComponents():void {
			
			if (this.MicroarrayCheckbox == null) {
				return;
			}
			
			this.MicroarrayCheckbox.selected = false;
			this.SolexaCheckbox.selected = false;
			this.BioanalyzerCheckbox.selected = false;
			
			this.lastMonthCheckbox.selected = false;
			this.lastWeekCheckbox.selected = false;
			this.lastYearCheckbox.selected = false;
			
			if (this.currentState == null) {
				this.myExperimentsCheckbox.selected = false;
				this.myLabsExperimentsCheckbox.selected = false;
				this.publicExperimentsOtherGroupsCheckbox.selected = false;
			} else if (this.currentState == "AdminState") {
				this.browseUserCombo.selectedItem = null;				
			}
		}
		
		public function refreshExperiments():void {
			if (this.selectedRequestNumber != null) {
				var params:Object = new Object();
				params.idProject = selectedExperiment.@idProject;
				params.showSamples = "N";
				params.showCategory = "N";
				params.searchPublicProjects = 'N';
				getProjectRequestList.send(params);
				
			} else {
				getProjectRequestList.send(getBrowseParameters());
			}

		}
		public function initializeExperiments():void {
			if (parentApplication.hasPermission('canAccessAnyObject')) {
			} else {
				if (getProjectRequestList.lastResult == null ||
				    selectedRequestNumber != null) {
				    collapseAllBrowseNodes();
				    selectedRequestNumber = null;
				    
				    if (currentState == "" || currentState == null) {
				    	this.myExperimentsCheckbox.selected = true;
				    }
				    this.refreshExperiments();
				    
				}
				
			}
		}
		
	  
	  	private function onDragDrop( event:DragEvent ) : void
		{
		   // Disallow drop operation if user is guest
		   if (parentApplication.isGuestMode()) {
				event.preventDefault();
		       	event.target.hideDropFeedback(event);
		       	DragManager.showFeedback(DragManager.NONE);		   	
		       	return;
		   }
	       var ds:DragSource = event.dragSource;
	       var dropTarget:Tree = Tree(event.currentTarget);
	
	       // retrieve the data associated with the "items" format. This will be the data that
	       // the dragInitiator has copied into the DragSource.
	       var items:Array = ds.dataForFormat("treeItems") as Array;
	
	       // determine where in the tree the drop occurs and select that node by the index; followed by 
	       // retrieving the node itself. 
	       var r:int = browseTree.calculateDropIndex(event);
	       browseTree.selectedIndex = r;
	       var node:XML = browseTree.selectedItem as XML;
	       var p:*;
	
	
	       if (node.name() == "Project") {
	       		p = node;
	       } else if (node.name() == "Request") {
	       		p = node.parent();
	       }else {
	       		//ignore drop operations on lab.
	       		event.preventDefault();
		       	event.target.hideDropFeedback(event);
		       	DragManager.showFeedback(DragManager.NONE);		 
		       	return;
	       }
	
	       // taking all of the items in the DragSouce, insert them into the 
	       // tree using parent p. 
	       if (p != null) {
		       for(var i:Number=0; i < items.length; i++) {
		       	if (items[i].name() == "Request") {
		             browseTree.dataDescriptor.addChildAt(p, items[i], r+i);
		             
		             dropProject = p;
		             dragRequest = items[i];
		           	 promptToReassignProject();
		             
		       	} 
		       }
	       	
	       }	       
		}
		
	    public function promptToReassignProject():void {
	   		var message:String = "Reassign experiment " + dragRequest.@requestNumber +
	               " to project " + dropProject.@projectName + "?";
	
			Alert.show(message, 
						null, 
						(Alert.YES | Alert.NO), this, 
						onPromptToReassignProject);
			
		}
		
		private function onPromptToReassignProject(event:CloseEvent):void {
            if (event.detail==Alert.YES) {
	            selectedProject = dropProject;
	            selectedIdProject = dropProject.@idProject;
	            selectedIdLab = dropProject.@idLab;
	            
	            var params:Object = new Object();
	            params.idRequest = dragRequest.@idRequest;
	            params.idProject = dropProject.@idProject;
	                  		
           		saveRequestProject.send(params);
            }
        }

       	private function onSaveRequestProject(event:ResultEvent): void {
   			dropProject = null;  
   			dragRequest = null;
       		if (saveRequestProject.lastResult.name() == "SUCCESS") {
				this.refreshExperimentsAfterDrag();
			
        	} else {
        		Alert.show(saveRequestProject.lastResult..ACTMESSAGE.@TEXT);
        	}	    	
	    } 
	    
	    public function deleteExperiment():void {
	    	var params:Object = new Object();
	    	params.idRequest = selectedExperiment.@idRequest;
	    	deleteRequest.send(params);
	    }
	    
	    private function onDeleteRequest(event:ResultEvent): void {
	    	if (deleteRequest.lastResult.name() == "SUCCESS") {
	    		this.refreshExperiments();
	    		theViews.selectedChild = browseView;
	    	} else {
	    		Alert.show(deleteRequest.lastResult..ACTMESSAGE.@TEXT);
	    	}
	    }
	    
	    private function getMembersOfLab():void {
			if (this.browseLabCombo.selectedItem.@idLab != null) {
				getLab.send();				
			}
		}
		private function onGetLab(event:ResultEvent):void {			
			var lab:Object = getLab.lastResult..Lab.(@idLab == browseLabCombo.selectedItem.@idLab);
			var emptyNode:XML = <AppUser idAppUser='' displayName=''/>;
			appUsers = new XMLListCollection(lab.members.AppUser);
			for each (var mgr:Object in lab.managers.AppUser) {
				if (!appUsers.contains(mgr)) {
					appUsers.addItem(mgr);				
				}
			}
			appUsers.addItemAt(emptyNode, 0);
			
			appUserSort = new Sort();
	        appUserSort.compareFunction = this.sortAppUsers;
	        appUsers.sort = appUserSort;
	        appUsers.refresh();
	        
	        this.browseUserCombo.selectedIndex = 0;
		}	    

        private function sortAppUsers(obj1:Object, obj2:Object, fields:Array=null):int {
			if (obj1 == null && obj2 == null) {
				return 0;
			} else if (obj1 == null) {
				return 1;
			} else if (obj2 == null) {
				return -1;
			} else {
				var display1:String = obj1.@displayName;
				var display2:String = obj2.@displayName;
				
				if (display1 < display2) {
					return -1;
				} else if (display1 > display2) {
					return 1;
				} else {
					return 0;
				}
				
			}
		}			

	private function toggleDateCheckboxes(checkBoxNumber:int):void {
		if (checkBoxNumber == 1) {
			this.lastMonthCheckbox.selected = false;
			this.lastYearCheckbox.selected  = false;
		} else if (checkBoxNumber == 2) {
			this.lastWeekCheckbox.selected = false;
			this.lastYearCheckbox.selected  = false;
		} else if (checkBoxNumber == 3) {
			this.lastWeekCheckbox.selected  = false;
			this.lastMonthCheckbox.selected = false; 
		}   
	}   
	private function toggleOwnershipCheckboxes(checkBoxNumber:int):void {
		if (checkBoxNumber == 1) {
			this.publicExperimentsOtherGroupsCheckbox.selected = false;
		} else if (checkBoxNumber == 2) { 
			this.myExperimentsCheckbox.selected = false;
		} 
	}
	private function toggleRequestKindCheckboxes(checkBoxNumber:int):void {
		if (checkBoxNumber == 1) {
			this.SolexaCheckbox.selected = false;
			this.BioanalyzerCheckbox.selected  = false;
		} else if (checkBoxNumber == 2) {
			this.MicroarrayCheckbox.selected = false;
			this.BioanalyzerCheckbox.selected  = false;
		} else if (checkBoxNumber == 3) {
			this.SolexaCheckbox.selected = false;
			this.MicroarrayCheckbox.selected = false;
		} 
	}
		
       ]]>
    </mx:Script> 
   	
   	
   	<mx:ArrayCollection id="requestNumbers"  sort="{requestNumberSorter}"/>
	<mx:Sort id="requestNumberSorter" compareFunction="sortRequestNumbers" unique="true"/>
     
	<mx:DateFormatter id="dateFormatter" formatString="YYYYMMDD"/>

  <mx:VBox width="100%" height="100%" id="vbox1" verticalGap="0">
 
	<mx:HBox horizontalGap="0" paddingTop="0" paddingBottom="4" paddingLeft="0" horizontalAlign="left" verticalAlign="middle" width="100%" id="hbox1">
	    <mx:HBox width="100%" id="hbox4" styleName="navBox">
		    	<mx:HBox horizontalGap="0" height="100%" verticalAlign="middle" verticalGap="0" paddingLeft="2" horizontalAlign="right" paddingRight="0" id="hbox2">
		    	  <mx:RadioButtonGroup id="experimentRadioGroup"/>
		    	  <mx:RadioButton label="My experiments" id="myExperimentsCheckbox"  groupName="experimentRadioGroup" selected="true" letterSpacing=".4" horizontalGap="0">
		    		    	<mx:click>selectedIdProject=null;selectedIdLab=null;selectedIdRequest=null;selectedRequestNumber=null;getProjectRequestList.send(getBrowseParameters());</mx:click>
		    	  </mx:RadioButton>
		    	  <mx:RadioButton label="My lab's experiments" id="myLabsExperimentsCheckbox"   groupName="experimentRadioGroup" paddingLeft="2" horizontalGap="0">
		    	    <mx:click>selectedIdProject=null;selectedIdLab=null;selectedIdRequest=null;selectedRequestNumber=null;getProjectRequestList.send(getBrowseParameters());</mx:click>
		    	  </mx:RadioButton>
		    	  <mx:RadioButton label="Other labs' public experiments" id="publicExperimentsOtherGroupsCheckbox"   groupName="experimentRadioGroup" paddingLeft="2" letterSpacing=".4" horizontalGap="0">
		    		    	<mx:click>selectedIdProject=null;selectedIdLab=null;selectedIdRequest=null;selectedRequestNumber=null;getProjectRequestList.send(getBrowseParameters());</mx:click>
		    	  </mx:RadioButton>
		    	</mx:HBox>
		    	<mx:VRule height="20" id="vrule1"/>
			    <mx:HBox horizontalGap="2">
			    	<mx:CheckBox label="Microarray" id="MicroarrayCheckbox" horizontalGap="0">
		    			<mx:click>toggleRequestKindCheckboxes(1);selectedIdLab=null;selectedIdProject=null;selectedIdRequest=null;selectedRequestNumber=null;getProjectRequestList.send(getBrowseParameters());</mx:click>
		    		</mx:CheckBox>
			    	<mx:CheckBox label="Illumina"  id="SolexaCheckbox"  paddingLeft="2" horizontalGap="0">
		    			<mx:click>toggleRequestKindCheckboxes(2);selectedIdLab=null;selectedIdProject=null;selectedIdRequest=null;selectedRequestNumber=null;getProjectRequestList.send(getBrowseParameters());</mx:click>
		    		</mx:CheckBox>
			    	<mx:CheckBox label="Sample Quality"  id="BioanalyzerCheckbox" paddingLeft="2" horizontalGap="0">
		    			<mx:click>toggleRequestKindCheckboxes(3);selectedIdLab=null;selectedIdProject=null;selectedIdRequest=null;selectedRequestNumber=null;getProjectRequestList.send(getBrowseParameters());</mx:click>
		    		</mx:CheckBox>
		    		
		    	</mx:HBox>
		    	<mx:VRule height="20"/>
		    	<mx:HBox horizontalGap="0" height="100%" verticalAlign="middle" verticalGap="0" horizontalAlign="right" paddingRight="0"  id="hbox3">
		    		<mx:CheckBox id="lastWeekCheckbox" label="In last week" paddingRight="0" paddingLeft="0" horizontalGap="0"  >
		    			<mx:click>toggleDateCheckboxes(1);selectedIdLab=null;selectedIdProject=null;selectedIdRequest=null;selectedRequestNumber=null;getProjectRequestList.send(getBrowseParameters());</mx:click>
		    		</mx:CheckBox>
		    		<mx:CheckBox  id="lastMonthCheckbox" label="month"  paddingRight="0" paddingLeft="2" horizontalGap="0"  >
		    			<mx:click>toggleDateCheckboxes(2);selectedIdLab=null;selectedIdProject=null;selectedIdRequest=null;selectedRequestNumber=null;getProjectRequestList.send(getBrowseParameters());</mx:click>
		    		</mx:CheckBox>
		    		<mx:CheckBox  id="lastYearCheckbox" label="year"  paddingLeft="2" paddingRight="0" horizontalGap="0"  >
		    			<mx:click>toggleDateCheckboxes(3);selectedIdLab=null;selectedIdProject=null;selectedIdRequest=null;selectedRequestNumber=null;getProjectRequestList.send(getBrowseParameters());</mx:click>
		    		</mx:CheckBox>
		    	</mx:HBox> 
	    	
	    </mx:HBox>
	</mx:HBox>		
     
	<mx:HDividedBox height="100%" width="100%" id="hdividedbox1">
	
	   
			
			<mx:Panel label="Experiments" width="220" height="100%" title="Experiments" id="navPanel" >
				<mx:VBox id="navContainer" width="100%" height="100%" verticalGap="0" paddingTop="0" label="Experiments">

					<mx:VBox id="treeContainer"  dropShadowEnabled="true" width="100%" height="100%" paddingLeft="2" verticalGap="0" >
					    <mx:ViewStack id="treeExpansionStack" width="100%">
					        <mx:HBox width="100%" horizontalAlign="right">
								<mx:LinkButton   label="Expand projects" click="{treeExpansionStack.selectedIndex = 1;this.expandAllNodes();}" textDecoration="underline" textAlign="left"/>
					        </mx:HBox>
					        <mx:HBox width="100%" horizontalAlign="right">
								<mx:LinkButton  label="Collapse projects" click="{treeExpansionStack.selectedIndex = 0;this.collapseProjectNodes()}" textDecoration="underline" textAlign="left"/>					    	
					        </mx:HBox>
					    </mx:ViewStack>
						<mx:Tree width="100%" height="100%" 
							dataProvider="{getProjectRequestList.lastResult}" 
							showRoot="false" 
							id="browseTree" click="onBrowseTree();" 
							borderStyle="solid"
							labelField="@label"
							labelFunction="getTreeLabel" 
					        dragEnabled="true" 
					        dropEnabled="true" 
					        dragMoveEnabled="true"
							dragDrop="onDragDrop(event);" 
							iconFunction="getTreeIcon" backgroundAlpha="1" dropShadowEnabled="true"></mx:Tree>
						 
					</mx:VBox>
     
				</mx:VBox>
					<mx:ControlBar width="100%" verticalAlign="top">
						<mx:VBox verticalGap="0">
						        <mx:LinkButton label="New project" id="newProjectButton" click="browseView.showNewProjectWindow()" 
						        	icon="@Embed('../../assets/folder_add.png')" 
						        	toolTip="New project..." 
						        	enabled="false" 
						        	disabledIcon="@Embed(source='../../assets/folder_add_disable.png')" textDecoration="underline"/>
						        <mx:LinkButton  label="Delete project" id="deleteProjectButton" icon="@Embed('../../assets/folder_delete.png')" 
						        	click="browseView.projectDetailView.promptToDeleteProject()"
						        	toolTip="Delete project" enabled="false" textDecoration="underline">
						        	<mx:disabledIcon>@Embed(source='../../assets/folder_delete_disable.png')</mx:disabledIcon>
						        </mx:LinkButton>
							
						</mx:VBox>
						<mx:VBox verticalGap="0">
						        <mx:LinkButton label="Refresh" icon="@Embed(source='../../assets/action_refresh.gif')" click="refreshExperiments()" textDecoration="underline"/>
						</mx:VBox>
								
					</mx:ControlBar>
			</mx:Panel> 


 
	    
 
		
	     <mx:ViewStack id="theViews" creationPolicy="all" visible="true" width="100%" height="100%">

	        <experiment:ExperimentBrowseView id="browseView"  width="100%" height="100%">
	        </experiment:ExperimentBrowseView>

	        <experiment:ExperimentDetailView id="detailView" width="100%" height="100%">
	        </experiment:ExperimentDetailView>
	        
			<experiment:ExperimentEditView id="editView" width="100%" height="100%">
	        </experiment:ExperimentEditView>

         </mx:ViewStack>
		
	</mx:HDividedBox>
  </mx:VBox>
	
	<mx:states>
		<mx:State name="GuestState">
			<mx:RemoveChild target="{hbox2}"/>
			<mx:SetStyle target="{hbox3}" name="paddingLeft" value="4"/>
		</mx:State>
		
		<mx:State name="AdminState">
			<mx:AddChild relativeTo="{vrule1}" position="before">
			   <mx:HBox horizontalGap="0" verticalAlign="middle">
					<mx:Label id="groupLabel" visible="true" text="Group:" paddingBottom="0"/>
					<mx:ComboBox id="browseLabCombo" 
						labelField="@name"
						dataProvider="{parentApplication.promptedLabList}"
						 change="{this.browseUserCombo.selectedIndex = 0;selectedIdLab=null;selectedIdProject=null;selectedIdRequest=null;selectedRequestNumber=null;getProjectRequestList.send(getBrowseParameters());getMembersOfLab()}"  
						 visible="true"  width="150">
					</mx:ComboBox>
			   	
			   </mx:HBox>
			</mx:AddChild>
			<mx:AddChild relativeTo="{vrule1}" position="before">
			     <mx:HBox  horizontalGap="0"  verticalAlign="middle">
					<mx:Label id="userLabel" visible="true" text="Requester:" paddingBottom="0" paddingLeft="10" />
					<mx:ComboBox id="browseUserCombo" 
						labelField="@displayName"
						dataProvider="{appUsers}"
						 change="selectedIdProject=null;selectedIdRequest=null;selectedRequestNumber=null;getProjectRequestList.send(getBrowseParameters())"  
						 visible="true"  width="140">
					</mx:ComboBox>
			     </mx:HBox>
			</mx:AddChild>
			<mx:AddChild relativeTo="{vrule1}" position="before">
			</mx:AddChild>
			<mx:RemoveChild target="{myExperimentsCheckbox}"/>
			<mx:RemoveChild target="{publicExperimentsOtherGroupsCheckbox}"/>
			<mx:RemoveChild target="{myLabsExperimentsCheckbox}"/>
			<mx:RemoveChild target="{hbox2}"/>
			<mx:SetStyle target="{hbox4}" name="verticalAlign" value="middle"/>
			<mx:SetStyle target="{hbox4}" name="horizontalGap" value="8"/>
			<mx:SetStyle target="{lastMonthCheckbox}" name="paddingLeft" value="2"/>
			<mx:SetStyle target="{lastYearCheckbox}" name="paddingLeft" value="2"/>


		</mx:State>

		
	</mx:states>
	

    
</mx:Canvas>
