<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" xmlns:views="views.*" >



   <mx:HTTPService  
    id="saveVisibility" 
    url="SaveVisibility.gx"
    destination="saveVisibility"
    resultFormat="e4x"
    showBusyCursor="true"
    result="onSaveVisibility(event)"
	fault="parentApplication.onFailHttpRequest('Failed to save visibility', event)"
    method="POST"
    useProxy="false">
  </mx:HTTPService>


 <mx:Script>
 <![CDATA[
 	import mx.collections.XMLListCollection; 
 	import mx.rpc.events.ResultEvent;
 	import views.renderers.ComboBoxVisibility;
 	import mx.events.CloseEvent;
 	import mx.controls.Alert;
 	import hci.flex.renderers.ComboBoxDictionary;
 	import hci.flex.renderers.LabelDictionary;
 	
 	private var broadestCodeVisibility:String = "MEM";
 	
 	public function checkSecurity():void {
 		if (parentDocument.parentDocument.browseTree.selectedItem.name() == "Project" &&
 		    parentDocument.parentDocument.selectedProject != null) {
 			if (parentDocument.parentDocument.selectedProject.@canUpdateVisibility == "Y") {
 				parentDocument.saveVisibilityButton.enabled = true;
 				projectVisibilityCombo.enabled = true;
 			} else {
 				parentDocument.saveVisibilityButton.enabled = false;
 				projectVisibilityCombo.enabled = false;
 			}
 		} else {
 			parentDocument.saveVisibilityButton.enabled = false;
 			projectVisibilityCombo.enabled = false;
 		}
 	}
			

 	private function getRequestKind(item:Object, col:int):String {
		var de:XMLList = parentApplication.dictionaryManager.getEntry('hci.gnomex.model.RequestCategory', item.@codeRequestCategory);
    	if (de.length() == 1) {
	    	return de[0].@display;
    	} else {
    		return "";
    	}
	}
	
	public function  save():void {
    	// Warn if visibility of experiment is broader than project
    	broadestCodeVisibility = "MEM";
    	for each (var r:Object in parentDocument.parentDocument.browseTree.selectedItem..Request) {
    		if (r.@codeVisibility > broadestCodeVisibility) {
    			broadestCodeVisibility = r.@codeVisibility;
    		}
    	}
    	
    	var broadestVisibility:String = parentApplication.dictionaryManager.getEntry('hci.gnomex.model.Visibility', broadestCodeVisibility).@visibility;
    	
    	if (projectVisibilityCombo.selectedItem.@codeVisibility < broadestCodeVisibility) {
    		
			mx.controls.Alert.show("It is recommeded that project '" +
			    		parentDocument.parentDocument.browseTree.selectedItem.@label + 
			    		"' have a visibility of '" + broadestVisibility +
			    		" '; otherwise, experiments with broader visibility will not apply. (Experiments with more restrictive visibility will remain restricted.)\n\n" +
			    		"Apply '" + broadestVisibility + "' visibility to project '" + parentDocument.parentDocument.browseTree.selectedItem.@label + "'?" ,
			    		"Warning",
						(Alert.YES | Alert.NO | Alert.CANCEL), 
						this, 
						onPromptToChangeProjectVisibility,						
						parentApplication.iconWarning);
    	} else {
    		saveImpl();
    	}
	}

	private function onPromptToChangeProjectVisibility(event:CloseEvent):void {
        if (event.detail == Alert.YES) {
        	projectVisibilityCombo.selectedItem = parentApplication.dictionaryManager.getEntry('hci.gnomex.model.Visibility', broadestCodeVisibility);
        	saveImpl();
        } else if (event.detail == Alert.NO) {
        	saveImpl();
        } 
    }		
	
	public function saveImpl():void {
	    var dirtyRequests:XMLList = parentDocument.parentDocument.browseTree.selectedItem..Request.(@isDirty == 'Y');        	
    	var dirtyRequestList:XMLListCollection = new XMLListCollection(dirtyRequests);
    	
    	var params:Object = new Object();
    	params.idProject = parentDocument.parentDocument.browseTree.selectedItem.@idProject;
    	params.codeVisibility = projectVisibilityCombo.selectedItem.@codeVisibility;
    	params.visibilityXMLString = dirtyRequestList.toXMLString();
    	saveVisibility.send(params);

	}
	
	private function onSaveVisibility(event:ResultEvent):void {
		if (saveVisibility.lastResult.name() == "SUCCESS" ||
		    saveVisibility.lastResult.name() == "ProjectRequestVisibilityList") {
    		parentDocument.parentDocument.refreshExperiments();
    		parentDocument.dirtyVisibility.resetDirty();
    	} else {
    		mx.controls.Alert.show(saveVisibility.lastResult..ACTMESSAGE.@TEXT);
    	}
	}
	
	
	
	                
    public function onEdit():void {
    	visibilityGrid.selectedItem.@isDirty = "Y";
    	parentDocument.dirtyVisibility.setDirty();
    }
    
    public function setVisibilityCombo():void {
    	projectVisibilityCombo.selectedIndex = 0;
        if (parentDocument.parentDocument.browseTree.selectedItem.@codeVisibility != null) {
            for(var i:Number = 0; i < projectVisibilityCombo.dataProvider.length; i++) {
            	if(projectVisibilityCombo.dataProvider[i].@value == parentDocument.parentDocument.browseTree.selectedItem.@codeVisibility) {
                      projectVisibilityCombo.selectedIndex = i;
                      break;
                 }
            }
        	
        }
    }
    
    
    
    
    
 ]]>
 </mx:Script>
	
	<mx:VBox  width="100%" height="100%"   paddingLeft="4" paddingRight="4" paddingTop="4" paddingBottom="4">
	       <mx:HBox width="100%" horizontalAlign="left">
					<mx:Label text="{'Visibility for ' + parentDocument.parentDocument.browseTree.selectedItem.@projectName}"/>
	                <mx:ComboBox id="projectVisibilityCombo" labelField="@display"  
                     	dataProvider="{parentApplication.dictionaryManager.xml.Dictionary.(@className == 'hci.gnomex.model.Visibility').DictionaryEntry}"  
                     	  width="250"  change="parentDocument.dirtyVisibility.setDirty()"/>
	       </mx:HBox>
	       <mx:DataGrid  id="visibilityGrid" 
	       	dataProvider="{parentDocument.browseList}" 
	       	 width="100%" height="100%" 
			 editable="true"
	       	 itemEditEnd="onEdit()">
	         <mx:columns>
	            <mx:DataGridColumn width="35" minWidth="35" headerText="#"  editable="false">
	                <mx:itemRenderer>
	                    <mx:Component >
	                        <mx:HBox  horizontalGap="0" >
			                    <mx:Image id="experimentImage"  
			                     	source="{(data.@codeRequestCategory == 'QC' ? parentApplication.iconQC : (data.@codeRequestCategory == 'SOLEXA' ? parentApplication.iconSolexa : parentApplication.iconExperiment))}" >
								</mx:Image>
	                           <mx:Label id="requestNumber" text="{data.@requestNumber}">
	                           </mx:Label>
	                        </mx:HBox>
	                    </mx:Component>
	                </mx:itemRenderer>
	            </mx:DataGridColumn> 
		       	<mx:DataGridColumn headerText="Date" width="20" dataField="@requestCreateDateDisplay"  editable="false"/>
		        <mx:DataGridColumn headerText="Requester" width="40" labelFunction="parentDocument.getFullName"  editable="false"/>
		       	<mx:DataGridColumn headerText="Experiment Kind" width="35" labelFunction="parentDocument.getExperimentKind"  editable="false"/>
		       	<mx:DataGridColumn headerText="Microarray" width="90" dataField="@slideProductName"  editable="false"/>
		       	<mx:DataGridColumn headerText="Experiment Visibility"  width="75" dataField="@codeVisibility" 
					editorDataField="value" sortable="false" editable="{projectVisibilityCombo.enabled}" 
					rendererIsEditor="false" 
					itemRenderer="{LabelDictionary.create(parentApplication.dictionaryManager, 'hci.gnomex.model.Visibility', '@codeVisibility')}"
					itemEditor="{ComboBoxVisibility.create(parentApplication.dictionaryManager)}"/>

	         </mx:columns>
	       </mx:DataGrid>
	
	     
	</mx:VBox>
	
	



</mx:Canvas>


