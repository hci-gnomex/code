<?xml version="1.0" encoding="utf-8"?>
<mx:VBox styleName="panelBox"  xmlns:mx="http://www.adobe.com/2006/mxml" 
	width="100%" height="100%" 
	xmlns:views="views.*"   
	xmlns:experiment="views.experiment.*"  
	xmlns:util="views.util.*" 
	verticalGap="0" >
	   
<mx:Label id="titleLabel" styleName="titleLabel" width="100%"/> 
   
   <mx:HTTPService  
    id="getRequest" 
    url="GetRequest.gx"
    destination="getRequest"
    resultFormat="e4x"
    showBusyCursor="true"
    result="onGetRequest(event)"
	fault="parentApplication.onFailHttpRequest('Failed to get request', event)"
    method="POST"
    useProxy="false">
  </mx:HTTPService>


   <mx:HTTPService  
    id="getNewProject" 
    url="GetProject.gx"
    destination="getProject"
    resultFormat="e4x"
    showBusyCursor="true"
    result="onGetNewProject(event)"
	fault="parentApplication.onFailHttpRequest('Failed to get new project', event)"
    method="POST"
    useProxy="false">
    <mx:request>
   		<idProject>0</idProject>    	
    </mx:request>
  </mx:HTTPService>

  
 <mx:Script>
 	<![CDATA[
 	import mx.collections.ArrayCollection;
 	import mx.containers.TitleWindow;
 	import mx.core.IContainer;
 	import mx.core.IFlexDisplayObject;
 	import mx.managers.PopUpManager;
 	import mx.rpc.events.ResultEvent;
 	
 	import views.experiment.ProjectDetailWindow;
 	import views.util.DownloadHints;



    [Bindable]
    public var selectedExperiment:Object;
    
    private var selectedIdRequest:int;

    private var projectDetailWindow:ProjectDetailWindow;
        
        
	public function checkSecurity():void { 
		if (parentApplication.isGuestMode()) {
			if (theViews.contains(this.trackView)) {
				theViews.removeChild(this.trackView);
			}
			if (theViews.contains(this.visibilityView)) {
				theViews.removeChild(this.visibilityView);
			}
		}
	}

        
    public function getFullName(item:Object, col:int):String {
    	return item.@ownerLastName + ", " + item.@ownerFirstName;
    }
    
    public function sortFullName(item1:Object, item2:Object):int {
    	var name1:String  = getFullName(item1, 0);
    	var name2:String  = getFullName(item2, 0);
    	
    	if (name1.toUpperCase() > name2.toUpperCase()) {
    		return 1;
    	} else if (name2.toUpperCase() > name1.toUpperCase()) {
    		return -1;
    	} else {
    		return 0;
    	}
    }

    public function sortRequestNumber(item1:Object, item2:Object):int {
    	var n1:String  = item1.@requestNumber;
    	var n2:String  = item2.@requestNumber;
    	
    	var number1:Number = -1;
    	var number2:Number = -1;
    	
    	var tokens:Array = n1.split("R");
    	if (tokens.length > 0) {
    		number1 = tokens[0];	
    	} else {
    		number1 = Number(n1);
    	}
    	tokens = n2.split("R");
    	if (tokens.length > 0) {
    		number2 = tokens[0];	
    	} else {
    		number2 = Number(n2);
    	}
    	
    	
    	if (number1 > number2) {
    		return 1;
    	} else if (number2 > number1) {
    		return -1;
    	} else {
    		return 0;
    	}
    }

    public function getRequestKind(item:Object, col:int):String {
    	var de:XMLList = parentApplication.dictionaryManager.getEntry('hci.gnomex.model.RequestCategory', item.@codeRequestCategory);
    	if (de.length() == 1) {
	    	return de[0].@display;
    	} else {
    		return "";
    	}
    }
    public function getExperimentKind(item:Object, col:int):String {
    	if (item.@codeApplication == "") {
    		return getRequestKind(item, col);
    	} else {
	    	var de:XMLList = parentApplication.dictionaryManager.getEntry('hci.gnomex.model.Application', item.@codeApplication);
	    	if (de.length() == 1) {
		    	return de[0].@display;    		
	    	} else {
	    		return getRequestKind(item, col);
	    	}
    	}
    }
    
    public function sortExperimentKind(item1:Object, item2:Object):int {
    	var buf1:String = getExperimentKind(item1, 0);
    	var buf2:String = getExperimentKind(item2, 0);
    	
    	if (buf1 > buf2) {
    		return 1;
    	} else if (buf2 > buf1) {
    		return -1;
    	} else {
    		return 0;
    	}
    }
    
   	public function reloadDownloadList():void {
   		// Getting the download list is slow since we now show an expanded file
   		// view by default.  Only show downloads at request or project level.
   		if (parentDocument.selectedIdProject != null || 
   		     parentDocument.selectedIdRequest != null) {
			downloadView.getRequestDownloadList.send(getBrowseParameters());
			downloadView.browseMessage = "";
		} else {
	   		downloadView.browseMessage = "Click on a project or request to narrow download selection.";
			downloadView.downloadSizeMessage = "";
			downloadView.rowCountMessage = "";
			downloadView.downloadTree.visible = false;
	   	}
   	}
    
  	public function refreshDownloadList():void {
		if (theViews.selectedChild == downloadView) {
	   		// Getting the download list is slow since we now show an expanded file
	   		// view by default.  Only show downloads at request or project level.
	   		if (parentDocument.selectedIdProject != null || 
	   		     parentDocument.selectedIdRequest != null) {
				downloadView.getRequestDownloadList.send(getBrowseParameters());
				downloadView.browseMessage = "";
	   		} else {
	   			downloadView.browseMessage = "Click on a project or request to narrow download selection.";
				downloadView.downloadSizeMessage = "";
				downloadView.rowCountMessage = "";
				downloadView.downloadTree.visible = false;
	   		}
		}
	}
	
  	public function refreshDownloadListForExperiment():void {
		var params:Object = new Object();
		params.requestNumber = parentDocument.selectedExperiment.@number;
		downloadView.getRequestDownloadList.send(params);
	
	}
	
	public function refreshTrackList():void {
		if (theViews.selectedChild == trackView) {
			
			if (trackView.progressList != null) {
				trackView.progressList.removeAll();
				trackView.progressList.refresh();
			}
			trackView.getRequestProgressList.send(getBrowseParameters());
			trackView.getRequestProgressSolexaList.send(getBrowseParameters());
			
		}		
	}
	
	private function getBrowseParameters():Object {
		var params:Object = new Object();
		if (parentDocument.selectedIdProject != null) {
			params.idProject = parentDocument.selectedIdProject;
		}
		if (parentDocument.selectedIdRequest != null) {
			params.idRequest = parentDocument.selectedIdRequest;
		}
		params.idLab = parentDocument.selectedIdLab;
		if (parentDocument.currentState == 'AdminState') {
		    if (parentDocument.selectedIdLab == null && parentDocument.browseLabCombo.selectedItem != null) {
				params.idLab = parentDocument.browseLabCombo.selectedItem.@idLab;
			}
			if (parentDocument.browseUserCombo.selectedItem != null) {
				params.idAppUser = parentDocument.browseUserCombo.selectedItem.@idAppUser;
			}
		} else if (parentDocument.currentState != "GuestState")  {
			if (parentDocument.myExperimentsCheckbox.selected) {
				params.idAppUser = parentApplication.getIdAppUser();
			}				
		}
		if (parentDocument.lastWeekCheckbox.selected) {
			params.lastWeek = 'Y';
		}
		if (parentDocument.lastMonthCheckbox.selected) {
			params.lastMonth = 'Y';
		}
		if (parentDocument.lastYearCheckbox.selected) { 
			params.lastYear = 'Y';
		}
		if (parentDocument.publicExperimentsOtherGroupsCheckbox.selected) {
			params.publicExperimentsInOtherGroups = 'Y';
		}
		params.isMicroarray  = this.parentDocument.MicroarrayCheckbox.selected ? "Y" : "N";
		params.isSolexa      = this.parentDocument.SolexaCheckbox.selected ? "Y" : "N";
		params.isBioanalyzer = this.parentDocument.BioanalyzerCheckbox.selected ? "Y" : "N";
		
		return params;					
	}
	
	public function refreshVisibilityList():void {
		if (theViews.selectedChild == visibilityView) {
			visibilityView.checkSecurity();
		}
	}
	
    
    private function getGridRowNumber(item:Object,col:int):String
    {
       var x:int = browseList.getItemIndex(item) + 1;
       return String(x);
    } 
  	
  	private function onExperimentRowSelected():void {
		showExperimentDetail(experimentList.selectedItem.@idRequest);
  	}
  	
  	public function showExperimentDetail(idRequest:int):void {
  		selectedIdRequest = idRequest;

		var parms:Object = new Object();
		parms.idRequest = selectedIdRequest;
		getRequest.send(parms);  		
  	}

  	private function onGetRequest(event:ResultEvent):void {
		selectedExperiment = getRequest.lastResult.Request.(@idRequest == selectedIdRequest);
		parentDocument.selectedExperiment = selectedExperiment;
		parentDocument.theViews.selectedChild = parentDocument.detailView;
		parentDocument.detailView.detail.setupForm(parentDocument.selectedExperiment);
	}

	private function onViewSwitch():void {
		
		this.refreshDownloadList();
		this.refreshTrackList();
		this.refreshVisibilityList();
		
		refreshButtonStack();
		refreshFilterView();
		filterList();
		

	}
	
	public function refreshFilterView():void {
		if (theViews.selectedChild == projectDetailView) {
			filterListView.visible = false;
		} else {
			filterListView.visible = true;
		}
	}
	
    
	public function refreshButtonStack():void {
		if (parentApplication.canSubmitRequests(parentDocument.selectedIdLab)) {
			parentDocument.newProjectButton.enabled = true;
		} else {
			parentDocument.newProjectButton.enabled = false;
		}
		if (theViews.selectedChild == experimentListView ) {
			buttonStack.selectedChild = browseButtonView;				
		} else if (theViews.selectedChild == projectDetailView) {
			buttonStack.selectedChild = saveProjectButtonView;
		} else if (theViews.selectedChild == trackView) {
			buttonStack.selectedChild = trackButtonView;
		} else if (theViews.selectedChild == visibilityView) {
			buttonStack.selectedChild = saveVisibilityButtonView;
		} else if (theViews.selectedChild == downloadView) {
			buttonStack.selectedChild = this.downloadExpandedFilesButtonView;
		}
		
	}
		

	
	
	public function filterList():void {
		if (theViews.selectedChild == experimentListView ||
		    theViews.selectedChild == visibilityView) {
			browseList.filterFunction = parentDocument.filterExperimentList;
			browseList.refresh();
		} else if (theViews.selectedChild == downloadView) {
			if (this.requestNumberCombo.selectedItem != null && this.requestNumberCombo.selectedItem.data != '') {
				this.downloadView.selectDownload(this.requestNumberCombo.selectedItem.data);				
			}
		} else if (theViews.selectedChild == this.trackView) {
			trackView.progressList.filterFunction = parentDocument.filterExperimentList;
			trackView.progressList.refresh();
		}
		
	}
	
	public function clearFilter():void {
		if (theViews.selectedChild == experimentListView ||
		    theViews.selectedChild == visibilityView) {
			browseList.filterFunction = null;
		} else if (theViews.selectedChild == downloadView) {
			downloadView.downloadList.filterFunction = null;
		} else if (theViews.selectedChild == this.trackView) {
			trackView.progressList.filterFunction =null;
		}
	}
	 
	
	
	public function showNewProjectWindow():void {
		if (parentApplication.canSubmitRequests(parentDocument.selectedIdLab)) {
			getNewProject.send();		
		} else {
			mx.controls.Alert.show("You are not able to add projects in this group.");
		}
	}

	private function onGetNewProject(event:ResultEvent):void {
		if (getNewProject.lastResult.name() == "OpenProjectList") {
			 parentDocument.selectedProject = getNewProject.lastResult.Project.(@idProject == 0);
			 parentDocument.selectedProject.@idAppUser = parentApplication.getIdAppUser();
			 parentDocument.selectedProject.@idLab = parentDocument.selectedIdLab;
			 this.projectDetailWindow = ProjectDetailWindow(PopUpManager.createPopUp(this, ProjectDetailWindow, true));
			 this.projectDetailWindow.projectDetailView.experimentDesignEntries.refresh();
			 this.projectDetailWindow.projectDetailView.experimentFactorEntries.refresh();
			 
			 var selectedLabNode:Object = parentApplication.getLabList.lastResult.Lab.(@idLab == parentDocument.selectedIdLab);
			 this.projectDetailWindow.labCombo.selectedItem = selectedLabNode;
			 
			 PopUpManager.centerPopUp(this.projectDetailWindow);		
			
		} else {
			mx.controls.Alert.show(getNewProject.lastResult..ACTMESSAGE.@TEXT, "Get new project failed");
		}
	}
	

	
	public function getExperimentIcon(codeRequestCategory:String):Class {
		var theIcon:Class = parentApplication.iconExperiment;
		switch(codeRequestCategory){  
			case 'QC': 
				theIcon = parentApplication.iconQC; 
				break; 
			case 'SOLEXA': 
				theIcon = parentApplication.iconSolexa; 
				break; 
            case 'HISEQ': 
				theIcon = parentApplication.iconHiSeq; 
				break; 				
			default: 
				theIcon = parentApplication.iconExperiment;	
		}
		return theIcon;
	}
	
	private function showDownloadHints():void {
		var hints:IFlexDisplayObject = DownloadHints(PopUpManager.createPopUp(this, DownloadHints, true));
		PopUpManager.centerPopUp(hints);			 		
	}
	
		public function showAlternateDownloadPage():void {
			if (parentDocument.selectedIdRequest != null) {
				var url:URLRequest = new URLRequest('ShowRequestDownloadForm' + 
					(parentApplication.isGuestMode() ? 'ForGuest' : '') +   
					'.gx?idRequest=' + 
					parentDocument.selectedIdRequest);
				navigateToURL(url, '_blank');					 
			} else if (this.theViews.selectedChild == this.downloadView) {
				var requestNumbers:String = "";
				for each(var dl:Object in this.downloadView.downloadList) {
					if (requestNumbers.length > 0) {
						requestNumbers += ":"
					}
					requestNumbers += dl.@requestNumber;
				}
				if (requestNumbers.length > 0) {
					var url1:URLRequest = new URLRequest('ShowRequestDownloadForm' + 
						(parentApplication.isGuestMode() ? 'ForGuest' : '') +   
						'.gx?requestNumbers=' + 
						requestNumbers);
					navigateToURL(url1, '_blank');					
					
				}
				
			}
		}
		
		public function showFastDataTransferDownloadPage():void {
			var selectedFiles:XMLList = downloadView.getRequestDownloadList.lastResult..FileDescriptor.(@isSelected == "true");        	
			var selectedFileList:XMLListCollection = new XMLListCollection(selectedFiles);
			if (selectedFileList.length == 0) {
				mx.controls.Alert.show("Please select the files to download.");
			} else {	
				var paramStr:String =  selectedFileList.toXMLString();			
				var url:URLRequest = new URLRequest('FastDataTransferDownloadServlet.gx');	
				url.data = paramStr;
				url.contentType = "text/xml";
				url.method = URLRequestMethod.POST;
				navigateToURL(url, '_self');					
			}			
		}
		

 	]]>
 </mx:Script>
 
    <mx:XMLListCollection id="browseList" source="{parentDocument.browseTree.selectedItem..Request}"/>
 
		

<mx:VBox width="100%" height="100%" verticalGap="0" paddingLeft="0" paddingBottom="0" paddingRight="0" paddingTop="0">
 	<mx:VBox width="100%"  verticalGap="0" paddingTop="2">


	        <mx:HBox id="filterListView"  width="100%" horizontalAlign="right" horizontalGap="0">
		        <mx:HBox horizontalGap="0" paddingTop="0" paddingRight="0" horizontalAlign="right" paddingLeft="2" verticalAlign="middle">
			        <mx:Label text="Experiment #"/>
			        <mx:ComboBox dataProvider="{parentDocument.requestNumbers}" id="requestNumberCombo" change="filterList()" width="80" >					    		
			        </mx:ComboBox> 
		        </mx:HBox>
	        	
	        </mx:HBox>




	        <mx:TabBar dataProvider="{theViews}" 
	        	itemClick="onViewSwitch()" 
	        	useHandCursor="true" paddingBottom="0" paddingTop="2" height="25" styleName="formTabBar">
	        </mx:TabBar>
 					    
	</mx:VBox>

		        
	<mx:VBox width="100%" height="100%" styleName="formTabBody"  paddingLeft="4" paddingRight="4" paddingTop="4" paddingBottom="4" verticalGap="0">
		<mx:ViewStack id="theViews"  paddingTop="0" width="100%" height="100%"  paddingLeft="0">
		       <mx:VBox id="experimentListView" width="100%" height="100%" 
		       	label="Experiments" paddingLeft="4" paddingBottom="4" paddingRight="4" paddingTop="4">
			       <mx:DataGrid  id="experimentList" 
			       	dataProvider="{browseList}"  
			       	width="100%" height="100%"  			       	 
			       	itemDoubleClick="onExperimentRowSelected()" 
			       	doubleClickEnabled="true">
			         <mx:columns>
			            <mx:DataGridColumn   minWidth="28" width="28" headerText="#"  sortCompareFunction="sortRequestNumber">
			                <mx:itemRenderer>
			                    <mx:Component >
			                        <mx:HBox  horizontalGap="0"   horizontalScrollPolicy="off">
			                          <mx:Image id="experimentImage"  
			                          	source="{data.@icon}" >
			                    	 </mx:Image> 
			                           <mx:Label id="requestNumber" text="{data.@requestNumber}">
			                           </mx:Label>
			                        </mx:HBox>
			                    </mx:Component>
			                </mx:itemRenderer>
			            </mx:DataGridColumn> 
				       	<mx:DataGridColumn headerText="Date" width="20" dataField="@requestCreateDateDisplay"/>
				        <mx:DataGridColumn headerText="Requester" width="40" labelFunction="getFullName" sortCompareFunction="sortFullName"/>
				       	<mx:DataGridColumn headerText="Project" width="40" dataField="@projectName"/>
				       	<mx:DataGridColumn headerText="Experiment Kind" width="40" labelFunction="getExperimentKind" sortCompareFunction="sortExperimentKind" />
				       	<mx:DataGridColumn headerText="Microarray" width="100" dataField="@slideProductName"/>
			            <mx:DataGridColumn width="10" headerText="Analysis?" >
			                <mx:itemRenderer>
			                    <mx:Component >
			                        <mx:HBox  horizontalAlign="center" horizontalGap="1" >
			                          <mx:Image source="{data.@analysisNames != '' ? parentApplication.iconCheck : ''}">
			                    	 </mx:Image> 
			                        </mx:HBox>
			                    </mx:Component>
			                </mx:itemRenderer>
			            </mx:DataGridColumn> 
				       	<mx:DataGridColumn headerText="Analysis Names" width="40" dataField="@analysisNames"/>
			         </mx:columns>
			       </mx:DataGrid> 	    
		       </mx:VBox>
			       
				<experiment:ExperimentDownloadView id="downloadView" 
					label="Downloads" 
					 borderStyle="none">
				</experiment:ExperimentDownloadView>
				
	 			<experiment:ExperimentTrackView id="trackView" label="Progress"  >
				</experiment:ExperimentTrackView>
				
	 			<experiment:ExperimentVisibilityView id="visibilityView" label="Visibility">
				</experiment:ExperimentVisibilityView>
				
				<experiment:ProjectDetailView id="projectDetailView" label="Project"  >
			     </experiment:ProjectDetailView>   
		</mx:ViewStack>
			
	</mx:VBox>	    	 
	
</mx:VBox>		     
		
	<mx:ControlBar width="100%"   height="34">
		<mx:HBox height="100%" verticalAlign="bottom" width="100%" horizontalAlign="left" horizontalGap="0">
			<mx:ViewStack  id="buttonStack"  width="100%" >
	            <mx:HBox horizontalAlign="left" id="browseButtonView" horizontalGap="2" width="100%" height="100%" verticalAlign="bottom">
 					<mx:Button  visible="false"/>
 	            </mx:HBox>
	            <mx:HBox  verticalAlign="middle"  horizontalAlign="right" id="browseProjectButtonView">
	            </mx:HBox>
	            <mx:HBox horizontalAlign="right" id="saveProjectButtonView" horizontalGap="2" verticalAlign="middle">
			        <util:DirtyNote id="dirtyProject"/>
		            <mx:Button id="saveProjectButton" label="Save" 
		            	icon="@Embed('../../assets/action_save.gif')" disabledIcon="@Embed(source='../../assets/save_disable.png')" 
		            	click="projectDetailView.save()"  toolTip="Save project">
		                  </mx:Button>
		            
	            </mx:HBox>
	            <mx:HBox  verticalAlign="middle" horizontalAlign="left" id="trackButtonView" horizontalGap="2">
	                <mx:Button label="Button" visible="false"/>
	            </mx:HBox>
	        	<mx:HBox  verticalAlign="middle" horizontalAlign="right" id="saveVisibilityButtonView" height="100%">
			        <util:DirtyNote id="dirtyVisibility"/>
		        	<mx:Button label="Save" id="saveVisibilityButton"
		        		icon="@Embed('../../assets/action_save.gif')" disabledIcon="@Embed(source='../../assets/save_disable.png')" 
		        		click="visibilityView.save()"  visible="true" toolTip="Save visibility for project and experiments">
		        	      </mx:Button>
	        	</mx:HBox>
	            <mx:HBox  verticalAlign="middle" horizontalAlign="left" id="downloadExpandedFilesButtonView" height="100%">
		            <mx:Button id="downloadExpandedFilesButton" 
		             	click="downloadView.downloadExpandedFiles()" 
		             	icon="@Embed('../../assets/download.png')" 
		             	toolTip="Download selected files"
		             	label="Download"/>
					<!--mx:LinkButton label="Download large files" textDecoration="underline" click="{this.showAlternateDownloadPage()}" icon="@Embed(source='../../assets/download_large.png')"/-->
					<mx:LinkButton label="Download - Fast Data Transfer" textDecoration="underline" click="{this.showFastDataTransferDownloadPage()}" icon="@Embed(source='../../assets/download_large.png')"/>
		            <mx:VBox styleName="hintBox">
			            <mx:LinkButton  label="having trouble downloading?" textDecoration="underline" click="showDownloadHints()" styleName="hintLink" icon="@Embed(source='../../assets/information.png')"/>		            	
		            </mx:VBox>
	            </mx:HBox>		                    
	        </mx:ViewStack>
		</mx:HBox>
	</mx:ControlBar>
	

</mx:VBox>



