<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" xmlns:views="views.*" xmlns:util="views.util.*">

 
   <mx:HTTPService  
    id="cacheFileDownloadList" 
    url="CacheFileDownloadList.gx"
    destination="cacheFileDownloadList"
    resultFormat="e4x"
    showBusyCursor="false"
    result="onCacheFileDownloadList(event)"
	fault="parentApplication.onFailHttpRequest('Failed to get cache file download list', event)"
    method="POST"
    useProxy="false">
    <mx:request>
    	<fileDescriptorXMLString>{selectedFileList.toXMLString()}</fileDescriptorXMLString>
    </mx:request>
  </mx:HTTPService>
  
    
    <mx:HTTPService  
    id="getRequestDownloadList" 
    url="GetRequestDownloadList.gx"
    destination="getRequestDownloadList"
    resultFormat="e4x"
    showBusyCursor="true"
    result="onGetRequestDownloadList(event)"
	fault="parentApplication.onFailHttpRequest('Failed to get request download list', event)"
    method="POST"
    useProxy="false">
    </mx:HTTPService >	
    

 <mx:Script>
 <![CDATA[
 	import mx.events.CloseEvent;
 	import views.experiment.DownloadProgressView;
 	import mx.formatters.DateFormatter;
    import mx.rpc.events.ResultEvent;
 	import flash.display.Sprite;
    import flash.net.FileReference;
    import flash.net.URLRequest;
    import flash.net.URLRequestMethod;
    import flash.net.URLVariables;
    import mx.managers.PopUpManager;
    import mx.events.ListEvent;
    import mx.controls.Alert;
    import hci.flex.controls.CheckBox;
    import views.renderers.CheckBoxGroupedItem;
    import mx.events.CollectionEvent;
    import mx.events.CollectionEventKind;
    import mx.events.PropertyChangeEvent;
        
    public var downloadProgressWindow:DownloadProgressView;


 	[Bindable]
    public var rowCountMessage:String = new String();
    
    [Bindable]
    public var downloadSizeMessage:String = new String();
    
    [Bindable]
    public var browseMessage:String = new String();
    
    
    [Bindable]
    private var selectedFileList:XMLListCollection;
    
    private var estimatedDownloadSize:Number;
    
    private const DOWNLOAD_FOLDERS_URL:String  = "DownloadResultsServlet.gx"; 
    private const DOWNLOAD_FILES_URL:String    = "DownloadFileServlet.gx"; 
	private const SUGGESTED_LOCAL_FILENAME:String = "gnomex-data";  
       
    [Bindable]
    public var resultKeys:String;
    
    private var filesExceedingMaxMessage:String = "";
    
  	private static const  KB:Number = Math.pow(2, 10);
  	private static const  MB:Number = Math.pow(2, 20);
  	private static const  GB:Number = Math.pow(2, 30);
  	public static const   MAX_DOWNLOAD_SIZE:Number = (4 * GB);
  	
  	public static const   ALERT_BUTTON_WIDTH:int = Alert.buttonWidth;
      
    
    private var dateFormatter:DateFormatter = new DateFormatter();
        
    private function onGetRequestDownloadList(event:ResultEvent): void {
		if (getRequestDownloadList.lastResult.name() != "RequestDownloadList") {
        	Alert.show(getRequestDownloadList.lastResult..ACTMESSAGE.@TEXT);
        	return;
        }    	 
        this.downloadTree.visible = true;
        setFileCount();
        callLater(expandNodes);
    	parentDocument.filterList();
		this.downloadList.removeEventListener(CollectionEvent.COLLECTION_CHANGE, this.underlyingDownloadsChange);
		this.downloadList.addEventListener(CollectionEvent.COLLECTION_CHANGE, this.underlyingDownloadsChange);
    		
    }
    
    private function expandNodes():void {
    	for each (var item3:Object in getRequestDownloadList.lastResult..FileDescriptor.(@type == "dir")) {
    		downloadTree.expandItem(item3, false);
    	}
    }

    private function getSelectedFolders():void {
		resultKeys = "";
    	for(var i:Number = 0; i < downloadList.length; i++) {
    		if (downloadList[i].@isSelected != null && downloadList[i].@isSelected.toString() == "true") {
        		resultKeys += downloadList[i].@key + ":"; 
      		}
        }     	
    }
    
    
 	public function downloadExpandedFiles():void {
		var selectedFiles:XMLList = getRequestDownloadList.lastResult..FileDescriptor.(@isSelected == "true");        	
    	selectedFileList = new XMLListCollection(selectedFiles);
    	
    	filesExceedingMaxMessage = "";
    	if (selectedFileList.length == 0) {
			Alert.show("Please select the files to download.");
    	} else {
	    	estimatedDownloadSize = 0;
    		for each (var item:Object in selectedFileList) {
    			var size:Number = getEstimatedCompressedSize(item);
    			if (size > MAX_DOWNLOAD_SIZE) {
    				if (filesExceedingMaxMessage.length > 0) {
    					filesExceedingMaxMessage += ", ";
    				}
    				filesExceedingMaxMessage += item.@displayName;
    			}
				estimatedDownloadSize += size;
			}
    	
    		var call:Object = cacheFileDownloadList.send();
    		call.marker = estimatedDownloadSize;    		
    	}
 	}
            
 	
 	private function getDownloadName(item:Object, col:int):String {
 		var empty:String = item.name() == "Request" && item.hasOwnProperty("@isEmpty") && item.@isEmpty == "Y" ? " (no downloads)" : "";
 		if (item.name() == "RequestDownload") {
 			if (item.hasOwnProperty("@itemNumber") && item.@itemNumber != '') {
 				return item.@itemNumber + " - " + item.@results + empty;
 			} else {
		    	return item.@results + empty;
 			}
 		} else {
 			return item.@displayName + empty;
 		} 
    }

    public function setFileCount():void {
    	rowCountMessage = getRequestDownloadList.lastResult..FileDescriptor.(@type != 'dir').length() + " file(s).";    	
    }
    
    
    
    private function onCacheFileDownloadList(event:ResultEvent):void {
    	var call:Object = event.token;
    	var message:String = "";
    	var title:String = "";
    	var estimatedSize:Number = call.marker;
    	if (filesExceedingMaxMessage != "") {
    		message =  
    			"The following files exceed the 4 GB maximum:\n" + this.filesExceedingMaxMessage + ".\n\nDo you want to be directed to the browser page for downloading large files individually?"
    		title = "Download file exceeds maximum size"; 
			Alert.show( message,
				title,
				(Alert.OK | Alert.CANCEL), this, 
				onAlternateDownloadFiles,						
				parentApplication.iconWarning);

    	} else if (estimatedSize >= MAX_DOWNLOAD_SIZE) {
    		message =  
    			"The total download size (" + views.experiment.ExperimentDownloadView.getFileSizeText(call.marker) + ") exceeds the 4 GB maximum.\n\nYou must unselect some files or click on the 'Download large files' link to download files individually."
    		title = "Total download size exceeds maximum size"; 
			Alert.show( message, title);

    	} else {
    		message = "Ready to download approximately " + views.experiment.ExperimentDownloadView.getFileSizeText(call.marker) + " of data.  Proceed?";
			Alert.show( message,
						title,
						(Alert.YES | Alert.NO), this, 
						onDownloadFiles,						
						parentApplication.iconWarning);
    	}
	
    	
    }
    

	private function onDownloadFiles(event:CloseEvent):void {
		if (event.detail==Alert.YES) {
			var parms:URLVariables = new URLVariables();
			parms.mode =  "zip";
	        var progressWindow:DownloadProgressView = new DownloadProgressView();
    		progressWindow.init(this, this.DOWNLOAD_FILES_URL, parms, parentDocument.downloadExpandedFilesButton, estimatedDownloadSize, this.SUGGESTED_LOCAL_FILENAME);		
		}
	}    

	private function onAlternateDownloadFiles(event:CloseEvent):void {
		if (event.detail==Alert.OK) {
			parentDocument.showAlternateDownloadPage();	
		} 		
	}
    
   

 	public static function getEstimatedCompressedSize(item:Object):Number {
 		var compressionRatio:Number = 1; 
 		if (item.@type == 'fep') {
 			compressionRatio = 1.6; 
 		} else if (item.@type.toString().toUpperCase() == 'PDF') {
 			compressionRatio = 1;
 		} else if (item.@type.toString().toUpperCase() == 'TIF' ||
 		           item.@type.toString().toUpperCase() == 'TIFF') {
 			compressionRatio = 1.9;
 		} else if (item.@type.toString().toUpperCase() == 'JPG') {
 			compressionRatio = 1;
 		} else if (item.@type.toString().toUpperCase() == 'TXT') {
 			compressionRatio = 2.7;  
 		} else if (item.@type.toString().toUpperCase() == 'RTF') {
 			compressionRatio = 2.7;
 		} else if (item.@type.toString().toUpperCase() == 'DAT') {
 			compressionRatio = 1.6;
 		} else if (item.@type.toString().toUpperCase() == 'CEL') { 
 			compressionRatio = 2.8;
 		} else if (item.@type.toString().toUpperCase() == 'ZIP') {
 			compressionRatio = 1;
 		}
 		
 		return (item.@fileSize / compressionRatio);
 	}
    
    public static function getFileSizeText(theFileSize:Number):String {
    	var size:Number = 0;
    	var sizeTxt:String = "";
    	if (theFileSize > GB ) {
    		// Round gigabyte to the one decimal place
      		size = Math.round((theFileSize / GB) * 10) / 10;
      		if (size == 0) {
      			size = 1;
      		} 
      		sizeTxt = size + " GB";
    	}  else if (theFileSize > MB ) {
    		// Round megabyte to neareast whole number
      		size = Math.round(theFileSize / MB);
      		if (size == 0) {
      			size = 1;
      		}
      		sizeTxt = size + " MB";
    	} else if (theFileSize > KB ) {
    		// Round kb to the nearest 100th place
      		size = Math.round((theFileSize / KB) / 100) * 100;
      		if (size == 0) {
      			size = 1;
      		}
      		sizeTxt = size + " KB";
    	} else {
    		// Round bytes to nearest 100th place
      		size = Math.round(theFileSize / 100) * 100;
			if (size == 0) {
      			size = 1;
      		}
      		sizeTxt = size + " bytes";
    	}
    	return sizeTxt;
  	}
  	public function getTreeIcon(item:Object):Class {
		if (item.name() == "Request" && item.@codeRequestCategory == "QC") {
			return parentApplication.iconQC;
		} else if (item.name() == "Request" && item.@codeRequestCategory == "SOLEXA") {
			return parentApplication.iconSolexa;
		} else if (item.name() == "Request" && item.@codeRequestCategory == "HISEQ") {
			return parentApplication.iconHiSeq;
		} else if (item.name() == "Request" && item.@codeRequestCategory == "AFFY") {
			return parentApplication.iconMicroarrayAffy;
		}else if (item.name() == "Request" && item.@codeRequestCategory == "AGIL") {
			return parentApplication.iconMicroarray;
		} else if (item.name() == "Request" && item.@codeRequestCategory == "AGIL1") {
			return parentApplication.iconMicroarraySingleColor;
		} else if (item.name() == "RequestDownload") {
			if (item.hasOwnProperty("@isEmpty") && item.@isEmpty == "Y") {
				return parentApplication.iconFolderDisabled;
			} else {
				return parentApplication.iconFolder;			
			}
		} else if (item.name() == "FileDescriptor" && item.hasOwnProperty("@type") && item.@type == "dir") {
				return parentApplication.iconFolder;
		} else {
			return null;
		}
	}
	
	public function selectDownload(requestNumber:String):void {
		var pos:int = -1;
		for each(var r:Object in downloadList) {
			if (r.@requestNumber == requestNumber) {
				downloadTree.selectedItem = r;
				break;
			}
			pos++;
			for each(var rd:Object in r.RequestDownload) {
				pos++;
				for each(var fd:Object in rd.FileDescriptor) {
					pos++;
				}
			}
		}
		
		if (pos >= 0) {
			downloadTree.scrollToIndex(pos);
		}
	}

		
	private function underlyingDownloadsChange(event:CollectionEvent):void {
		if (event.kind == CollectionEventKind.RESET || event.kind == CollectionEventKind.REFRESH) {
			// ignore refresh and resets 
			return;
		}
		
		var selectedDownloadSize:Number = 0;
		var selectedFiles:XMLList = getRequestDownloadList.lastResult..FileDescriptor.(@isSelected == "true");        	
    	for each(var file:Object in selectedFiles) {
    		if (file.@type != "dir") {
    			var size:Number = getEstimatedCompressedSize(file);
    			selectedDownloadSize += size;
    		}
    	}
    	downloadSizeMessage = "Ready to download " + getFileSizeText(selectedDownloadSize);

	}

    
 ]]>
 </mx:Script>
 
    <mx:XMLListCollection id="downloadList" source="{getRequestDownloadList.lastResult.Request}"/>
	
	<mx:VBox width="100%" height="100%" paddingBottom="0" paddingLeft="4" paddingRight="4" paddingTop="0" verticalGap="0">
		<mx:HBox width="100%">
			<mx:Label text="{downloadSizeMessage}" fontWeight="normal" fontStyle="normal" width="200"/>
			<mx:Spacer width="10"/>
			<mx:HBox width="100%" horizontalAlign="center" styleName="warningLabel">
				<mx:Label text="{browseMessage}"/>			
			</mx:HBox>
			<mx:Spacer width="210"/>
		</mx:HBox>
		<mx:AdvancedDataGrid id="downloadTree" width="100%" height="100%" 
                 groupItemRenderer="views.renderers.CheckBoxGroupedItem"
				 dropEnabled="false"  
				 displayItemsExpanded="true"
				 iconFunction="getTreeIcon"
				 dragEnabled="false" 
				 dragMoveEnabled="false"  
				 showHeaders="true"  
				 sortExpertMode="true" 
				 sortableColumns="false"
				 borderStyle="solid" 
				 headerWordWrap="false" 
				 resizableColumns="true">						
	 	        <mx:dataProvider> 
  				  <mx:HierarchicalData source="{downloadList}"/>
		        </mx:dataProvider>						  
		        <mx:columns>
  				  <mx:AdvancedDataGridColumn dataField="@displayName"  labelFunction="getDownloadName"  headerText="Folder or File" width="250"/> 
  				  <mx:AdvancedDataGridColumn dataField="@viewURL" headerText="" width="25" itemRenderer="views.renderers.LinkButtonViewFile"/>
  				  <mx:AdvancedDataGridColumn dataField="@info" headerText="Info" width="80"/>
  				  <mx:AdvancedDataGridColumn dataField="@fileSizeText"  headerText="Size" width="40" textAlign="right" /> 
  				  <mx:AdvancedDataGridColumn dataField="@lastModifyDateDisplay"  headerText="Modified" width="50"/> 
  			    </mx:columns>
  			</mx:AdvancedDataGrid>
		<mx:Label text="{rowCountMessage}" width="100%" textAlign="right"/>

	</mx:VBox>
	



</mx:Canvas>


