<?xml version="1.0" encoding="utf-8"?>
<util:ResizableTitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" 
						   layout="vertical" width="1000" 
						   xmlns:util="views.util.*"
						   height="408" creationPolicy="all"
						   showCloseButton="true" creationComplete="centerMe()"
						   close="{closeMe()}" 
						   title="{'Upload Files for Experiment ' + requestNumber}"  xmlns:controls="hci.flex.controls.*">
	
	<mx:HTTPService  
		id="getExperimentUploadURL" 
		url="UploadExperimentURLServlet.gx"
		resultFormat="e4x"
		showBusyCursor="true" 
		result="onGetExperimentUploadURL(event)" 
		fault="parentApplication.onFailHttpRequest('Failed to get experiment upload URL', event)"
		method="POST"
		useProxy="false">
	</mx:HTTPService>
	
	
	<mx:HTTPService  
		id="fdtUploadStart" 
		url="FastDataTransferUploadStart.gx"
		resultFormat="e4x"
		showBusyCursor="true" 
		result="onFdtUploadStart(event)" 
		fault="parentApplication.onFailHttpRequest('Failed to start FDT upload', event)"
		method="POST"
		useProxy="false">
	</mx:HTTPService>
	
	
	
	<mx:HTTPService  
		id="getRequest" 
		url="GetRequest.gx"
		resultFormat="e4x"
		showBusyCursor="true" 
		result="onGetRequest(event)" 
		fault="parentApplication.onFailHttpRequest('Failed to get experiment', event)"
		method="POST"
		useProxy="false">
	</mx:HTTPService>
	
	<mx:HTTPService  
		id="getRequestDownloadList" 
		url="GetRequestDownloadList.gx"
		destination="getRequestDownloadList"
		resultFormat="e4x"
		showBusyCursor="true"
		result="onGetRequestDownloadList(event)"
		fault="parentApplication.onFailHttpRequest('Failed to get request download list', event)"
		method="POST"
		useProxy="false">
	</mx:HTTPService >
	
	<mx:HTTPService  
		id="organizeExperimentUploadFiles" 
		url="OrganizeExperimentUploadFiles.gx"
		resultFormat="e4x"
		showBusyCursor="true"
		result="onOrganizeExperimentUploadFiles(event)"
		fault="parentApplication.onFailHttpRequest('Failed to organize experiment upload files', event)"
		method="POST"
		useProxy="false">
	</mx:HTTPService >
	
	<mx:HTTPService  
		id="getLinkedSampleFiles" 
		url="GetLinkedSampleFiles.gx"
		resultFormat="e4x"
		showBusyCursor="true"
		result="onGetLinkedSampleFiles(event)"
		fault="parentApplication.onFailHttpRequest('Failed to link sample files', event)"
		method="POST"
		useProxy="false">
		<mx:request>
			<idRequest>{this.idRequest}</idRequest>
		</mx:request>
	</mx:HTTPService >
	
	<mx:Script>
		<![CDATA[
			import mx.collections.HierarchicalData;
			import mx.collections.Sort;
			import mx.collections.SortField;
			import mx.controls.Alert;
			import mx.controls.List;
			import mx.controls.listClasses.ListBase;
			import mx.core.DragSource;
			import mx.events.CloseEvent;
			import mx.events.DragEvent;
			import mx.events.ItemClickEvent;
			import mx.events.ListEvent;
			import mx.managers.DragManager;
			import mx.managers.PopUpManager;
			import mx.rpc.events.ResultEvent;
			
			import views.util.MultiFileUpload;
			
			[Bindable]
			public var idRequest:Object;
			
			[Bindable]
			private var requestNumber:Object;
			
			[Bindable]
			private var request:Object;
			
			[Bindable]
			public var parentView:ExperimentEditView = new ExperimentEditView;
			
			[Bindable]
			private var sampleList:XMLListCollection = new XMLListCollection();
			
			[Bindable]
			private var experimentFileList:XMLListCollection = new XMLListCollection();
			
			[Bindable]
			private var masterFileList:XMLListCollection = new XMLListCollection();
			
			private var multiFileUpload:MultiFileUpload;
			
			private var expFileDict:Dictionary = new Dictionary();
			
			private var downloadDraggedItems:Array = null;
			
			private var clickedItems:XMLListCollection = new XMLListCollection();
			
			private var treeItemToSelect:Object = null;
			
			private var selectedExpFileTreeIndex:int = 0;
			
			private var linkedSampleFiles:XMLListCollection = new XMLListCollection();
			
			[Bindable]
			private var SAMPLE_GROUP_FOLDER_NAME:String = "SampleGroup";
			[Bindable]
			private var SAMPLE_NAME:String = "Sample";
			
			private var SEQ_RUN_FOLDER_NAME:String = "SeqRunNumber";
			private var FILE_DESCRIPTOR_NAME:String = "FileDescriptor";
			
			public function init(request:Object):void {
				this.idRequest = request.@idRequest;
				this.requestNumber = request.@requestNumber;
				this.request = request;
				
				//remove linkSamplesTab if property not enabled or if the experiment isn't hiseq or miseq
				if(parentApplication.getProperty(parentApplication.PROPERTY_EXPERIMENT_FILE_SAMPLE_LINKING_ENABLED) == null || 
					parentApplication.getProperty(parentApplication.PROPERTY_EXPERIMENT_FILE_SAMPLE_LINKING_ENABLED) == 'N'){
					theTab.removeChildAt(2);
				}
				
				fdtStartButton.enabled = true;
				saveUUID.text = "";
				
				getExperimentUploadURL.send();
				
				if(theTab.selectedIndex == 2){
					getLinkedSampleFiles.send();
				}
				
				var params:Object = new Object();
				params.idRequest = idRequest;
				params.includeUploadStagingDir = 'N';
				getRequestDownloadList.send(params);
				refreshUploadFiles();
				this.addEventListener("refreshUploadFiles", onUploadFinished);
			}
			
			public function centerMe():void{
				PopUpManager.centerPopUp(this);
			}
			
			private function refreshFiles():void {
				expFileDict = new Dictionary();
				filesToRemove.removeAll();
				filesToUnlink.removeAll();
				this.linkedSampleFiles.removeAll();
				linkSamplesTab.enabled = true;
				this.dirty.resetDirty();
				
				refreshUploadFiles();
				
				var params:Object = new Object();
				params.idRequest = idRequest;
				params.includeUploadStagingDir = 'N';
				getRequestDownloadList.send(params);
				
			}
			
			private function onGetExperimentUploadURL(event:ResultEvent):void {
				if (getExperimentUploadURL.lastResult.name() == "UploadExperimentURL") {
					var url:String = getExperimentUploadURL.lastResult.@url;
					initializeFileUploadControl(url);
				}
			}
			
			
			private function onFdtUploadStart(event:ResultEvent):void {
				if (fdtUploadStart.lastResult.name() == "FDTUploadUuid") {
					var uuid:String = fdtUploadStart.lastResult.@uuid;
					saveUUID.text = uuid;
					fdtStartButton.enabled = false;
					
					var url:URLRequest = new URLRequest('FastDataTransferUploadGetJnlpServlet.gx');
					var uv:URLVariables = new URLVariables();
					uv.uuid = uuid;
					url.data = uv;
					url.method = URLRequestMethod.POST;
					navigateToURL(url, '_self');				
				}
			}
			
			
			public function initializeFileUploadControl(uploadServletURL:String):void{
				
				if (multiFileUpload == null) {
					var postVariables:URLVariables = new URLVariables;
					postVariables.idRequest = idRequest;
					postVariables.requestNumber = requestNumber; 
					
					multiFileUpload = new MultiFileUpload(
						this.uploadFilesGrid,
						this.uploadBrowseButton,
						this.uploadClearButton,
						this.uploadDeleteButton,
						this.uploadButton,
						this.uploadProgressBar,
						uploadServletURL,
						postVariables,
						0,
						null
					);
					
					multiFileUpload.addEventListener(Event.COMPLETE, onUploadFinished);
					multiFileUpload.addEventListener(Event.OPEN, onChooseFileToUpload);
					
					this.uploadBrowseButton.enabled = true;
					this.uploadClearButton.enabled = true;
					this.uploadDeleteButton.enabled = true;
				} 
				
			}
			
			public function refreshUploadFiles():void {
				if(idRequest != null){  
					var params:Object = new Object();
					params.idRequest = idRequest;
					params.showUploads = "Y";
					getRequest.send(params);
				}
			}
			
			public function onUploadFinished(event:Event):void{
				this.theTab.selectedIndex = 1;
				refreshUploadFiles();
				refreshFiles();
			}
			
			private function onGetRequest(event:ResultEvent): void {
				if (getRequest.lastResult.name() == "OpenRequestList") {
					newFolderButton.enabled = true;
					sortFileList(uploadedFiles);
					for each(var fd:XML in uploadedFiles){
						expFileDict[String(fd.@displayName)] = fd;
					}
					constructExperimentFileList();
				}
			}
			
			private function onGetLinkedSampleFiles(event:ResultEvent): void {
				if (getLinkedSampleFiles.lastResult.name() == "SampleList") {
					sampleList = new XMLListCollection(XMLList(getLinkedSampleFiles.lastResult.children()));
				} else{
					mx.controls.Alert.show(getLinkedSampleFiles.lastResult..ACTMESSAGE.@TEXT);
				}
			}
			
			public function onChooseFileToUpload(event:Event):void {
			}
			
			
			private function onGetRequestDownloadList(event:ResultEvent): void {
				if (getRequestDownloadList.lastResult.name() != "RequestDownloadList") {
					mx.controls.Alert.show(getRequestDownloadList.lastResult..ACTMESSAGE.@TEXT);
					return;
				} else {
					recurseGetFileDescriptors(downloadList.children());
					constructExperimentFileList();
					callLater(expandDownloadTree);
				}  	 
			}
			
			private function recurseGetFileDescriptors(list:XMLList):void{
				if(list.length() == 0){
					return;
				}
				for each(var fd:XML in list){
					if(fd.name() == this.FILE_DESCRIPTOR_NAME){
						expFileDict[String(fd.@displayName)] = fd.copy();
					} else if(fd.name() == 'RequestDownload'){
						recurseGetFileDescriptors(fd.children());
					}
					
				}
			}
			
			private function recurseFindFileToDelete(expFileList:XMLList, fileToFind:XML):void{
				for each(var file:XML in expFileList){
					if(file.name() == 'RequestDownload' && file.children().length() > 0){
						recurseFindFileToDelete(file.children(), fileToFind);
					}
					else if(file.name() == this.FILE_DESCRIPTOR_NAME){
						if(file.@type == 'dir'){
							recurseFindFileToDelete(file.children(), fileToFind);
						} else{
							if(file.@zipEntryName == fileToFind.@zipEntryName){
								deleteNode(file);
								return;
							}
						}
					}
				}
			}
			
			private function deleteEmptyFolders(expFile:XML):void{
				if((expFile.@type == 'dir' || expFile.name() == 'RequestDownload') && expFile.children().length() == 0){
					var parent:XML = expFile.parent(); 
					deleteNode(expFile);
					if(parent != null){
						deleteEmptyFolders(parent);
					}
				} else if(expFile.@type == 'dir'){
					for each(var child:XML in expFile.children()){
						deleteEmptyFolders(child);
					}
				}
			}
			
			private function constructExperimentFileList():void{
				var list:XMLList = new XMLList(downloadList.copy());
				var uploadNode:XML = <RequestDownload itemNumber="upload_staging" />;
				for each(var uf:XML in uploadedFiles){
					uploadNode.appendChild(uf.copy());
				}
				if(uploadNode.children().length() > 0){
					list.appendChild(uploadNode);
				}
				experimentFileList = new XMLListCollection(list.copy());
				sortFileList(experimentFileList);
				masterFileList = new XMLListCollection(list.copy());
				for each(var s:XML in sampleList.source..FileDescriptor){
					recurseFindFileToDelete(experimentFileList.children(), s);
				}
				
				for each(var node:XML in experimentFileList.children()){
					if((node.children().length() == 0 && node.name() != this.FILE_DESCRIPTOR_NAME) || (node.children().length() == 0 && node.name() == this.FILE_DESCRIPTOR_NAME && node.@type == 'dir')){
						deleteNode(node);
					}
				}
				
				for each(var fd:XML in experimentFileList.children().children()){
					deleteEmptyFolders(fd);	
				}
				
				if(experimentFileList.length > 0){
					callLater(experimentFilesTree.expandItem, [experimentFileList.getItemAt(0), true]);
				}
				
				if(treeItemToSelect != null){
					callLater(selectTreeItem);
				}
			}
			
			private function sortFileList(theList:XMLListCollection):void{
				var sort:Sort = new Sort();
				sort.fields = [new SortField("@displayName", true)];
				theList.sort = sort;
				theList.refresh();
				for each(var child:XML in theList){
					if(child.children().length() > 0){
						var childList:XMLListCollection = new XMLListCollection(child.children());
						sortFileList(childList);
						child.setChildren(childList.copy());
					}
					
				}
				theList.refresh();
			}
			
			private function sortClickedItems():void{
				var sort:Sort = new Sort();
				sort.fields = [new SortField("@displayName", true)];
				clickedItems.sort = sort;
				clickedItems.refresh();
			}
			
			private function expandDownloadTree():void {
				var maxFiles:int = 60;
				var count:int = 0;
				if (downloadTree != null && downloadList.length > 0) {
					downloadTree.expandItem(downloadList.getItemAt(0),true); 
					for each(var a:Object in getRequestDownloadList.lastResult..Request.FileDescriptor) {
						downloadTree.expandItem(a,true);
						count++;
						if (count>=maxFiles){
							break;
						}
					}
				}
			}
			
			private function save():void {
				var params:Object = new Object();
				params.idRequest = idRequest;
				params.filesXMLString = downloadList.toXMLString();
				params.linkedSampleFileXMLString = sampleList.toXMLString();
				params.experimentFileXMLString = this.linkedSampleFiles.toXMLString();
				if (filesToRemove.length > 0) {
					params.filesToRemoveXMLString = filesToRemove.toXMLString();			
				}
				if (filesToUnlink.length > 0 ){
					params.filesToUnlinkXMLString = filesToUnlink.toXMLString();
				}
				organizeExperimentUploadFiles.send(params);
			}
			
			private function onOrganizeExperimentUploadFiles(event:ResultEvent): void {
				if (organizeExperimentUploadFiles.lastResult.name() != "SUCCESS") {
					mx.controls.Alert.show(organizeExperimentUploadFiles.lastResult..ACTMESSAGE.@TEXT);
				} else {
					if(theTab.selectedIndex == 2){
						getLinkedSampleFiles.send();
					}
					refreshFiles();
				} 	 
			}
			private function getFilesGridRowNumber(item:Object,col:int):String
			{
				var x:int = filesGrid.dataProvider.getItemIndex(item) + 1;
				return String(x);
			}
			
			private function selectTreeItem():void {
				if (treeItemToSelect != null && recurseFindFile(treeItemToSelect as XML)) {
					recurseExpandParent(treeItemToSelect);
					experimentFilesTree.validateNow();
					callLater(autoScrollTree);
				} else {
					experimentFilesTree.selectedItem = null;
				}
			}
			
			private function autoScrollTree():void{
				experimentFilesTree.scrollToIndex(selectedExpFileTreeIndex);
			}
			
			private function recurseExpandParent(item:Object):void {
				if (item.name() == 'RequestDownload') {
					experimentFilesTree.expandItem(XML(experimentFilesTree.dataProvider.source.RequestDownload.(@itemNumber == item.@itemNumber)), true);				
				} else if(item.name() == this.FILE_DESCRIPTOR_NAME && item.@type == 'dir'){
					experimentFilesTree.expandItem(XML(experimentFilesTree.dataProvider.source..FileDescriptor.(@displayName == item.@displayName)), true);
				}
				if (item.parent() != null) {
					recurseExpandParent(item.parent());									
				}
				
			}
			
			private function recurseFindFile(fileToFind:XML):Boolean{
				
				for each(var file:XML in experimentFileList.children()){
					if(file.name() == 'RequestDownload' && file.@itemNumber == fileToFind.@itemNumber){
						return true;
					} else if (file.name() == 'RequestDownload' && file.children().length() > 0){
						recurseFindFileToDelete(file.children(), fileToFind);
					} else if(file.name() == this.FILE_DESCRIPTOR_NAME && file.@displayName == fileToFind.@displayName ){
						return true;
					} else if(file.name() == this.FILE_DESCRIPTOR_NAME && file.@type == 'dir' && file.children().length() > 0){
						recurseFindFileToDelete(file.children(), fileToFind);
					} else{
						return false;
					}
				}
				return false;
			}
			
			
			private function getDownloadName(item:Object):String {
				var info:String = item.name() == "RequestDownload" && item.hasOwnProperty("@info") && item.@info != "" ? " - " + item.@info : "";
				if (item.name() == "RequestDownload") {
					if (item.hasOwnProperty("@itemNumber") && item.@itemNumber != '') {
						var results:String = item.hasOwnProperty("@results") && item.@results != '' ? " - " + item.@results : '';
						return item.@itemNumber + results + info;
					} else {
						return item.@results + info;
					}
				} else {
					return item.@displayName + info;
				} 
			}
			
			public function showAddFolderWindow():void {
				if (this.downloadTree.selectedItem == null) {
					this.downloadTree.selectedIndex = 0;
				}
				
				var addFolderWindow:FolderAddWindow = FolderAddWindow(PopUpManager.createPopUp(DisplayObject(parentApplication.theBody), FolderAddWindow, true));
				PopUpManager.centerPopUp(addFolderWindow);
				addFolderWindow.uploadWindow = this;
				addFolderWindow.isDownloadFolder = true;
				addFolderWindow.folderName.setFocus();
			}
			
			public function showSampleGroupAddFolderWindow():void {
				var addFolderWindow:FolderAddWindow = FolderAddWindow(PopUpManager.createPopUp(DisplayObject(parentApplication.theBody), FolderAddWindow, true));
				PopUpManager.centerPopUp(addFolderWindow);
				addFolderWindow.uploadWindow = this;
				addFolderWindow.isDownloadFolder = false;
				addFolderWindow.folderName.setFocus();
			}
			
			public function addDownloadFolder(folderName:String):void {
				
				var parent:XML = XML(downloadTree.selectedItem);
				if (this.downloadTree.selectedItem.@type != 'dir' && parent.name() != "Request" && parent.name() != "RequestDownload") {
					parent = XML(downloadTree.selectedItem).parent();
				} 
				
				var folder:XML = new XML("<RequestDownload isNew='Y' " +
					"key='X\tX\tX\t" + folderName + "' " +
					"itemNumber='" + folderName + "' " +
					"type='dir'" +
					"isEmpty='Y'" +
					"info='' />");
				if (parent.children().length() > 0) {
					var firstChild:XML = parent.children()[0];
					parent.insertChildBefore(firstChild, folder);	 		    	
				} else {
					parent.appendChild(folder);
				}	    	                         
				this.downloadTree.expandItem(parent, true);
				this.downloadTree.selectedItem = null;
				this.downloadTree.selectedItem = folder;
				this.downloadTree.scrollToIndex(this.downloadTree.selectedIndex); 
				this.downloadTree.selectedItem = null;
			}
			
			
			private function removeExperimentFile():void {
				if (this.filesGrid.selectedItems != null && this.filesGrid.selectedItems.length > 0) {
					for each(var uploadFile:XML in this.filesGrid.selectedItems) {
						this.filesToRemove.addItem(uploadFile);
						this.uploadedFiles.removeItemAt(uploadedFiles.getItemIndex(uploadFile));
					}
					linkSamplesTab.enabled = false;
					this.dirty.setDirty();
				} else if (this.downloadTree.selectedItems != null && this.downloadTree.selectedItems.length > 0) {
					for each(var downloadFile:XML in this.downloadTree.selectedItems) {
						// Only add to remove list if this file exists
						if (downloadFile.hasOwnProperty("@fileName")) {
							this.filesToRemove.addItem(downloadFile);
						}
						// Delete from XML hierarchy
						this.deleteNode(downloadFile);
						
					}
					
					linkSamplesTab.enabled = false;
					this.dirty.setDirty();
				}
			}
			
			private function onTabChange():void {
				if (theTab.selectedIndex == 1) {
					if (this.uploadedFiles == null || this.uploadedFiles.length == 0) {
						refreshFiles();
					}
				} else if (theTab.selectedIndex == 2){
					refreshFiles();
					refreshUploadFiles();
					getLinkedSampleFiles.send();
				}
			}
			
			private function onDetermineDragDrop(event:DragEvent):void{
				event.preventDefault();
				var isSampleGroupDrop:Boolean = false;
				//event.target.hideDropFeedback(event);
				//DragManager.showFeedback(DragManager.NONE);
				var dropNode:XML = null;
				
				if(samplesTree.selectedItem != null) {
					dropNode = samplesTree.selectedItem as XML;
					// Don't allow drag operation if nothing no drop node
					// found or we aren't dropping on a sample folder
					if (dropNode == null) {
						event.preventDefault();
						event.target.hideDropFeedback(event);
						DragManager.showFeedback(DragManager.NONE);
						Alert.show("You must drop files into a Seq Run Folder, a Sample Folder, or a Sample");
						return;
					}
				}
				
				if (dropNode.name() == this.FILE_DESCRIPTOR_NAME) {
					dropNode = dropNode.parent();
				}
				
				sortClickedItems();
				
				var draggedItems:Array = null;
				
				if (event.dragInitiator.name == "samplesTree") {
					// For some reason, if we are dragging within the tree, the dragged
					// items has to be detected on the dragEnter event.  By the time
					// with get to the dragDrop event, the event.draggedItems contains
					// the drop target, not the dragged items.
					draggedItems = this.downloadDraggedItems;	    
				} else {
					//var draggedFormat:String = event.dragSource.formats[0];
					//draggedItems = event.dragSource.dataForFormat(draggedFormat) as Array;
					draggedItems = clickedItems.toArray();
				}
				
				if(dropNode.name() == this.SAMPLE_GROUP_FOLDER_NAME){
					isSampleGroupDrop = true;
					for each(var di:XML in draggedItems){
						if(di.name() != this.SAMPLE_NAME && di.name() != this.SAMPLE_GROUP_FOLDER_NAME){
							Alert.show("You can only drag and drop samples to this folder.");
							clickedItems = new XMLListCollection();
							return;
						}
						if (di.name() == this.SAMPLE_GROUP_FOLDER_NAME && this.checkRecursiveParent(di, dropNode)) {
							Alert.show("A folder may not be dragged into one of it's subfolders");
							clickedItems = new XMLListCollection();
							return;
						}
					}
				} else if(dropNode.name() == this.SEQ_RUN_FOLDER_NAME || dropNode.name() == this.SAMPLE_NAME){
					for each(var dropItem:XML in draggedItems){
						if(dropItem.name() != this.FILE_DESCRIPTOR_NAME){
							Alert.show("You can only drag and drop files to this folder.");
							clickedItems = new XMLListCollection();
							return;
						}
					}
					
				}
				
				if(isSampleGroupDrop){
					onDragDropSamples(event, dropNode, draggedItems);
				} else{
					onDragDropExpFile(event, dropNode, draggedItems);
				}
				
			}
			
			private function checkRecursiveParent(dragItem:XML, dropNode:XML):Boolean {
				if (dropNode.name() == "SampleList") {
					return false;
				}
				if (dropNode.parent().@displayName == dragItem.@displayName) {
					return true;
				}
				return checkRecursiveParent(dragItem, dropNode.parent());
			}
			
			private function onDragDropSamples(event:DragEvent, dropNode:XML, draggedItems:Array):void{
				event.preventDefault();
				
				if(dropNode.name() == this.SAMPLE_GROUP_FOLDER_NAME){
					for each(var di:XML in draggedItems){
						if(di.name() == this.SAMPLE_GROUP_FOLDER_NAME && di.@displayName == dropNode.@displayName){
							return;
						}
						dropNode.appendChild(di.copy());
						deleteNode(di);
					}
					
					samplesTree.expandItem(dropNode, true);
					
				}
				
				samplesTree.selectedItem = null;
				
			}
			
			
			private function onDragDropExpFile(event:DragEvent, dropNode:XML, draggedItems:Array):void{
				event.preventDefault();

				
				if(dropNode.name() == this.SAMPLE_NAME){
					var seqRunNumberNode:XML = <SeqRunNumber />;
					if(dropNode.children().length() == 0){
						for each(var fd:XML in draggedItems){
							if(seqRunNumberNode.children().length() == 2){
								dropNode.appendChild(seqRunNumberNode);
								seqRunNumberNode = <SeqRunNumber />;
							}
							seqRunNumberNode.appendChild(fd.copy());
							this.linkedSampleFiles.addItem(fd.copy());
							if (event.dragInitiator.name == "samplesTree") {
								filesToUnlink.addItem(fd.copy());
								deleteNode(fd);
								samplesTree.selectedItem = null;
							} else{
								experimentFilesTree.selectedItem = null;
								recurseFindFileToDelete(experimentFileList.children(), fd);
							}
						}
						dropNode.appendChild(seqRunNumberNode);
						samplesTree.expandItem(dropNode, true);
						samplesTree.expandItem(seqRunNumberNode, true);
					} else{
						var seqRunNumberNode_two:XML = <SeqRunNumber />;
						for(var i:int = 0; i < draggedItems.length; i++){
							var fileDescript:XML = draggedItems[i];
							var droppedFileName:String = fileDescript.@displayName.replace(/_/gi, "");
							droppedFileName = droppedFileName.replace(/-/gi, "");
							droppedFileName = droppedFileName.substring(0, droppedFileName.indexOf(".") - 1);
							var isMatch:Boolean = false;
							
							for each(var file:XML in dropNode.children().children()){
								var currentFileName:String = file.@displayName.replace(/_/gi, "");
								currentFileName = currentFileName.replace(/-/gi, "");
								currentFileName = currentFileName.substring(0, currentFileName.indexOf(".") - 1);
								
								if(droppedFileName == currentFileName){
									if(file.parent().children().length() < 2){
										file.parent().appendChild(fileDescript.copy());
										this.linkedSampleFiles.addItem(fileDescript.copy());
										samplesTree.expandItem(dropNode, true);
										samplesTree.expandItem(file.parent(), true);
										isMatch = true;
										break;
									} else{
										break;
									}
								}
							}
							
							if(!isMatch){
								seqRunNumberNode_two.appendChild(fileDescript.copy());
								this.linkedSampleFiles.addItem(fileDescript.copy());
								samplesTree.expandItem(dropNode, true);
							}
							
							if(seqRunNumberNode_two.children().length() == 2 || (i + 1 >= draggedItems.length)){
								dropNode.appendChild(seqRunNumberNode_two);
								seqRunNumberNode_two = <SeqRunNumber />;
							}
							
							if (event.dragInitiator.name == "samplesTree") {
								if(fileDescript.@idExperimentFile != null && fileDescript.@idExperimentFile != ""){
									filesToUnlink.addItem(fileDescript.copy());
								}
								deleteNode(fileDescript);
								samplesTree.selectedItem = null;
							} else{
								recurseFindFileToDelete(experimentFileList.children(), fileDescript);
								experimentFilesTree.selectedItem = null;
							}
						}
					}
					event.target.hideDropFeedback(event);
					samplesTree.selectedItem = null;
					
				}
				
				if(dropNode.name() == this.SEQ_RUN_FOLDER_NAME){
					if(draggedItems.length == 1 && dropNode.children().length() == 2 && dropNode.children().contains(draggedItems[0])){
						var fileOne:XML = dropNode.children()[0].copy();
						var fileTwo:XML = dropNode.children()[1].copy();;
						
						deleteNode(dropNode.children()[0]);
						deleteNode(dropNode.children()[0]);
						
						dropNode.appendChild(fileTwo);
						dropNode.appendChild(fileOne);
						this.linkedSampleFiles.addItem(fileOne.copy());
						this.linkedSampleFiles.addItem(fileTwo.copy());
						
					} else if(dropNode.children().length() == 2){//Each seq run folder can have a max of 2 exp files
						event.target.hideDropFeedback(event);
						Alert.show("A Seq Run Folder can only contain two Experiment Files.  Please remove one Experiment File from this folder and try again.");
						return;
					} else{
						for(var j:int = 0; j < draggedItems.length; j++){
							var dragItem:XML = XML(draggedItems[j]);
							dropNode.appendChild(dragItem.copy());
							this.linkedSampleFiles.addItem(dragItem.copy());
							if (event.dragInitiator.name == "samplesTree") {
								if(dragItem.@idExperimentFile != null && dragItem.@idExperimentFile != ""){
									filesToUnlink.addItem(dragItem.copy());
								}
								deleteNode(dragItem);
								samplesTree.selectedItem = null;
							} else{
								recurseFindFileToDelete(experimentFileList.children(), dragItem);
								experimentFilesTree.selectedItem = null;
							}
						}
					}
				}
				
				for each(var emptyfolder:XML in sampleList.source..SeqRunNumber){
					if(emptyfolder.children().length() == 0){
						deleteNode(emptyfolder);
					}
				}
				
				clickedItems = new XMLListCollection();
				samplesTree.expandItem(dropNode, true);
				callLater(samplesTree.invalidateDisplayList);
				this.dirty.setDirty();
				event.target.hideDropFeedback(event);
			}
			
			private function onDragDropFile(event:DragEvent) : void
			{
				
				// For some reason, preventDefault on AdvancedDataGrid
				// isn't enough to stop the defaut drop operation from
				// occurring. So I added hidDropFeedback and showFeedback
				// and that seemed to do the trick.	
				//this.downloadTree.hideDropFeedback(event);
				//event.preventDefault();
				//DragManager.showFeedback(DragManager.NONE);
				
				// determine where in the grid the drop occurs and select that node by the index; 
				// followed by retrieving the node itself. 
				var dropNode:XML = null;
				var dropFolderNode:XML = null;
				var siblingDropNode:XML = null;
				//var r:int = downloadTree.calculateDropIndex(event);
				//if (r > 0) { 
				//  r--;
				//}
				//downloadTree.selectedIndex = r;
				if(downloadTree.selectedItem != null) {
					dropNode = downloadTree.selectedItem as XML;
					
					// Don't allow drag operation if nothing no drop node
					// found
					if (dropNode == null) {
						event.preventDefault();
						event.target.hideDropFeedback(event);
						DragManager.showFeedback(DragManager.NONE);
						return;
					}
					
					dropFolderNode = dropNode;
					if (dropNode.name() == this.FILE_DESCRIPTOR_NAME && dropNode.@type != "dir") {
						dropFolderNode = dropNode.parent();
						siblingDropNode = dropNode;
					}
					// Don't allow drag operation if nothing no drop folder node
					// found
					if (dropFolderNode == null) {
						event.preventDefault();
						event.target.hideDropFeedback(event);
						DragManager.showFeedback(DragManager.NONE);
						return;
					}
					
					// get the drag format (its always items in our case
					// Get the dragged items
					var draggedItems:Array = null;		    
					if (event.dragInitiator.name == "downloadTree") {
						// For some reason, if we are dragging within the tree, the dragged
						// items has to be detected on the dragEnter event.  By the time
						// with get to the dragDrop event, the event.draggedItems contains
						// the drop target, not the dragged items.
						draggedItems = this.downloadDraggedItems;	    
					} else {
						var draggedFormat:String = event.dragSource.formats[0];
						draggedItems = event.dragSource.dataForFormat(draggedFormat) as Array;	
					}
					for(var i:int = 0; i < draggedItems.length; i++) {
						var dragItem:XML = XML(draggedItems[i]);
						if (dragItem.name() == this.FILE_DESCRIPTOR_NAME || dragItem.name() == "RequestDownload") {
							var fileNode:XML;
							if (dragItem.name() == this.FILE_DESCRIPTOR_NAME ){
								fileNode = new XML("<FileDescriptor " +
									" displayName='" +  dragItem.@displayName + "'" + 
									" fileName='" +  dragItem.@fileName + "'" + 
									" type='" + dragItem.@type + "'" +
									" directoryName='" + dragItem.@directoryName + "'/>");
							}
							if (dragItem.name() == "RequestDownload" ){
								fileNode = new XML("<FileDescriptor " +
									" displayName='" +  dragItem.@itemNumber + "'" + 
									" fileName='" +  dragItem.@fileName + "'" + 
									" type='dir'" +
									" directoryName='" + dragItem.@directoryName + "'/>");
							}
							
							
							for each ( var child:Object in dragItem.children() ) {
								fileNode.appendChild(child);	
							}
							
							// Don't allow dropping on yourself (causes a cyclical error)
							if ( fileNode.@fileName == dropFolderNode.@fileName ){
								return;
							}
							
							if (siblingDropNode != null) {
								dropFolderNode.insertChildAfter(siblingDropNode, fileNode);	       				
							} else {
								dropFolderNode.appendChild(fileNode);
							}
							downloadTree.expandItem(dropFolderNode, true);
							
							// We have to manually remove the file when we are
							// dragging within the download tree.
							if (event.dragInitiator.name == "downloadTree") {
								deleteNode(dragItem);
								downloadTree.selectedItem = null;
							}
							linkSamplesTab.enabled = false;
							this.dirty.setDirty();
						} 
					}
				}
			}
			
			private function deleteNode(xmlToDelete:XML):Boolean {
				var cn:XMLList = XMLList(xmlToDelete.parent()).children();
				
				for ( var i:Number = 0 ; i < cn.length() ; i++ ){
					if ( cn[i] == xmlToDelete ) {
						delete cn[i];       
						return true;
					}
				}
				return false;
			}
			
			private function doDragEnter(event:DragEvent):void
			{
				// Keep track of what was dragged.
				var draggedFormat:String = event.dragSource.formats[0];
				downloadDraggedItems = event.dragSource.dataForFormat(draggedFormat) as Array;
				
				// Cancel default behaviour
				event.preventDefault();
				// Tell the DragManager that the Tree will accent the DragDrop
				DragManager.acceptDragDrop(Tree(event.target));
				// hide the "drop line" that is shown in Tree control
				// when dropping in a Tree
				//downloadTree.showDropFeedback(event);
			}
			
			private function doDragOver(event:DragEvent):void
			{
				// Show the default "drop line" in the Tree control
				//downloadTree.showDropFeedback(event);
				// Cancel default behaviors

				var r:int = downloadTree.calculateDropIndex(event);
				if (r > 0) { 
					r--;
				}
				downloadTree.selectedIndex = r;
				var dropNode:XML = downloadTree.selectedItem as XML;
				
				var dropFolderNode:XML = dropNode;
				if (dropNode.name() == this.FILE_DESCRIPTOR_NAME && dropNode.@type != "dir") {
					dropFolderNode = dropNode.parent();
				}  else if (dropNode.name() == "Request" && r != 0) {
					if (dropNode.children().length() > 0) {
						dropFolderNode = dropNode.children()[0];
					} else {
						dropFolderNode = null;
					}
				} 
				
				if (dropFolderNode != null) {
					downloadTree.selectedItem = dropFolderNode;
				}
				
				event.preventDefault();
			}
			
			private function doDragExit(event:DragEvent):void
			{
				// hide the "drop line" that is shown in Tree control
				// when dropping in a Tree
				downloadTree.hideDropFeedback(event);
			}
			
			private function doDragComplete(event:DragEvent):void
			{
				// hide the "drop line" that is shown in Tree control
				// when dropping in a Tree
				downloadTree.hideDropFeedback(event);
			}
			
			//FUNCTIONS FOR SAMPLES TREE*********************************************************
			
			private function doDragEnterSamples(event:DragEvent):void
			{
				event.preventDefault();
				// Keep track of what was dragged.
				var draggedFormat:String = event.dragSource.formats[0];
				downloadDraggedItems = event.dragSource.dataForFormat(draggedFormat) as Array;
				
				DragManager.acceptDragDrop(AdvancedDataGrid(event.target));
				// hide the "drop line" that is shown in Tree control
				// when dropping in a Tree
				//samplesTree.showDropFeedback(event);
				samplesTree.hideDropFeedback(event);
			}
			
			private function doDragEnterFiles(event:DragEvent):void{
				event.preventDefault();
				samplesTree.selectedItem = event.draggedItem;
				
			}
			
			private function doDragExitSamples(event:DragEvent):void {
				// hide the "drop line" that is shown in Tree control
				// when dropping in a Tree
				event.preventDefault();
				samplesTree.hideDropFeedback(event);
			}
			
			private function doDragOverSamples(event:DragEvent):void {
				// Show the default "drop line" in the Tree control
				event.preventDefault();
				//samplesTree.showDropFeedback(event);
				samplesTree.hideDropFeedback(event);
				// Cancel default behaviors
				
				var r:int = samplesTree.calculateDropIndex(event);
				if (r > 0) { 
					r--;
				}
				samplesTree.selectedIndex = r;
				var dropNode:XML = samplesTree.selectedItem as XML;
				
				if (dropNode.name() == this.FILE_DESCRIPTOR_NAME) {
					dropNode = dropNode.parent();
				}
				
				if (dropNode != null) {
					samplesTree.selectedItem = dropNode;
				}
				
			}
			
			private function doDragCompleteSamples(event:DragEvent):void{
				event.preventDefault();
			}
			
			public function showHTMLUploadForm():void {
				var url:URLRequest = new URLRequest('uploadExperimentFile.jsp?requestNumber=' + this.requestNumber);
				navigateToURL(url, '_blank');			
				this.theTab.selectedIndex = 1;
			}
			
			private function renameFile(event:MouseEvent):void{
				if((filesGrid.selectedItem == null && downloadTree.selectedItem == null) || downloadTree.selectedIndex == 0){
					return;
				}
					
				else{
					var renameWindow:RenameExperimentWindow = RenameExperimentWindow(mx.managers.PopUpManager.createPopUp(this, RenameExperimentWindow, true));
				}
				
				if(downloadTree.selectedItem != null){
					var selectedNode:XML = XML(downloadTree.selectedItem);
					if(selectedNode.name() == "RequestDownload"){
						renameWindow.oldExperimentName = selectedNode.@key;
					}
					else{
						renameWindow.oldExperimentName = downloadTree.selectedItem.@displayName;
					}
				}
				else{
					var filesGridNode:XML = XML(filesGrid.selectedItem);
					if(filesGridNode.name() == "RequestDownload"){
						renameWindow.oldExperimentName = filesGridNode.@key;
					}
					else{
						renameWindow.oldExperimentName = filesGrid.selectedItem.@displayName;
					}
				}
				var oldName:String = downloadTree.selectedItem.name() == "RequestDownload" ? downloadTree.selectedItem.@itemNumber : downloadTree.selectedItem.@displayName;
				renameWindow.oldName = oldName;
				renameWindow.fileList = this.downloadList;
				renameWindow.idRequest = this.idRequest;
				renameWindow.addEventListener("refreshUploadFiles", onUploadFinished);
				mx.managers.PopUpManager.centerPopUp(renameWindow);
				renameWindow.nameExperiment.setFocus();
			}
			
			private function closeMe():void{
				var params:Object = new Object();
				params.idRequest = request.@idRequest;
				params.idLab = request.@idLab;
				params.isMicroarray  = "N";
				params.isNextGenSeq  = "N";
				params.isBioanalyzer = "N";
				if(parentView.downloadView != null){
					parentView.downloadView.getRequestDownloadList.send(params);
				}
				PopUpManager.removePopUp(this);
			}
			
			public function startFDTUpload():void {			
				var params:Object = new Object();		
				params.idRequest = idRequest;
				
				fdtUploadStart.send(params);
			}
			
			private function getDisplayName(item:Object, column:Object):String {
				if (item.name() == this.SAMPLE_NAME) {
					return item.@name;
				} else{
					return '';
				} 
			}
			
			private function getSampleNumberOrFileName(item:Object, column:Object):String {
				if (item.name() == this.SAMPLE_NAME) {
					return item.@number;
				} else if(item.name() == this.FILE_DESCRIPTOR_NAME){
					return item.@displayName;
				} else if(item.name() == this.SAMPLE_GROUP_FOLDER_NAME){
					return item.@displayName;
					
				} else{
					return '';
				} 
			}
			
			private function getTreeIcon(item:Object):Class{
				if(item.name() == this.SAMPLE_NAME){
					return parentApplication.testTube;
				} else if(item.name() == this.SEQ_RUN_FOLDER_NAME && item.children().length() > 0){
					return parentApplication.iconFolder;
				} else if(item.name() == this.SAMPLE_GROUP_FOLDER_NAME && item.children().length() > 0){
					return parentApplication.iconBlueFolder;
				}else if(item.name() == this.SEQ_RUN_FOLDER_NAME || item.name() == this.SAMPLE_GROUP_FOLDER_NAME){
					return parentApplication.iconFolderDisabled;
				} else{
					return null;
				}
			}
			
			private function createSeqRunFolder():void{
				var seqRunNumberNode:XML = <SeqRunNumber />;
				for each(var child:XML in samplesTree.selectedItem.children()){
					if(child.name() == this.SEQ_RUN_FOLDER_NAME && child.children().length() == 0){
						return;
					}
				}
				
				samplesTree.selectedItem.appendChild(seqRunNumberNode);
			}
			
			public function addSampleGroupFolder(folderName:String):void{
				var sampleGroupNode:XML = <SampleGroup displayName={folderName}/>;
				if(sampleList.attributes()[0] == folderName){
					Alert.show("The folder name (" + folderName +") is already in use for this list of samples.  Please choose a new name");
					return;
				}
				for each(var folder:XML in sampleList.source.elements(this.SAMPLE_GROUP_FOLDER_NAME)){
					if(folder.@displayName == folderName || folderName == "*||*"){//Reserved name for samples not nested under a specified group folder
						Alert.show("The folder name (" + folderName +") is already in use for this list of samples.  Please choose a new name");
						return;
					} 
				}
				for each(var folder1:XML in sampleList.source){
					if(folder1.@displayName == folderName || folderName == "*||*"){//Reserved name for samples not nested under a specified group folder
						Alert.show("The folder name (" + folderName +") is already in use for this list of samples.  Please choose a new name");
						return;
					} 
				}
				sampleList.addItem(sampleGroupNode);

			}
			
			private function deleteSampleGroupFolder():void{
				if(samplesTree.selectedItem.name() == this.SAMPLE_GROUP_FOLDER_NAME){
					recurseDeleteSampleGroupFolder(XML(samplesTree.selectedItem));
				}
			}
			
			private function recurseDeleteSampleGroupFolder(folder:XML):void{
				if(folder.children().length() == 0){
					deleteNode(folder);
					return;
				} else{
					for each(var item:XML in folder.children()){
						if(item.name() == this.SAMPLE_GROUP_FOLDER_NAME){
							recurseDeleteSampleGroupFolder(item);
						} else if(item.name() == this.SAMPLE_NAME){
							sampleList.addItem(item.copy());
							deleteNode(item);
						}
					}
					if(folder.children().length() == 0){
						deleteNode(folder);
					}
				}
			}
			
			private function resetExpFileTree():void{
				for each(var item:XML in samplesTree.selectedItems){
					experimentFileList.addItem(item.copy());
					if(item.hasOwnProperty("@idExperimentFile")){
						filesToUnlink.addItem(item.copy());
					}
					
					deleteNode(item);
				}
				
				for each(var folder:XML in sampleList.source..SeqRunNumber){
					if(folder.children().length() == 0){
						deleteNode(folder);
					}
				}
				
				dirty.setDirty();
			}
			
			private function showFileNameToolTip(item:Object):String {
				if(item != null && item.hasOwnProperty("@displayName")){
					return item.@displayName;
				} else{
					return "";
				}
				
			}
			
			private function getClickedItem(event:MouseEvent):void{
				if(event.currentTarget.name == 'experimentFilesTree' && experimentFilesTree.selectedItem != null){
					treeItemToSelect = this.experimentFilesTree.selectedItem.parent(); 
					selectedExpFileTreeIndex = this.experimentFilesTree.selectedIndex;
				}
				if(event.currentTarget.selectedItem == null || event.currentTarget.selectedItem.name() == 'RequestDownload' 
					|| event.currentTarget.selectedItem.name() == this.SEQ_RUN_FOLDER_NAME
					|| (event.currentTarget.selectedItem.name() == this.FILE_DESCRIPTOR_NAME && event.currentTarget.selectedItem.@type == 'dir')){
					return;
				}
				
				if(event.currentTarget.selectedItems.length == 1){
					clickedItems = new XMLListCollection();
				}
				
				for each(var item:XML in clickedItems){
					if(item.@zipEntryName == event.currentTarget.selectedItem.@zipEntryName){
						return;
					}
				}
				clickedItems.addItem(event.currentTarget.selectedItem.copy());
				
				//The following code seems redundant but it is accounting for the case when
				//the user performs a shift click
				var isFound:Boolean = false;	
				for each(var si:XML in event.currentTarget.selectedItems){
					for each(var ci:XML in clickedItems){
						if(ci.@zipEntryName == si.@zipEntryName){
							isFound = true;
							break;
						}
					}
					
					if(!isFound){
						clickedItems.addItem(si.copy());
					}
					isFound = false;
					
				}
			}
			
			private function onDragDropReturn(event:DragEvent):void{
				event.preventDefault();
				event.target.hideDropFeedback(event);
				
				if(event.dragInitiator.name == 'experimentFilesTree'){
					clickedItems = new XMLListCollection();
					return;
				}
				
				for each(var file:XML in clickedItems){
					if(file.name() == this.SAMPLE_NAME || file.name() == this.SEQ_RUN_FOLDER_NAME){
						continue;
					} else{
						if(file.hasOwnProperty("@idExperimentFile")){
							filesToUnlink.addItem(file.copy());
						}
						var fileName:String = file.@zipEntryName; 
						var shortFileName:String = fileName.substring(fileName.indexOf(request.@number));
						var filePart:String = fileName.split("/")[1];
						var node:XML = null;
						for each(var dl:XML in masterFileList.children()){
							if(filePart == dl.@itemNumber || fileName == dl.@zipEntryName){
								node = dl.copy();
								break;
							}
						}
						for each(var expFile:XML in experimentFileList.children()){
							if(expFile.@itemNumber == filePart){
								deleteNode(expFile);
								break;
							}
						}
						if(node != null){
							experimentFileList.source.appendChild(node);
						}
						//Since we are dragging files away from the sample list,
						//delete the appropriate file.
						for each(var f:XML in sampleList.source..FileDescriptor){
							if(f.@zipEntryName == file.@zipEntryName){
								deleteNode(f);
								break;
							}
						}
						
						//If the file we are dragging back is in a directory we add
						//all files in that directory back to the experiment file list.
						//Delete the ones we didn't actually drag over by comparing to what is still left in sample list.
						for each(var sf:XML in sampleList.source..FileDescriptor){
							recurseFindFileToDelete(experimentFileList.children(), sf);
						}
						
						for each(var fd:XML in experimentFileList.children().children()){
							deleteEmptyFolders(fd);	
						}
					}
				}
				
				for each(var emptyfolder:XML in sampleList.source..SeqRunNumber){
					if(emptyfolder.children().length() == 0){
						deleteNode(emptyfolder);
					}
				}
				
				clickedItems = new XMLListCollection();
				callLater(samplesTree.invalidateDisplayList);
				this.dirty.setDirty();
				event.target.hideDropFeedback(event);
			}
			
			private function doDragEnterReturn(event:DragEvent):void{
				event.preventDefault();
				DragManager.acceptDragDrop(Tree(event.target));
				experimentFilesTree.hideDropFeedback(event);
			}
			private function doDragOverReturn(event:DragEvent):void{
				event.preventDefault();
				experimentFilesTree.hideDropFeedback(event);
				
				experimentFilesTree.selectedIndex = 0;
				var dropNode:XML = experimentFilesTree.selectedItem as XML;
				
				experimentFilesTree.hideDropFeedback(event);
			}
			private function doDragExitReturn(event:DragEvent):void{
				event.preventDefault();
				experimentFilesTree.hideDropFeedback(event);
			}
			
			private function doDragCompleteReturn(event:DragEvent):void{
				event.preventDefault();
			}
			
		]]>
	</mx:Script>	
	
	<mx:XMLListCollection id="files"/>
	<mx:XMLListCollection id="filesToMove"/>
	<mx:XMLListCollection id="uploadedFiles" source="{getRequest.lastResult..RequestUpload.FileDescriptor}"/>
	<mx:XMLListCollection id="downloadList" source="{getRequestDownloadList.lastResult..Request}"/>
	<mx:XMLListCollection id="filesToRemove"/>
	<mx:XMLListCollection id="filesToUnlink"/>
	<mx:Canvas width="100%" height="100%">
		<mx:TextArea width="504" height="20" 
					 styleName="instructionBox" id="instructionBox" editable="false" wordWrap="true" visible="{theTab.selectedIndex == 1 || theTab.selectedIndex == 2}" text="Drag uploaded file into one of the folders on the right.To drag multiple files, hold CTRL or SHIFT key while selecting." right="0"/>
		<mx:TabNavigator id="theTab" change="onTabChange();"   width="100%" paddingTop="0" height="100%" styleName="form">
			<mx:VBox label="Upload files" width="100%" height="100%" verticalGap="2" paddingBottom="4" paddingLeft="4" paddingRight="4" paddingTop="4">
				<mx:ViewStack id="uploadFilesStack" width="100%" height="100%">
					<mx:VBox width="100%" height="100%">
						<mx:HBox height="20" width="100%">						
							<mx:LinkButton id="uploadBrowseButton" label="Choose files" icon="@Embed(source='../../assets/add.png')" disabledIcon="@Embed(source='../../assets/add_disable.png')"  toolTip="Choose file(s) to upload"  textDecoration="underline" enabled="false"/>
							<mx:LinkButton id="uploadDeleteButton" label="Remove" disabledIcon="@Embed(source='../../assets/delete_disable.png')" icon="@Embed(source='../../assets/delete.png')" toolTip="Remove file"  textDecoration="underline" enabled="false"/>
							<mx:LinkButton label="Clear all" toolTip="Clear all files" id="uploadClearButton" icon="@Embed(source='../../assets/cross.png')" disabledIcon="@Embed(source='../../assets/cross_disable.png')" textDecoration="underline" enabled="false"/>
							<mx:HBox width="100%"  horizontalAlign="right">
								<mx:VRule height="20"/>
								<mx:LinkButton visible="{parentApplication.isFDTSupported ? false : true}" label="Upload problems?  Try this." click="showHTMLUploadForm()" textDecoration="underline" icon="@Embed(source='../../assets/application_get.png')"/>								
								<util:ContextHelp context1="uploadhelp" 
												  showEdit="{parentApplication.isAdminState}" title="Upload Help"
												  label="Upload Help" labelPlacement="right" id="labelApplication0"/>								
							</mx:HBox>			          
						</mx:HBox>
						<mx:DataGrid id="uploadFilesGrid" width="100%" height="100%">				
						</mx:DataGrid>	
					</mx:VBox>
					<mx:VBox width="100%" height="100%" horizontalScrollPolicy="off" verticalScrollPolicy="off">
						<mx:TextArea styleName="instructionBox" width="100%" wordWrap="true" height="100%" id="textarea1" fontSize="12">
							<mx:htmlText><![CDATA[This method of uploading files uses a Fast Data Transfer (FDT) server. To upload files:<br><br>     1. Select the "Start" button below.<br><br>     2. This will download a special "jnlp" file that can be opened using Java Web Start (either directly or after first saving<br>         the file to your desktop).<br><br>     3. Use the window launched by Java Web Start to select and upload the files to the server.<br><br>     4. After all of the files have been uploaded (% Completed column indicates 100 for all files), click the<br>        "Organize files" link on Experiment Detail to move the uploaded files to the appropriate folders.<br>         button.]]></mx:htmlText>
						</mx:TextArea>							
					</mx:VBox>
				</mx:ViewStack>
			</mx:VBox>
			<mx:VBox width="100%" height="100%" label="Organize files"  paddingBottom="4" paddingLeft="4" paddingRight="4" paddingTop="4" verticalGap="2">
				<mx:HBox width="100%" horizontalGap="2" paddingLeft="0">
					<mx:LinkButton id="newFolderButton" label="New folder"  enabled="false" disabledIcon="@Embed(source='../../assets/folder_add_disable.png')" icon="@Embed(source='../../assets/folder_add.png')" toolTip="Create folder"  textDecoration="underline" click="showAddFolderWindow()" paddingLeft="0"/>
					<mx:LinkButton id="removeFileButton" label="Remove (from file system)"  enabled="{filesGrid.selectedItem != null || (this.downloadTree.selectedItem != null)}" disabledIcon="@Embed(source='../../assets/delete_disable.png')" icon="@Embed(source='../../assets/delete.png')" toolTip="Remove file"  textDecoration="underline" click="removeExperimentFile()" paddingLeft="0"/>
					<mx:LinkButton id="renameFileButton" label="Rename" textDecoration="underline" paddingLeft="0" click="renameFile(event)" enabled="{(this.downloadTree.selectedItem != null) &amp;&amp; this.downloadTree.selectedIndex != 0}" toolTip="Rename selected file or folder" icon="@Embed(source='../../assets/segment_import.png')" disabledIcon="@Embed(source='../../assets/segment_import_disable.png')"/>
					<mx:Spacer width="100%"/>
					<mx:LinkButton id="refresh" label="Refresh" icon="@Embed(source='../../assets/refresh.png')" toolTip="Refresh files list"  textDecoration="underline" click="refreshFiles()"/>
					
				</mx:HBox>
				
				<mx:HDividedBox width="100%" height="100%">
					<mx:VBox width="300" height="100%" verticalGap="0" styleName="form" >
						<mx:HBox width="100%"  backgroundColor="#C9BEC0" backgroundAlpha="0.22" >
							<mx:Label text="Uploaded Files"/>
						</mx:HBox>
						<mx:DataGrid id="filesGrid"  
									 dragEnabled="true"
									 showHeaders="false"
									 allowMultipleSelection="true"
									 width="100%" height="100%"
									 dataProvider="{uploadedFiles}" dragMoveEnabled="true" itemClick="{this.downloadTree.selectedItem = null; this.renameFileButton.enabled = false}">
							<mx:columns>
								<mx:DataGridColumn headerText="Uploaded file"  dataField="@displayName"/>
							</mx:columns>
						</mx:DataGrid>
					</mx:VBox>
					<mx:VBox width="50%" height="100%" verticalGap="0" styleName="form">
						<mx:HBox width="100%"  backgroundColor="#C9BEC0" backgroundAlpha="0.22" >
							<mx:Label text="Experiment Folders"/>
						</mx:HBox>
						<mx:Tree id="downloadTree"
								 width="100%" height="100%" 
								 iconFunction="parentApplication.getTreeIcon" 
								 dataProvider="{downloadList}"
								 borderStyle="solid" 
								 labelFunction="{getDownloadName}"
								 dropEnabled="true"  		                
								 dragEnabled="true"
								 dragMoveEnabled="true"  
								 allowMultipleSelection="true"
								 showRoot="true"
								 dragDrop="onDragDropFile(event)"
								 dragOver="doDragOver(event)"
								 dragEnter="doDragEnter(event)"
								 dragExit="doDragExit(event)"
								 itemClick="{this.filesGrid.selectedItem = null;}">
						</mx:Tree>		
					</mx:VBox>
				</mx:HDividedBox>
			</mx:VBox>
			<mx:VBox width="100%" id="linkSamplesTab" toolTip="{linkSamplesTab.enabled == false ? 'Please save current changes to enable this tab' : ''}" height="100%" label="Link Samples"  paddingBottom="4" paddingLeft="4" paddingRight="4" paddingTop="4" verticalGap="2" >
				<mx:HBox width="100%" horizontalGap="2" paddingLeft="0" horizontalAlign="right"> 
					<mx:LinkButton id="newSeqRunFolder" label="New Seq Folder"  enabled="{samplesTree.selectedIndex > -1 &amp;&amp; samplesTree.selectedItem.name() == this.SAMPLE_NAME}" disabledIcon="@Embed(source='../../assets/folder_add_disable.png')" icon="@Embed(source='../../assets/folder_add.png')" toolTip="Create a new folder for organizing experiment files"  textDecoration="underline" click="createSeqRunFolder()" paddingLeft="0"/>
					<mx:LinkButton id="newSampleGroupFolder" label="New Sample Folder" disabledIcon="@Embed(source='../../assets/folder_add_disable.png')" icon="@Embed(source='../../assets/folder_add.png')" toolTip="Create a new folder for organizing samples"  textDecoration="underline" click="showSampleGroupAddFolderWindow()" paddingLeft="0"/>
					<mx:LinkButton id="deleteSampleFolder" label="Delete Sample Folder"  enabled="{samplesTree.selectedIndex > -1 &amp;&amp; samplesTree.selectedItem.name() == this.SAMPLE_GROUP_FOLDER_NAME}" disabledIcon="@Embed(source='../../assets/folder_delete_disable.png')" icon="@Embed(source='../../assets/folder_delete.png')" toolTip="Delete folder for samples.  Will not delete samples or their files."  textDecoration="underline" click="deleteSampleGroupFolder()" paddingLeft="0"/>
					<mx:LinkButton id="refreshLinkage" label="Refresh" icon="@Embed(source='../../assets/refresh.png')" toolTip="Refresh files list"  textDecoration="underline" click="refreshFiles(); getLinkedSampleFiles.send();"/>
				</mx:HBox>
				<mx:HDividedBox width="100%" height="100%">
					<mx:VBox width="300" height="100%" verticalGap="0" styleName="form" >
						<mx:Tree id="experimentFilesTree"   
								 allowMultipleSelection="true" dataTipFunction="showFileNameToolTip"
								 iconFunction="parentApplication.getTreeIcon"
								 dataProvider="{experimentFileList}"
								 labelFunction="{getDownloadName}"
								 mouseDown="{getClickedItem(event);}"
								 showDataTips="true"
								 showRoot="true"
								 dragDrop="onDragDropReturn(event)"
								 dragOver="doDragOverReturn(event)"
								 dragExit="doDragExitReturn(event)"
								 dragEnter="doDragEnterReturn(event)"
								 dragComplete="doDragCompleteReturn(event)"
								 width="100%" height="100%"
								 dragMoveEnabled="true" dropEnabled="true" dragEnabled="true" itemClick="{this.samplesTree.selectedItem = null;}">
						</mx:Tree>
					</mx:VBox>
					<mx:VBox width="50%" height="100%" verticalGap="0" styleName="form">
						<mx:AdvancedDataGrid id="samplesTree" displayItemsExpanded="true"
											 width="100%" height="100%"  
											 dragDrop="onDetermineDragDrop(event)"
											 dragOver="doDragOverSamples(event)"
											 dragExit="doDragExitSamples(event)"
											 dragEnter="doDragEnterSamples(event)" dragComplete="doDragCompleteSamples(event)"
											 iconFunction="getTreeIcon"
											 sortExpertMode="true"
											 sortableColumns="false"
											 borderStyle="solid" 
											 dropEnabled="true"  		                
											 dragEnabled="true"  
											 dragMoveEnabled="true"
											 allowMultipleSelection="false" dataTipFunction="showFileNameToolTip"
											 itemClick="{this.experimentFilesTree.selectedItem = null;}" mouseDown="getClickedItem(event)">
							<mx:dataProvider>
								<mx:HierarchicalData id="sampleHierarchy" source="{sampleList}"/>
							</mx:dataProvider>	
							<mx:columns>
								<mx:AdvancedDataGridColumn headerText="Sample ID" labelFunction="{getSampleNumberOrFileName}" width="330"/>
								<mx:AdvancedDataGridColumn headerText="Name" labelFunction="{getDisplayName}" showDataTips="true" />
								<mx:AdvancedDataGridColumn headerText="Date" dataField="@lastModifyDateDisplay"/>
								<mx:AdvancedDataGridColumn headerText="Size" dataField="@fileSizeText"/>
							</mx:columns>
						</mx:AdvancedDataGrid>
					</mx:VBox>
				</mx:HDividedBox>
			</mx:VBox>
		</mx:TabNavigator>
	</mx:Canvas>
	<mx:ControlBar width="100%">
		<mx:ViewStack id="buttonStack" width="100%" selectedIndex="{theTab.selectedIndex == 2 ? 1 : theTab.selectedIndex}">
			<mx:HBox width="100%">
				<mx:ViewStack id="buttonStack_1" width="100%" selectedIndex="{uploadFilesStack.selectedIndex}">
					<mx:HBox height="100%" width="100%">
						<mx:ProgressBar id="uploadProgressBar"  width="100%" labelPlacement="center" trackHeight="15" height="20" fontWeight="normal" themeColor="#EAED9A"/>
						<mx:Button label="Upload files" id="uploadButton"  enabled="false" visible="true" disabledIcon="@Embed(source='../../assets/upload_disable.png')" icon="@Embed(source='../../assets/upload.png')"/>	
					</mx:HBox>				 
					<mx:HBox height="100%" width="100%">
						<mx:Spacer  width="100%" height="10"/>	
						<mx:Label id="saveUUID" text="" visible="false"/>
						<mx:Button id="fdtStartButton" label="Start" click="startFDTUpload();"  enabled="true" visible="true"/>	      	
					</mx:HBox>		  			 
				</mx:ViewStack>
			</mx:HBox>
			<mx:HBox width="100%">
				<mx:Spacer  width="100%" height="10"/>		
				<util:DirtyNote id="dirty"/>
				<mx:Button label="Save" id="saveButton"  enabled="true" click="save()" visible="true" icon="@Embed(source='../../assets/save.png')"/>	      	
				<mx:Button label="Close" enabled="true" click="{closeMe()}" visible="true" />	      	
			</mx:HBox>
		</mx:ViewStack>
	</mx:ControlBar>
	
</util:ResizableTitleWindow>
