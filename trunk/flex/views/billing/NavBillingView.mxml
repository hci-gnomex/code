<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml"
        width="100%"
        creationPolicy="all" show="init()"
        height="100%" xmlns:util="views.util.*" >


 
   <mx:HTTPService                
    id="getLab" 
    url="GetLab.gx"
    destination="getLab" 
    resultFormat="e4x"
    result="onGetLab(event)" 
    fault="parentApplication.onFailHttpRequest('Failed to get group', event)"
    method="POST"
    useProxy="false"> 
    <mx:request>
    	<idLab>{this.labCombo.selectedItem.@idLab}</idLab>
    </mx:request>
   </mx:HTTPService>
   	 

    <mx:HTTPService 
    id="getPricingList" 
    url="GetPricingList.gx"
    resultFormat="e4x"
    result="onGetPricingList(event)"
    fault="parentApplication.onFailHttpRequest('Unable to get prices', event)"
    method="POST"
    useProxy="false">
    <mx:request>
    	<showInactive>{this.showInactivePricesCheckbox.selected ? "Y" : "N"}</showInactive>
    </mx:request>
    </mx:HTTPService > 

   <mx:HTTPService                
    id="getPrice" 
    url="GetPrice.gx"
    destination="getPrice" 
    resultFormat="e4x"
    result="onGetPrice(event)" 
    fault="parentApplication.onFailHttpRequest('Failed to get price', event)"
    method="POST"
    useProxy="false"> 
   </mx:HTTPService>

   <mx:HTTPService                
    id="deletePrice" 
    url="DeletePrice.gx"
    destination="deletePrice" 
    resultFormat="e4x"
    result="onDeletePrice(event)" 
    fault="parentApplication.onFailHttpRequest('Failed to delete price', event)"
    method="POST"
    useProxy="false"> 
   </mx:HTTPService>

  <mx:HTTPService                
    id="getPriceSheet" 
    url="GetPriceSheet.gx"
    destination="getPriceSheet" 
    resultFormat="e4x"
    result="onGetPriceSheet(event)" 
    fault="parentApplication.onFailHttpRequest('Failed to get price sheet', event)"
    method="POST"
    useProxy="false"> 
   </mx:HTTPService>
   
    <mx:HTTPService                
    id="deletePriceSheet" 
    url="DeletePriceSheet.gx"
    destination="deletePriceSheet" 
    resultFormat="e4x"
    result="onDeletePriceSheet(event)" 
    fault="parentApplication.onFailHttpRequest('Failed to delete price sheet', event)"
    method="POST"
    useProxy="false"> 
   </mx:HTTPService>

  <mx:HTTPService                
    id="getPriceCategory" 
    url="GetPriceCategory.gx"
    destination="getPriceCategory" 
    resultFormat="e4x"
    result="onGetPriceCategory(event)" 
    fault="parentApplication.onFailHttpRequest('Failed to get price category', event)"
    method="POST"
    useProxy="false"> 
   </mx:HTTPService>

    <mx:HTTPService                
    id="deletePriceCategory" 
    url="DeletePriceCategory.gx"
    destination="deletePriceCategory" 
    resultFormat="e4x"
    result="onDeletePriceCategory(event)" 
    fault="parentApplication.onFailHttpRequest('Failed to delete price category', event)"
    method="POST"
    useProxy="false"> 
   </mx:HTTPService>
    
    <mx:HTTPService 
    id="getBillingRequestList" 
    url="GetBillingRequestList.gx"
    resultFormat="e4x"
    showBusyCursor="true"
    result="onGetBillingRequestList(event)"
    fault="parentApplication.onFailHttpRequest('Unable to get billing request list', event)"
    method="POST"
    useProxy="false">
       <mx:request>
         <idBillingPeriod>{billingPeriodCombo.selectedItem.@value}</idBillingPeriod>       	
         <idLab>{labCombo.selectedItem.@idLab}</idLab>       	
         <idBillingAccount>{billingAccountCombo.selectedItem != null ? billingAccountCombo.selectedItem.@idBillingAccount : ""}</idBillingAccount>       	
       </mx:request>
    </mx:HTTPService >  
    
    <mx:HTTPService 
    id="getBillingItemList" 
    url="GetBillingItemList.gx"
    showBusyCursor="true"
    resultFormat="e4x"
    result="onGetBillingItemList(event)"
    fault="parentApplication.onFailHttpRequest('Unable to get billing item list', event)"
    method="POST"
    useProxy="false">
       <mx:request>
         <requestNumber>{requestNumberText.text}</requestNumber>
         <idBillingPeriod>{requestNumberText.text == "" ? billingPeriodCombo.selectedItem.@value : ""}</idBillingPeriod>       	
         <idLab>{requestNumberText.text == "" ? labCombo.selectedItem.@idLab : ""}</idLab>       	
         <idBillingAccount>{requestNumberText.text == "" ? billingAccountCombo.selectedItem.@idBillingAccount : ""}</idBillingAccount>       	
       </mx:request>
    </mx:HTTPService >          
    
    <mx:HTTPService 
    id="createBillingItems" 
    url="CreateBillingItems.gx"
    showBusyCursor="true"
    resultFormat="e4x"
    result="onCreateBillingItems(event)"
    fault="parentApplication.onFailHttpRequest('Unable to create new billing items', event)"
    method="POST" 
    useProxy="false"> 
    </mx:HTTPService >
        
    <mx:HTTPService 
    id="saveBillingItemList" 
    url="SaveBillingItemList.gx"
    resultFormat="e4x"
    destination="saveBillingItemList"
    showBusyCursor="true"
    result="onSaveBillingItemList(event)"
    fault="parentApplication.onFailHttpRequest('Unable to save billing items', event)"
    method="POST"
    useProxy="false">
    </mx:HTTPService >    
    
    <mx:HTTPService 
    id="movePriceCategory" 
    url="MovePriceCategory.gx"
    resultFormat="e4x"
    destination="movePriceCategory"
    showBusyCursor="true"
    result="onMovePriceCategory(event)"
    fault="parentApplication.onFailHttpRequest('Unable to move price category', event)"
    method="POST"
    useProxy="false">
    </mx:HTTPService >    
    
     

<mx:Script>
 	<![CDATA[
 		import mx.formatters.CurrencyFormatter;
 		import mx.collections.XMLListCollection; 
 		import mx.collections.Sort;
		import mx.events.DragEvent;
		import mx.managers.DragManager;
        import mx.core.DragSource;
        import mx.controls.Alert;
		import mx.rpc.events.FaultEvent;
		import mx.rpc.events.ResultEvent;
		import hci.flex.controls.DropdownLabel;
		import hci.flex.controls.ComboBox;
        import views.renderers.ComboBoxBillingStatus;
        import mx.events.CollectionEvent;	
        import mx.events.CollectionEventKind;	
        import mx.events.PropertyChangeEvent;
        import mx.events.CloseEvent;
        import views.renderers.LabelPercentage;
        import mx.controls.advancedDataGridClasses.AdvancedDataGridColumn;
        
        private var revisionNumber:int = 1;
 		
		private var billingItems:XMLListCollection = new XMLListCollection();

        [Bindable]
        private var billingAccounts:XMLListCollection = new XMLListCollection();

        [Bindable]
        private var requests:XMLListCollection = new XMLListCollection();

        [Bindable]
        private var billingTemplates:XMLListCollection = new XMLListCollection();

        [Bindable]
        private var pricingList:XMLListCollection = new XMLListCollection();

        [Bindable]
        private var billingItemsDisplayed:XMLListCollection = new XMLListCollection();

        [Bindable]
        private var billingPeriods:XMLListCollection = new XMLListCollection();
        
        private var billingItemsToRemove:XMLListCollection = new XMLListCollection();
        
        [Bindable]
        private var dirtyBillingItems:XMLListCollection = null;
        
        private var currencyFormatter:CurrencyFormatter = new CurrencyFormatter();
        
        private var lastSelectedItem:Object = null;
        
        private var lastSelectedBillingPeriod:Object = null;
        
        [Bindable]
        private var grandTotalPrice:String = "";
        
        private var requestTabWidth:Number = 220;
        private var priceTabWidth:Number = 400;
        
        private var pricingNodeToSelect:String = null;
        private var pricingIdToSelect:Object = null;
        
        private function init():void {
        	initializeData();
        }

      	public function initializeData():void
      	{
      		glFileButton.visible = parentApplication.getProperty(parentApplication.PROPERTY_GENERATE_GL_INTERFACE) == "N" ? false : true;
      		
			getPricingList.send();
			
			billingPeriods = new XMLListCollection(parentApplication.dictionaryManager.getEntries("hci.gnomex.model.BillingPeriod"));
        	var billingPeriodSort:Sort = new Sort();
        	billingPeriodSort.compareFunction = this.sortBillingPeriods;	   
        	billingPeriods.sort = billingPeriodSort;  
        	billingPeriods.refresh();
        	billingPeriodCombo.dataProvider = billingPeriods;   
 
 			var params:Object = new Object();
 			if(this.lastSelectedBillingPeriod == null) {
	 			var currentBillingPeriod:Object = null;
				for each( var de:Object in billingPeriods) {
					if (de.@value != "" && de.@isCurrentPeriod == "Y") {
						billingItemsDisplayed = new XMLListCollection();
						dirty.resetDirty();
						this.billingItemGrid.dataProvider = new HierarchicalData(billingItemsDisplayed);
		
						currentBillingPeriod = de;
			        	billingPeriodCombo.selectedItem = currentBillingPeriod;
			        	
			        	params.idBillingPeriod = currentBillingPeriod.@value;
			        	getBillingRequestList.send(params);
			        	getBillingItemList.send(params);
						break;
					}
				} 				
 			} else {
				billingItemsDisplayed = new XMLListCollection();
				dirty.resetDirty();
				this.billingItemGrid.dataProvider = new HierarchicalData(billingItemsDisplayed);
	

	        	billingPeriodCombo.selectedItem = lastSelectedBillingPeriod;
	        	
	        	params.idBillingPeriod = lastSelectedBillingPeriod.@value;
	        	getBillingRequestList.send(params);
	        	getBillingItemList.send(params);
 			}

        	
        	
        	currencyFormatter.currencySymbol = "$";
        	currencyFormatter.precision = 2;
        	currencyFormatter.useThousandsSeparator;        	
      	}		
		public function checkDirty():void {      
		}
		
		public function recalculateTotalPrice(experiment:Object):void {
			for each(var bi:Object in experiment.BillingItem) {
				var percentageDisplay:String = bi.@percentageDisplay;
				percentageDisplay = percentageDisplay.replace("%", "");
				
				var unitPriceString:String = bi.@unitPrice;
				unitPriceString = unitPriceString.replace("$", "");
				unitPriceString = unitPriceString.replace(",", "");
				var unitPrice:Number = Number(unitPriceString);
				
				bi.@totalPrice = unitPrice * bi.@qty * (int(percentageDisplay) / 100); 
				bi.@totalPrice = currencyFormatter.format(bi.@totalPrice);	 
			}
		}
		
		 
		private function addBillingItem():void {
			dirty.setDirty();
			var emptyNode:XML = null;
			emptyNode = new XML("<BillingItem " +
				    " idBillingItem='" + "BillingItem" + billingItems.length + "'" +
				    " category='' description='' " +
				    " codeBillingChargeKind=''" +				
					" idBillingPeriod=''" +				
					" idBillingAccount=''" +				
					" qty=''" +
					" unitPrice=''" + 
					" totalPrice=''" +
					" isDirty='Y'" +
					" codeBillingStatus = 'PENDING'" +
					"/>");
			var requestNode:Object = null;
			if ( billingItemGrid.selectedItem == null) {
				requestNode = billingItemsDisplayed.getItemAt(0);
			} else if (billingItemGrid.selectedItem.name() == "BillingItem") {
	       		requestNode = billingItemGrid.selectedItem.parent();
	       	} else {
	       		requestNode = billingItemGrid.selectedItem;
	       	}
	       	requestNode.appendChild(emptyNode);
	       	requestNode.@isDirty = "Y";
	       							
		} 
		
		private function removeBillingItem():void {
			dirty.setDirty();
			if (billingItemGrid.selectedItems.length > 0) {
				var item:XML = null;
				for each(item in billingItemGrid.selectedItems) {		
					item.@remove = "Y";
					
					if (item.name() == "Request") {
						for each(var bi:XML in item.BillingItem) { 
							billingItemsToRemove.addItem(bi); 
						}
						billingItems.removeItemAt(billingItems.getItemIndex(item));
						
					} else if (item.name() == "BillingItem") {
						billingItemsToRemove.addItem(item);
						var requestNode:XML = item.parent();
						for (var i:int = 0; i < requestNode.children().length(); i++) {
							if(requestNode.children()[i].@remove == "Y") {
								delete requestNode.children()[i];
							}
						}
					}
				}
			}
			this.onRequestNodeSelected();
		}
		
      	  
		public function getTreeIcon(item:Object):Class {
				if (item.name() == "Status" ){ 
					return parentApplication.iconProject;
				}else if (item.name() == "Request") {
					if (item.@codeRequestCategory == "SOLEXA") {
						return parentApplication.iconSolexa;
					} else if (item.@codeRequestCategory == "AFFY") {
						return parentApplication.iconMicroarrayAffy; 
					} else if (item.@codeRequestCategory == "AGIL") {
						return parentApplication.iconMicroarray;
					} else if (item.@codeRequestCategory == "AGIL1") {
						return parentApplication.iconMicroarraySingleColor;
					} else if (item.@codeRequestCategory == "QC") {
						return parentApplication.iconQC; 
					} else {
						return parentApplication.iconFlask;
					}
				}else if (item.name() == "PriceSheet") {
				    return parentApplication.iconPriceSheet;
				}else if (item.name() == "PriceCategory" ) {
					return parentApplication.iconBillingFolder;
				} else if (item.name() == "Price" ) {
					if (item.@isActive == "N") {
						return parentApplication.iconBillingDisabled;
					} else {
						return parentApplication.iconBilling;
					}
				} else if (item.name() == "Lab") {
					return parentApplication.iconGroup;
				} else {
					return parentApplication.iconBilling; 
				}  
		}   
		
		public function getTreeLabel(item:Object, column:AdvancedDataGridColumn):String {
			if (item.name() == "Request" && item.parent().name() == "Status" && item.parent().@status == "PENDING") {
				return item.@label + " " + item.@labName;
			} else {
				return item.@label;
			}
		} 
		
		public function refreshAndSelectPricing(nodeName:String, id:Object):void {
			this.pricingNodeToSelect = nodeName;
			this.pricingIdToSelect = id;
			refresh();
		}

		public function refresh():void {
			if (dirty.isDirty()) {
				Alert.show( "Unsaved changes will be discarded.  Proceed anyways?",
						"Warning",
						(Alert.YES | Alert.NO), this, 
						onPromptToRefresh,						
						parentApplication.iconWarning);
			} else {
				refreshImpl();
			}
		}
				
		private function onPromptToRefresh(event:CloseEvent):void {
            if (event.detail==Alert.YES) {
            	refreshImpl();
            }
        }
		public function refreshImpl():void {
			billingItemsDisplayed = new XMLListCollection();
			this.billingItemGrid.dataProvider = new HierarchicalData(billingItemsDisplayed);
			
			var params:Object = new Object();
			params.action = "refresh";
			getPricingList.send();
			
			if (requestNumberText.text != "") {
				this.refreshBillingItemsByRequestNumber();
			} else {
				this.refreshBillingItems();
			}
			
		}
		
		private function refreshBillingItems():void {
			if (billingPeriodCombo.selectedItem == null) {
				billingPeriodCombo.setFocus();
				Alert.show("Please select a billing period or enter an experiment #");
			} else {
				this.dirty.resetDirty();
				this.getBillingRequestList.send();
			}
		}
		
		private function refreshBillingItemsByRequestNumber():void {
			billingPeriodCombo.selectedItem = null;
			labCombo.selectedItem = null;
			billingAccountCombo.selectedItem = null;
			
			this.dirty.resetDirty();

			var params:Object = new Object();
			params.requestNumber = requestNumberText.text;
			getBillingRequestList.send(params);
		}
		
		private function underlyingBillingItemChange(event:CollectionEvent):void {
			if (event.kind == CollectionEventKind.RESET || 
			    event.kind == CollectionEventKind.REFRESH) {
				// ignore refresh and resets 
				return;
			}			
			dirty.setDirty();  
				
			for each(var e:Object in event.items) {
				if (e is PropertyChangeEvent) {
					var pe:PropertyChangeEvent = PropertyChangeEvent(e);
					var experiment:Object = pe.source;
					billingItems.removeEventListener(CollectionEvent.COLLECTION_CHANGE, underlyingBillingItemChange);
					experiment.@isDirty = "Y";  
					recalculateTotalPrice(experiment);					
					billingItems.addEventListener(CollectionEvent.COLLECTION_CHANGE, underlyingBillingItemChange);
					
				}
			}				
		}  
		private function onGetPricingList(event:ResultEvent):void {
			if (getPricingList.lastResult.name() == "Pricing") {
				this.pricingList = new XMLListCollection(getPricingList.lastResult.PriceSheet);
				this.pricingTree.dataProvider = new HierarchicalData(pricingList);
				this.pricingTree.dataProvider.refresh();   
				
				callLater(this.selectPricingTreeItem);	

			} else {
				Alert.show("An error occurred while getting price sheet.");
			}			
		}
		
		private function selectPricingTreeItem():void {
			// Now select the pricing node
			if (this.pricingNodeToSelect != null && this.pricingIdToSelect != null) {
				var node:Object = null;
				if (this.pricingNodeToSelect == 'Price') {
					node = getPricingList.lastResult..Price.(@idPrice == pricingIdToSelect);
				} else if (this.pricingNodeToSelect == 'PriceCategory') {
					node = getPricingList.lastResult..PriceCategory.(@idPriceCategory == pricingIdToSelect);
				} else if (this.pricingNodeToSelect == 'PriceSheet') {
					node = getPricingList.lastResult.PriceSheet.(@idPriceSheet == pricingIdToSelect);
				} 
				if  (node != null && node is XMLList) {
					this.pricingTree.selectedItem = node[0];
					this.pricingTree.scrollToIndex(this.pricingTree.selectedIndex); 
				} else {
					this.pricingTree.selectedItem = node;
					this.pricingTree.scrollToIndex(this.pricingTree.selectedIndex); 
				} 
			}
			
		}
		private function onGetBillingRequestList(event:ResultEvent):void {
			if (getBillingRequestList.lastResult.name() == "BillingRequestList") {
				this.requests = new XMLListCollection(getBillingRequestList.lastResult.Status);
				this.requestTree.dataProvider = new HierarchicalData(requests);
				this.requestTree.dataProvider.refresh();   	
				
				var pendingItems:XMLList = getBillingRequestList.lastResult.Status.(@label == 'Pending');
				jumpPending.visible =  pendingItems.length() ? true : false;
				
				var completedItems:XMLList = getBillingRequestList.lastResult.Status.(@label == 'Completed');
				jumpCompleted.visible =  completedItems.length() ? true : false;
				
				var approvedItems:XMLList = getBillingRequestList.lastResult.Status.(@label == 'Approved');
				jumpApproved.visible =  approvedItems.length() ? true : false;
				
				
				getBillingItemList.send();
			}  else {
				var message:String = getBillingRequestList.lastResult.ERROR.@message;
				Alert.show(message);
			}				
		} 
		private function onGetBillingItemList(event:ResultEvent):void {
			if (getBillingItemList.lastResult.name() == "BillingItemList") {
				this.billingItems = new XMLListCollection(getBillingItemList.lastResult.Request);
				this.billingItems.addEventListener(CollectionEvent.COLLECTION_CHANGE, underlyingBillingItemChange);			

				reselectRequestTree();
				this.onRequestNodeSelected();
				
			} else {
				var message:String = getBillingItemList.lastResult.ERROR.@message;
				Alert.show(message);
			}			
		} 
		
		private function jumpTo(status:String):void {
			for each(var item:XML in requests) {
				if (item.@label == status) {
					this.requestTree.selectedItem = item;
					this.requestTree.firstVisibleItem = item;
					this.onRequestNodeSelected();
					break;
				}
			}
		}
		private function jumpToApproved():void {
			// First try to jump to 'Approved (External)'
			var found:Boolean = false;
			for each(var item:XML in requests) {
				if (item.@label == 'Approved (External)') {
					this.requestTree.selectedItem = item;
					this.requestTree.firstVisibleItem = item;
					this.onRequestNodeSelected();
					found = true;
					break;
				}
			}
			if (!found) {
			for each(var item1:XML in requests) {
				if (item1.@label == 'Approved') {
					this.requestTree.selectedItem = item1;
					this.requestTree.firstVisibleItem = item1;
					this.onRequestNodeSelected();
					found = true;
					break;
				}
			}
				
			}
		}
		
		private function reselectRequestTree():void {
			if (lastSelectedItem != null) {
				if (lastSelectedItem.name() == "Request") {
					for each(var p:XML in requests) {
						for each(var request:XML in p..Request) {
							if (request.@requestNumber == lastSelectedItem.@requestNumber) {
								this.requestTree.selectedItem = request;
								this.requestTree.firstVisibleItem = request;
								break; 
							}
						}
					}
				} else if (lastSelectedItem.name() == "Status") {
					for each(var status:XML in requests) {
						if (status.@label == lastSelectedItem.@label) {
							this.requestTree.selectedItem = status;
							this.requestTree.firstVisibleItem = status;
							break;
						}
					}
				} else if (lastSelectedItem.name() == "Lab") {
					for each(var st:XML in requests) {
						for each(var lab:XML in st.Lab) {
							if (lab.@label == lastSelectedItem.@label) {
								this.requestTree.selectedItem = lab;
								this.requestTree.firstVisibleItem = lab;								
								break;
							}								
						}
					}
				}	
			}			
		}
		private function onCreateBillingItems(event:ResultEvent):void {
			if (createBillingItems.lastResult.name() == "NewBilling") {
				this.billingItems.addItem(createBillingItems.lastResult..Request);
				this.reselectRequestTree();
				onRequestNodeSelected();
			} else {
				Alert.show("An error occurred while creating new billing items.");
			}			
		}		
		private function onGetLab(event:ResultEvent):void {			
			var lab:Object = getLab.lastResult..Lab.(@idLab == labCombo.selectedItem.@idLab);
			billingAccounts = new XMLListCollection(lab.billingAccounts.BillingAccount);
			var blankNode:XML = new XML("<BillingAccount idBillingAccount='' accountName=''/>");
			billingAccounts.addItemAt(blankNode, 0);
			billingAccountCombo.dataProvider = billingAccounts;
		}
		

 		private function onRequestNodeSelected():void {
 			if (this.requestTree.selectedItem == null) {
 				return;
 			}
 			gatherDisplayInfo(this.requestTree.selectedItem);
 		}
 		
 		private function gatherDisplayInfo(selectedItem:Object):void {

			billingItemsDisplayed = new XMLListCollection();
			
			var totalPrice:Number = new Number(0);
			var request:XML = null;
			var lab:XML = null;
			var item:Object = null;
 			if (selectedItem.name() == "Status" && 
 			    selectedItem.@status == 'PENDING') {
 				this.billingItemsDisplayed = new XMLListCollection();
 				for each(request in selectedItem..Request) {
 					for each(item in billingItems) {
	 					if (item.@requestNumber == request.@requestNumber &&
		 					item.@idBillingAccount == request.@idBillingAccount) {
	 						billingItemsDisplayed.addItem(item);
		 					totalPrice = sumTotalPrice(totalPrice, item);
 						}
 					}  
 					
 				}
 			} else if (selectedItem.name() == "Status") {
 				this.billingItemsDisplayed = new XMLListCollection();
 				for each(lab in selectedItem..Lab) {
	 				for each(request in lab.Request) {
	 					for each(item in billingItems) {
		 					if (item.@requestNumber == request.@requestNumber &&
		 					    item.@idBillingAccount == lab.@idBillingAccount) {
		 						billingItemsDisplayed.addItem(item);
		 						totalPrice = sumTotalPrice(totalPrice, item);
	 						}
	 					}  
	 					
 					}
 				}
 			}  else if (selectedItem.name() == "Lab") {
 				this.billingItemsDisplayed = new XMLListCollection();
 				lab = XML(selectedItem); 
 				for each(request in lab.Request) {
 					for each(item in billingItems) {
	 					if (item.@requestNumber == request.@requestNumber &&
	 					    item.@idBillingAccount == lab.@idBillingAccount) {
	 						billingItemsDisplayed.addItem(item);
		 					totalPrice = sumTotalPrice(totalPrice, item);
 						}
 					}  
 				}
 			} else if (selectedItem.name() == "Request") {
 				for each(item in billingItems) { 
 					if (item.@requestNumber == selectedItem.@requestNumber &&
 					    item.@idBillingAccount == selectedItem.@idBillingAccount) {
	 					billingItemsDisplayed.addItem(item);
						totalPrice = sumTotalPrice(totalPrice, item);
 					}
 				}
 			}  
			this.billingItemGrid.dataProvider = new HierarchicalData(this.billingItemsDisplayed);
			this.billingItemGrid.dataProvider.refresh();
			
			if (totalPrice > 0) {
				grandTotalPrice = currencyFormatter.format(totalPrice.toFixed(2).toString());			
			} else {
				grandTotalPrice = "";
			}
 		}
 		
 		private function sumTotalPrice(totalPrice:Number, billingItem:Object):Number {
 			var price:String = billingItem.@totalPrice;
 			price = price.replace('$', '');
 			price = price.replace(',', '');
 			totalPrice += (price != '' ? new Number(price) : 0);
 			return totalPrice;
 		}
        private function onDragDropBillingItemGrid(event:DragEvent) : void
		{
			// For some reason, preventDefault on AdvancedDataGrid
			// isn't enough to stop the defaut drop operation from
			// occurring. So I added hidDropFeedback and showFeedback
			// and that seemed to do the trick.	
			this.billingItemGrid.hideDropFeedback(event);
			event.preventDefault();
			DragManager.showFeedback(DragManager.NONE);

		   
			var ds:DragSource = event.dragSource;
           
           	// retrieve the data associated with the "items" format. This will be the data that
	       	// the dragInitiator has copied into the DragSource.
	       	var items:Array = ds.dataForFormat("treeDataGridItems") as Array;
           
           	var isRequest:Boolean = false;
           	var isPrice:Boolean = false;
           
           	for(var i:int = 0; i < items.length; i++) {
           		var dragItem:Object = items[i];
	       		if (dragItem.name() == "Request") {
	       			isRequest = true;
	       			break;
	       		} else if (dragItem.name() == "Price") {
	       			isPrice = true;
	       			break;
	       		}
           	}
            
	      

		   	// The user didn't drag new request, so just treat
		   	// as click.
		   	if (isRequest) {
		       if (requestTree.selectedItem.name() != "Request" || 
		       requestTree.selectedItem.@codeBillingStatus != "NEW") {
		       		onRequestNodeSelected();
			       	return;
		       }
		   	}
		   	if (isPrice) {
		       if (pricingTree.selectedItem.name() != "Price" ) {
		       		Alert.show("Please drag price onto billing items grid.");
			       	event.preventDefault();
			       	event.target.hideDropFeedback(event);
			       	DragManager.showFeedback(DragManager.NONE);
			       	return;
		       }
		   	}		   
	       
	       	// determine where in the grid the drop occurs and select that node by the index; 
	       	// followed by retrieving the node itself. 
	        var dropNode:XML = null;
	        var dropRequestNode:XML = null;
	        var idBillingAccount:Object = null;
	        var idLab:Object = null;
	       	if (isPrice) {
		       	var r:int = billingItemGrid.calculateDropIndex(event);
		       	if (r > 0) { 
	    	   		r--;
	       		}
	       		billingItemGrid.selectedIndex = r;
	       		dropNode = billingItemGrid.selectedItem as XML;
	       		
	       		// Don't allow drag operation if nothing in billing grid
	       		if (dropNode == null) {
	       			event.preventDefault();
			       	event.target.hideDropFeedback(event);
			       	DragManager.showFeedback(DragManager.NONE);
			       	return;
	       		}
	       		
	       		
	       		if (dropNode.name() == "BillingItem") {
	       			dropRequestNode = dropNode.parent();
	       		} else {
	       			dropRequestNode = dropNode;
	       		} 	      
	       	}
	
 		   	// Initialize billing items if we have anything other than pending items
 		   	if (isRequest){ 
	 		   	if (billingItems == null) {
		 		   	billingItems = new XMLListCollection(); 		
 				   	this.billingItemGrid.dataProvider = new HierarchicalData(billingItems);
				   	this.billingItemGrid.dataProvider.refresh();   	
 		   		}  
 		   	} 
 		   		
	       	// insert a Request node under the Pending status
          	for(var x:int = 0; x < items.length; x++) {
          		var dragNode:Object = items[x]; 
	       		if (dragNode.name() == "Request") {  
	       			this.lastSelectedItem = this.requestTree.selectedItem;
	       			dragNode.@codeBillingStatus = "PENDING";
	       			var params:Object = new Object(); 
	       			params.idRequest = dragNode.@idRequest;
	       			params.idBillingPeriod = billingPeriodCombo.selectedItem != null ? billingPeriodCombo.selectedItem.@value : "";
	       			this.createBillingItems.send(params);
	    	   	} else if (dragNode.name() == "Price") {
       				var newBillingItem:XML = new XML(
       				 "<BillingItem " + 
       				 " codeBillingChargeKind='" + dragNode.@codeBillingChargeKind + "'" +
       			 	 " category='" + dragNode.@category + "'" +
       			 	 " description='" + dragNode.@name + "'" +
       			 	 " unitPrice='" + dragNode.@unitPrice + "'" +	
       			 	 " codeBillingStatus='PENDING'" +	
       			 	 " idBillingPeriod='" + (billingPeriodCombo.selectedItem != null ? billingPeriodCombo.selectedItem.@value : "") + "'" + 
       			 	 " idPriceCategory='" +	dragNode.parent().@idPriceCategory + "'" +
       			 	 " idPrice='" + dragNode.@idPrice + "'" +
       			 	 " idRequest='" + dropRequestNode.@idRequest + "'" +
       			 	 " idBillingAccount='" + dropNode.@idBillingAccount + "'" +
       			 	 " idLab='" + dropNode.@idLab + "'" +
       			 	 " percentagePrice='1'" +
       			 	 " percentageDisplay='100%'" +
       			 	 " notes=''" +
       			 	 " qty=''" + 
       			 	 " isDirty='Y'" +
       			 	 "/>");
		       	
		       		dropRequestNode.@isDirty = "Y";
		       		dropRequestNode.appendChild(newBillingItem);
	       		}
	       	}

			onRequestNodeSelected();
	       
		}
		
		private function onDragDropPricing(event:DragEvent) : void
		{
			// For some reason, preventDefault on AdvancedDataGrid
			// isn't enough to stop the defaut drop operation from
			// occurring. So I added hidDropFeedback and showFeedback
			// and that seemed to do the trick.	
			this.pricingTree.hideDropFeedback(event);
			event.preventDefault();
			DragManager.showFeedback(DragManager.NONE);

		   
			var ds:DragSource = event.dragSource;
           
           	// retrieve the data associated with the "items" format. This will be the data that
	       	// the dragInitiator has copied into the DragSource.
	       	var items:Array = ds.dataForFormat("treeDataGridItems") as Array;
           
           	var isPriceCategory:Boolean = false;
           	
           	for(var i:int = 0; i < items.length; i++) {
           		var dragItem:Object = items[i];
	       		if (dragItem.name() == "PriceCategory") {
	       			isPriceCategory = true;
	       			break;
	       		} else if (dragItem.name() == "Price") {
	       			Alert.show("Drag-and-drop not allowed for Prices.");
	       			return;
	       		} else if (dragItem.name() == "PriceSheet") {
	       			Alert.show("Drag-and-drop not allowed for Price Sheets");
	       			return;
	       		}
           	}
            
		   	// Only allow price categories to be dragged.
		   	if (!isPriceCategory) {
		    	return;
		   	}
		   	
	       	// determine where in the grid the drop occurs and select that node by the index; 
	       	// followed by retrieving the node itself. 
	        var dropNode:XML = null;
	        var dropPriceCategoryNode:XML = null;
	        var dropPriceSheetNode:XML = null;
	        var dropPosition:String = "after";
	       	if (isPriceCategory) {
		       	var r:int = pricingTree.calculateDropIndex(event);
		       	if (r > 0) { 
	    	   		r--;
	       		}
	       		pricingTree.selectedIndex = r;
	       		dropNode = pricingTree.selectedItem as XML;
	       		
	       		// Don't allow drag operation if nothing no drop node
	       		// found
	       		if (dropNode == null) {
	       			event.preventDefault();
			       	event.target.hideDropFeedback(event);
			       	DragManager.showFeedback(DragManager.NONE);
			       	return;
	       		}
	       		
	       		if (dropNode.name() == "PriceSheet") {
	       			dropPriceSheetNode = dropNode;
	       			if (dropPriceSheetNode.childIndex() >= 0) {
	       				dropPriceCategoryNode = dropPriceSheetNode.children()[0];
	       				dropPosition = "before";
	       			} 
	       		} else if (dropNode.name() == "PriceCategory") {
	       			dropPriceSheetNode = dropNode.parent();
	       			dropPriceCategoryNode = dropNode;
	       		} else if (dropNode.name() == "Price") {
	       			dropPriceSheetNode = dropNode.parent().parent();
	       			dropPriceCategoryNode = dropNode.parent();
	       		} 	      
	       	}
	       	
	       	if (dropPriceCategoryNode != null) {
	       		if (dropPriceCategoryNode.@idPriceCategory == dragItem.@idPriceCategory) {
	       			return;
	       		} 
	       	}
	
			// Move the price category
			var params:Object = new Object();
			params.idPriceCategorySource = dragItem.@idPriceCategory;
			params.idPriceSheetTarget = dropPriceSheetNode.@idPriceSheet;
			params.idPriceCategoryPosition = dropPriceCategoryNode != null ? dropPriceCategoryNode.@idPriceCategory : "";
			params.dropPosition = dropPosition;
			
			this.movePriceCategory.send(params);
		}

        private function sortBillingPeriods(obj1:Object, obj2:Object, fields:Array=null):int {
			if (obj1 == null && obj2 == null) {
				return 0;
			} else if (obj1 == null) {
				return 1;
			} else if (obj2 == null) {
				return -1;
			} else {
				var order1:Object = obj1.@startDateSort;
				var order2:Object = obj2.@startDateSort;
				
				if (obj1.@value == '') {
					return -1;
				} else if (obj2.@value == '') {
					return 1;
				} else {
					if (order1 < order2) {
						return -1;
					} else if (order1 > order2) {
						return 1;
					} else {
						return 0;
					}
				}
			}			
		}			 
		
		
		private function validateAndSave():void {
			var isExternalApproved:Boolean = false;
			for each(var request:XML in billingItems) {
 				if (request.@isDirty == "Y") { 
 					if (request.@isExternal == "Y") {
	 					for each(var bi:XML in request.BillingItem) {
	 						if (bi.@codeBillingStatus == 'APPROVED') {
	 							isExternalApproved = true;
	 							break;
	 						}
 						}					
 					}
 				} 
 			}
 			if (isExternalApproved) {
 				Alert.show("External account to be approved.  Has pricing been verified?",
 				  "Warning",
 				  Alert.YES | Alert.NO,
 				  this,
 				  onPromptValidateSave);
 			} else {
 				save();
 			}
		}
		
		private function onPromptValidateSave(event:CloseEvent):void {
			if (event.detail == Alert.YES) {
				save();
			} 
		}
		
 		private function save():void {  
 			var params:Object = new Object();
 			params.billingItemXMLString = "<SaveList>";
 			
 			for each(var request:XML in billingItems) {
 				if (request.@isDirty == "Y") { 
 					for each(var bi:XML in request.BillingItem) {
 						params.billingItemXMLString += bi.toXMLString();	
 					}
 				} 
 			}
 			params.billingItemXMLString += "</SaveList>";
 			 
 			params.billingItemXMLString += "<RemoveList>";
 			for each(var billingItemToRemove:XML in this.billingItemsToRemove) {
 				params.billingItemXMLString += billingItemToRemove.toXMLString();
 			} 
 			params.billingItemXMLString += "</RemoveList>";

 			
 			saveBillingItemList.send(params);
  
 		}
        private function onSaveBillingItemList(event:ResultEvent):void {
        	if (saveBillingItemList.lastResult.name() == "SUCCESS") {
        		dirty.resetDirty();
        		
        		billingItemsToRemove = new XMLListCollection();

        		lastSelectedItem = this.requestTree.selectedItem;
        		billingItemsDisplayed = new XMLListCollection();
        		billingItemGrid.dataProvider = new HierarchicalData(billingItemsDisplayed);
        		
        		if (requestNumberText.text == "") {
	        		this.refreshBillingItems();    			    			
        		} else {
        			this.refreshBillingItemsByRequestNumber();
        		}
        		
        		
        	} else {
        		Alert.show(saveBillingItemList.lastResult..ACTMESSAGE.@TEXT);
        	}        	
        }
        
		public function showBillingInvoice():void {
			
			if (billingPeriodCombo.selectedItem == null ||
			    billingPeriodCombo.selectedItem.@value == "") {
				Alert.show("Please select a billing period.");
				return;    	
			}
			if (this.requestTree.selectedItem != null) {
				var labNode:Object = null;
				if (this.requestTree.selectedItem.name() == "Request") {
					if (this.requestTree.selectedItem.parent().name() == "Lab") {
						labNode = this.requestTree.selectedItem.parent();
					} else {
						Alert.show("Please complete the billing items first.");
						return;
					}					
				} else if (this.requestTree.selectedItem.name() == "Lab") {
					labNode = this.requestTree.selectedItem; 
				}
				if (labNode != null) {
					var url:URLRequest = new URLRequest('ShowBillingInvoiceForm.gx' +
						'?idBillingPeriod=' + billingPeriodCombo.selectedItem.@idBillingPeriod + 
						'&idLab=' + labNode.@idLab +
						'&idBillingAccount=' + labNode.@idBillingAccount);
					navigateToURL(url, '_blank');			
					
				} else {
					Alert.show("Please select a lab folder");
				}
			} else {
				Alert.show("Please select a lab folder");
			}
		} 
		public function showBillingMonthendReport():void {
			
			if (billingPeriodCombo.selectedItem == null ||
			    billingPeriodCombo.selectedItem.@value == "") {
				Alert.show("Please select a billing period.");
				return;    	   
			}
			if (this.requestTree.selectedItem != null) { 
				var statusNode:Object = null;
				if (this.requestTree.selectedItem.name() == "Lab") {
					statusNode = this.requestTree.selectedItem.parent(); 
				} else if (this.requestTree.selectedItem.name() == "Status") {
					statusNode = this.requestTree.selectedItem;
				}
				if (statusNode != null && 
				    (statusNode.@status == "APPROVED" || statusNode.@status == "APPROVEDEX") ) {
					var url:URLRequest = new URLRequest('ShowBillingMonthendReport.gx' +
					    '?codeBillingStatus=' + statusNode.@status +
						'&idBillingPeriod=' + billingPeriodCombo.selectedItem.@idBillingPeriod);
					navigateToURL(url, '_blank');			
					
				} else {
					Alert.show("Please select the 'Approved' or 'Approved (External)' folder.")
				}
				
			} else {
				Alert.show("Please select the 'Approved' or 'Approved (External)' folder.")
			}
		}
		
		public function showBillingSummaryWindow():void {
			var summaryWindow:BillingTotalByLabView = BillingTotalByLabView(mx.managers.PopUpManager.createPopUp(this, BillingTotalByLabView, true));
            mx.managers.PopUpManager.centerPopUp(summaryWindow);
            summaryWindow.setFocus();
            summaryWindow.billingPeriods = this.billingPeriods; 				
		}
		public function showBillingGLInterface():void {
			if (billingPeriodCombo.selectedItem == null ||
			    billingPeriodCombo.selectedItem.@value == "") {
				Alert.show("Please select a billing period.");
				return;    	
			} 
			
			// Gather up grand total for approved node.  We do this as a
			// double-check to make sure the journal entries match to
			// what we see in the interface.
			var approvedNode:Object = null;
			for each(var item:Object in requests) {
				if (item.@label == "Approved") {
					approvedNode = item;
					break;
				}
			}
			if (approvedNode == null) {
				Alert.show("Unable to generate GL interface.  Cannot find approved folder.")
				return;
			}
			this.gatherDisplayInfo(approvedNode);

			// Show popup window for generating gl interface			
    		var runWindow:BillingGLInterfaceView = BillingGLInterfaceView(mx.managers.PopUpManager.createPopUp(this, BillingGLInterfaceView, true));
    		mx.managers.PopUpManager.centerPopUp(runWindow);
    		runWindow.setFocus();
    		runWindow.idBillingPeriod = billingPeriodCombo.selectedItem.@idBillingPeriod;
    		runWindow.billingPeriod = billingPeriodCombo.selectedItem.@billingPeriod;
    		runWindow.grandTotalPrice = this.grandTotalPrice;
		}		
		
		private function showPricingView():void {
			var params:Object = new Object();
			if (pricingTree.selectedItem.name() == "Price") {
				params.idPrice =     this.pricingTree.selectedItem.@idPrice;
				this.getPrice.send(params);
			} else if (pricingTree.selectedItem.name() == "PriceSheet") {
				params.idPriceSheet =     this.pricingTree.selectedItem.@idPriceSheet;
				this.getPriceSheet.send(params);
			} else if (pricingTree.selectedItem.name() == "PriceCategory") {
				params.idPriceCategory =     this.pricingTree.selectedItem.@idPriceCategory;
				this.getPriceCategory.send(params);
			}
		}
		private function showNewPriceView():void {
			var priceCategory:Object = getAncestor(XML(pricingTree.selectedItem), "PriceCategory");
			if (priceCategory == null) {
				Alert.show("Please select a price category");
				return;
			}

			var params:Object = new Object();
			params.idPrice = "0";
			this.getPrice.send(params);
		}
		private function showNewPriceSheetView():void {
			var params:Object = new Object();
			params.idPriceSheet = "0";
			this.getPriceSheet.send(params);
		}
		private function showNewPriceCategoryView():void {
			var priceSheet:Object = getAncestor(XML(pricingTree.selectedItem), "PriceSheet");
			if (priceSheet == null) {
				Alert.show("Please select a price sheet");
				return;
			}
			var params:Object = new Object();
			params.idPriceCategory = "0";
			this.getPriceCategory.send(params);
		}

		
		private function removePricing():void {
			if (this.pricingTree.selectedItem != null) {
				// After the delete operation has finished,
        		// select either the next sibling, or if no more children,
        		// the parent node.
        		var itemToSelect:Object = null;
        		if (this.pricingTree.selectedItem.childIndex() == 0) {
	        		itemToSelect = pricingTree.selectedItem.parent();
        		} else {
        			var idx:int = pricingTree.selectedItem.childIndex() - 1;
        			itemToSelect = pricingTree.selectedItem.parent().children()[idx];
        		}   
        		
        		this.pricingNodeToSelect = itemToSelect.name();
        		this.pricingIdToSelect   = itemToSelect["@id" + this.pricingNodeToSelect];
				
				var params:Object = new Object();
				if (this.pricingTree.selectedItem.name() == "Price") {
					params.idPrice = this.pricingTree.selectedItem.@idPrice;
					this.deletePrice.send(params);
				} else if (this.pricingTree.selectedItem.name() == "PriceCategory") {
					params.idPriceCategory = this.pricingTree.selectedItem.@idPriceCategory;
					params.idPriceSheet = this.pricingTree.selectedItem.parent().@idPriceSheet;
					this.deletePriceCategory.send(params);
				} else if (this.pricingTree.selectedItem.name() == "PriceSheet") {
					params.idPriceSheet = this.pricingTree.selectedItem.@idPriceSheet;
					this.deletePriceSheet.send(params);
				}
			}
		}
		private function onGetPrice(event:ResultEvent):void {		
			if (getPrice.lastResult.name() != "PriceList") {
				Alert.show(getPrice.lastResult..ERROR.@message);
				return;
			}	
			var price:Object = getPrice.lastResult..Price;
			var priceCategory:Object = getAncestor(XML(pricingTree.selectedItem), "PriceCategory");
            if (priceCategory != null) {
	       		var priceWindow:PriceView = PriceView(mx.managers.PopUpManager.createPopUp(this, PriceView, true));
            	mx.managers.PopUpManager.centerPopUp(priceWindow);
            	priceWindow.setFocus();
                priceWindow.init(price, priceCategory);
                priceWindow.priceName.setFocus();				        	
            }
		}

		private function onDeletePrice(event:ResultEvent):void {			
			if (deletePrice.lastResult.name() == "SUCCESS") {
				this.refresh();			        	
            } else {
            	Alert.show(deletePrice.lastResult..ERROR.@message);
            }
		}

		private function onGetPriceSheet(event:ResultEvent):void {			
			if (getPriceSheet.lastResult.name() != "PriceSheetList") {
				Alert.show(getPriceSheet.lastResult..ERROR.@message);
				return;
			}	

			var priceSheet:Object = getPriceSheet.lastResult..PriceSheet;
            if (priceSheet != null) {
	       		var priceSheetWindow:PriceSheetView = PriceSheetView(mx.managers.PopUpManager.createPopUp(this, PriceSheetView, true));
            	mx.managers.PopUpManager.centerPopUp(priceSheetWindow);
            	priceSheetWindow.setFocus();
                priceSheetWindow.init(priceSheet);
                priceSheetWindow.priceSheetName.setFocus();				        	
            } else {
            	Alert.show("Unable to get price sheet");
            }
		}

		private function onDeletePriceSheet(event:ResultEvent):void {			
			if (deletePriceSheet.lastResult.name() == "SUCCESS") {
				this.refresh();			        	
            } else {
            	Alert.show(deletePriceSheet.lastResult..ERROR.@message);
            }
		}
		
		private function onGetPriceCategory(event:ResultEvent):void {	
			if (getPriceCategory.lastResult.name() != "PriceCategoryList") {
				Alert.show(getPriceCategory.lastResult..ERROR.@message);
				return;
			}		
			var priceCategory:Object = getPriceCategory.lastResult..PriceCategory;
			var priceSheet:Object = getAncestor(XML(pricingTree.selectedItem), "PriceSheet");
            if (priceSheet != null) {
	       		var priceCategoryWindow:PriceCategoryView = PriceCategoryView(mx.managers.PopUpManager.createPopUp(this, PriceCategoryView, true));
            	mx.managers.PopUpManager.centerPopUp(priceCategoryWindow);
            	priceCategoryWindow.setFocus();
                priceCategoryWindow.init(priceCategory, priceSheet);
                priceCategoryWindow.priceCategoryName.setFocus();				        	
            } else {
            	Alert.show("Please select the price sheet");
            }
		}

		private function onDeletePriceCategory(event:ResultEvent):void {			
			if (deletePriceCategory.lastResult.name() == "SUCCESS") {
				this.refresh();			        	
            } else {
           		Alert.show(deletePriceCategory.lastResult..ERROR.@message);
            }
		}

		private function onMovePriceCategory(event:ResultEvent):void {			
			if (movePriceCategory.lastResult.name() == "SUCCESS") {
				Alert.show(movePriceCategory.lastResult.@message);
				this.refresh();			        	
            } else {
           		Alert.show(movePriceCategory.lastResult..ERROR.@message);
            }
		}


        private function getAncestor(item:XML, ancestorName:String):XML {
        	var ancestor:XML = null;
        	while( item )
			{
				if( item.name() == ancestorName )
				{
					ancestor = item;
					break;
				}
				item = item.parent();
			}
			return ancestor;
        }

		
		private function showSplitAccountsView():void {
			var requestNode:Object = null;
			if (this.billingItemGrid.selectedItem != null && this.billingItemGrid.selectedItem.name() == "Request") {
				  
				requestNode = this.billingItemGrid.selectedItem;
			} else if (this.billingItemGrid.selectedItem != null && this.billingItemGrid.selectedItem.name() == "BillingItem") {
				requestNode = this.billingItemGrid.selectedItem.parent();
			} else if (this.billingItemGrid.selectedItem == null) {
				for each(var item:Object in this.billingItemsDisplayed) {
					// We have more than 1 request in the billing item grid, so we don't
					// know which one to selected.  
					if (requestNode != null && requestNode.@idRequest != item.@idRequest) {
						requestNode = null;
						break;
					}
					// We only have one request is the billing item grid.  Assume
					// this one should be split.
					requestNode = item;
				}
			}
			
			if (requestNode == null) { 
				Alert.show("Please select an experiment (in the list) to split into separate accounts.");
				return;
			}
			
			
			if (requestNode.@status == "NEW") {
					Alert.show("Please save the billing items before splitting into separate accounts.");
					return;				
			} 
			
			var billingAccountEntries:XMLListCollection = new XMLListCollection();	
			var billingPeriods:XMLListCollection = new XMLListCollection();
			var labs:XMLListCollection = new XMLListCollection();
			for each(var lab:Object in parentApplication.promptedLabList) {
				labs.addItem(lab);
			}
			for each(var r:Object in this.billingItemsDisplayed) {
				for each(var billingItem:Object in r.BillingItem) {
					if (billingItem.@idRequest != requestNode.@idRequest) {
						continue; 
					}
					
					var found:Boolean = false;
					for each(var acct:Object in billingAccountEntries) {
						if (acct.@idBillingAccount == billingItem.@idBillingAccount) {
							found = true;
							break; 
						} 
					}  
					if (!found) {
	       				var newBillingAccount:XML = new XML(
		   				 "<BillingAccount " + 
	   			 	 	 " idBillingAccount='" + billingItem.@idBillingAccount + "'" +
	   			 	 	 " accountName='" + billingItem.@accountNameAndNumber + "'" +
	   			 	 	 " idLab='" + billingItem.@idLab + "'" +
	   			 	 	 " labName='" + billingItem.@labName + "'" +
	   			 	 	 " percentage='" + billingItem.@percentageDisplay + "'" +
	   			 	 	 "/>"); 
	   			 	 	 billingAccountEntries.addItem(newBillingAccount);
					} 
					
					var foundBillingPeriod:Boolean = false;
					for each(var bp:Object in billingPeriods) {
						if (bp.@idBillingPeriod == billingItem.@idBillingPeriod) {
							foundBillingPeriod = true;
							break; 
						} 
					}  
					if (!foundBillingPeriod) {
						var newBillingPeriod:XML = new XML("<BillingPeriod idBillingPeriod='" + billingItem.@idBillingPeriod + "'/>");
						billingPeriods.addItem(newBillingPeriod);
					}
					
				}
				
			}
			
			if (billingPeriods.length > 1) {
				Alert.show("This experiment has billing items spanning multiple billing periods.  Please select a billing period from the dropdown to qualify the billing items to be split.");
				return;
			}
			
			if (billingPeriods.length == 0) {
				Alert.show("Please select a billing period from the dropdown first.");
				return;
			}
			
			var idBillingPeriod:Object = billingPeriods.getItemAt(0).@idBillingPeriod;
			
			var splitWindow:BillingSplitView = BillingSplitView(mx.managers.PopUpManager.createPopUp(this, BillingSplitView, true));
        	mx.managers.PopUpManager.centerPopUp(splitWindow); 
        	splitWindow.setFocus();
        	splitWindow.idBillingPeriod = idBillingPeriod;
        	splitWindow.request = requestNode;			
        	splitWindow.labs = labs;
        	splitWindow.billingAccountEntries = billingAccountEntries;
        	splitWindow.initializeData();
			
		}
		

		private function showReassignAccountView():void {
			var requestNode:Object = null;
			if (this.dirty.isDirty()) {
				Alert.show("Please save the billing items before trying to reassign the account.");
				return;
			}
			if (this.billingItemGrid.selectedItem != null && this.billingItemGrid.selectedItem.name() == "Request") {				 
				requestNode = this.billingItemGrid.selectedItem;
				if (requestNode.@status == "NEW") {
						Alert.show("Please save the new billing items before trying to reassign the account.");
						return;				
				} 
			} else if (this.requestTree.selectedItem != null && this.requestTree.selectedItem.name() == "Request") {
				requestNode = this.requestTree.selectedItem;
				if (requestNode.@codeBillingStatus == "NEW") {
						Alert.show("Please save the new billing items before trying to reassign the account.");
						return;				
				} 
			} 
			
			if (requestNode == null) { 
				Alert.show("To reassign the account, you must first select an experiment in the list.");
				return;
			}
			
			
			
			var billingPeriods:XMLListCollection = new XMLListCollection();
			for each(var r:Object in this.billingItemsDisplayed) {
				for each(var billingItem:Object in r.BillingItem) {
					if (billingItem.@idRequest != requestNode.@idRequest) {
						continue; 
					}
					var foundBillingPeriod:Boolean = false;
					for each(var bp:Object in billingPeriods) {
						if (bp.@idBillingPeriod == billingItem.@idBillingPeriod) {
							foundBillingPeriod = true;
							break; 
						} 
					}  
					if (!foundBillingPeriod) {
						var newBillingPeriod:XML = new XML("<BillingPeriod idBillingPeriod='" + billingItem.@idBillingPeriod + "'/>");
						billingPeriods.addItem(newBillingPeriod);
					}
					
				}
				
			}
			
			if (billingPeriods.length > 1) {
				Alert.show("This experiment has billing items spanning multiple billing periods.  Please select a billing period from the dropdown to qualify the billing items to be reassigned to different account.");
				return;
			}
			if (billingPeriods.length == 0) {
				Alert.show("Please select a billing period from the dropdown first.");
				return;
			}
			var idBillingPeriod:Object = billingPeriods.getItemAt(0).@idBillingPeriod;
			
			
			var reassignWindow:BillingReassignView = BillingReassignView(mx.managers.PopUpManager.createPopUp(this, BillingReassignView, true));
        	mx.managers.PopUpManager.centerPopUp(reassignWindow);
        	reassignWindow.setFocus();
        	reassignWindow.request = requestNode;
        	reassignWindow.idBillingPeriod = idBillingPeriod;
        	reassignWindow.initializeData();

		}
		
		private function onBillingTabChange():void {
			if (billingTabNavigator.selectedChild == this.priceTab) {
				this.requestTabWidth = this.billingTabNavigator.width;
				this.billingTabNavigator.width = this.priceTabWidth;
			} else {
				this.priceTabWidth = this.billingTabNavigator.width;
				this.billingTabNavigator.width = this.requestTabWidth;
			}
		}


 	]]>
 </mx:Script>	
 
  <mx:VBox width="100%" verticalGap="4" height="100%" paddingTop="0">
 
	    <mx:VBox width="100%" styleName="panelBox" horizontalGap="8" paddingTop="0" verticalGap="2" paddingRight="0" paddingLeft="0">
	                    <mx:HBox backgroundSize="100%" width="100%" backgroundImage="@Embed(source='../../assets/filterBarNarrow.png')" verticalAlign="middle" paddingTop="4" paddingBottom="4">
	                                    <mx:LinkButton textDecoration="underline" label="Refresh" icon="@Embed('../../assets/action_refresh.gif')" 
	                                               click="refresh()" toolTip="Refresh"/>
	                                    <mx:VRule height="20"/>
	                                    <mx:LinkButton label="Invoice" textDecoration="underline" icon="@Embed(source='../../assets/email_open.png')" click="showBillingInvoice()"/>
	                                    <mx:VRule height="20"/>
	                                    <mx:LinkButton  id="glFileButton" label="Generate GL File" textDecoration="underline" icon="@Embed(source='../../assets/feed_disk.png')" click="showBillingGLInterface()"/>
	                                    <mx:VRule height="20"/>
	                                    <mx:LinkButton label="Monthend Report" textDecoration="underline" icon="@Embed(source='../../assets/page.png')" click="showBillingMonthendReport()"/>
	                                    <mx:VRule height="20"/>
	                                    <mx:LinkButton  id="summaryReportButton" label="Total Billing by Lab Report" textDecoration="underline" icon="@Embed(source='../../assets/page.png')" click="showBillingSummaryWindow()"/>
	                                    <mx:VRule height="20"/>
	                                    <mx:Spacer width="30"/>
	                                    <mx:HBox horizontalGap="0" verticalAlign="middle" verticalGap="0" horizontalAlign="right" paddingRight="0"   paddingLeft="8">
	                                    	<mx:Label id="requestNumberLabel" visible="true" text="Lookup by Experiment #" paddingBottom="0" styleName="labelSmall"/>
	                                    	<mx:TextInput id="requestNumberText" width="60">
	                                    	</mx:TextInput>			   	
	                                    	<mx:Button id="lookupButton"  
	                                    		height="18"
	                                    		click="refresh()" visible="true" icon="@Embed('../../assets/arrow_right.png')" width="20"/>
	                                     </mx:HBox>
                	
                	
                </mx:HBox>

	      <mx:HBox horizontalGap="8" verticalAlign="middle">
		    	<mx:HBox horizontalGap="0" verticalAlign="middle" verticalGap="0" horizontalAlign="right" paddingRight="0"  >
					<mx:Label text="Filter by:   " paddingLeft="4"/>
					<mx:Label id="billingPeriodLabel" visible="true" text="Period" paddingBottom="0"/>
					<mx:ComboBox id="billingPeriodCombo" 
						labelField="@display"
						visible="true"  
						width="100" change="lastSelectedBillingPeriod=billingPeriodCombo.selectedItem;requestNumberText.text = &quot;&quot;;refresh()">
					</mx:ComboBox>			   	
		    	</mx:HBox>  

			   <mx:HBox horizontalGap="0" verticalAlign="middle">
					<mx:Label id="groupLabel" visible="true" text="Group" paddingBottom="0"/>
					<mx:ComboBox id="labCombo" 
						labelField="@name"
						dataProvider="{parentApplication.promptedLabList}"
						visible="true"  width="160" change="requestNumberText.text = &quot;&quot;;getLab.send();refresh()">
					</mx:ComboBox>
			   </mx:HBox>

		    	<mx:HBox horizontalGap="0" height="100%" verticalAlign="middle" verticalGap="0" horizontalAlign="right" paddingRight="0">
					<mx:Label id="billingAccountLabel" visible="true" text="Account" paddingBottom="0"/>
					<mx:HBox width="100%" height="100%" verticalGap="0" horizontalGap="0" paddingLeft="0" paddingBottom="0" paddingRight="0" paddingTop="0" verticalAlign="middle">
						<mx:ComboBox id="billingAccountCombo" 
							labelField="@accountNameDisplay"
							 visible="true"  width="400" change="requestNumberText.text = &quot;&quot;;refresh()">
						</mx:ComboBox>
						<mx:VBox verticalGap="0" paddingBottom="0" paddingTop="0">
							<mx:RadioButton label="Acct name" styleName="labelSmall" selected="true" id="radioShowBillingAccountName" paddingBottom="0" paddingLeft="4" paddingRight="0" paddingTop="0" textAlign="left" horizontalGap="0">
								<mx:click>{radioShowBillingAccountName.selected ? billingAccountCombo.labelField = '@accountNameDisplay' : billingAccountCombo.labelField = '@accountNumberDisplay'}</mx:click>
							</mx:RadioButton>
							<mx:RadioButton label="Acct #" styleName="labelSmall" id="radioShowBillingAccountNumber" paddingLeft="4" paddingBottom="0" paddingRight="0" paddingTop="0" horizontalGap="0">
								<mx:click>{radioShowBillingAccountName.selected ? billingAccountCombo.labelField = '@accountNameDisplay' : billingAccountCombo.labelField = '@accountNumberDisplay'}</mx:click>
							</mx:RadioButton>
							
						</mx:VBox>
					</mx:HBox>
			   	
		    	</mx:HBox> 
		   </mx:HBox> 	
	    </mx:VBox>

        <mx:HDividedBox height="100%" width="100%">
        	<mx:TabNavigator height="100%" width="220" id="billingTabNavigator"
        		paddingTop="2" styleName="navTabNavigator" 
        		paddingLeft="4" paddingRight="4" paddingBottom="4"
 change="{onBillingTabChange()}" dropShadowEnabled="true">
        		<mx:VBox label="Experiments" height="100%" verticalGap="0" width="100%" color="#000000" >
        			<mx:HBox horizontalGap="0" paddingBottom="2" paddingTop="0" color="#000000">
						<mx:LinkButton label="Pending" textDecoration="underline" paddingRight="0" styleName="formLinkSmallButton" click="jumpTo(&quot;Pending&quot;)" id="jumpPending"/>
						<mx:LinkButton label="Completed" textDecoration="underline" paddingRight="0" styleName="formLinkSmallButton" click="jumpTo(&quot;Completed&quot;)" id="jumpCompleted"/>
						<mx:LinkButton label="Approved" textDecoration="underline" paddingRight="0"  styleName="formLinkSmallButton" click="jumpToApproved()" id="jumpApproved"/>
        				
        			</mx:HBox>
        		    
					<mx:AdvancedDataGrid id="requestTree" width="100%" height="100%"  
						showHeaders="false"  
						displayItemsExpanded="true" 
						dragEnabled="true" 
						itemClick="onRequestNodeSelected()"
						iconFunction="getTreeIcon"  
						labelFunction="getTreeLabel"
						showDataTips="true"
						dragMoveEnabled="false" dropEnabled="false" borderStyle="solid" dropShadowEnabled="true">
    		 	        <mx:dataProvider>
  						  <mx:HierarchicalData id="rc" source="{requests}"/>
				        </mx:dataProvider>		 				  
				        <mx:columns>
  						  <mx:AdvancedDataGridColumn dataField="@label"  dataTipField="@dataTip"/> 
  					    </mx:columns>
  					</mx:AdvancedDataGrid>
       			
        		</mx:VBox> 
        		
        		<mx:VBox id="priceTab" label="Prices" height="100%" verticalGap="0" width="100%" color="#000000">
					<mx:HBox width="100%">
						<mx:CheckBox label="Show inactive prices" id="showInactivePricesCheckbox" click="{this.getPricingList.send()}"/>
						<mx:Spacer width="100%"/>
						<mx:VBox styleName="dragAndDropHint" borderStyle="solid" cornerRadius="4">
							<mx:LinkButton label="Drag-and-drop hint" 
								click="{Alert.show('Drag-and-drop price category to reorder within a price sheet or attach to another price sheet.')}">
							</mx:LinkButton>
						</mx:VBox>
					</mx:HBox>
					<mx:AdvancedDataGrid id="pricingTree" width="100%" height="100%" 
						 showHeaders="true" 
						 sortExpertMode="true" 
						 displayItemsExpanded="true" 
						 iconFunction="getTreeIcon" 
						 sortableColumns="false"
						 dropEnabled="true"  
        				 dragDrop="this.onDragDropPricing(event)"
						 dragEnabled="true" 
						 dragMoveEnabled="true" 
						 doubleClick="showPricingView()" doubleClickEnabled="true"
						 borderStyle="solid" dropShadowEnabled="true" headerWordWrap="true" 
						 resizableColumns="true">
    		 	        <mx:dataProvider> 
  						  <mx:HierarchicalData source="{pricingList}"/>
				        </mx:dataProvider>						  
				        <mx:columns>
  						  <mx:AdvancedDataGridColumn dataField="@display"  headerText="" width="200"/> 
  						  <mx:AdvancedDataGridColumn dataField="@unitPriceCurrency"  headerText="Price" width="50" textAlign="right" /> 
  						  <mx:AdvancedDataGridColumn dataField="@unitPriceExternalCurrency"  headerText="External Price" width="50" textAlign="right" /> 
  					    </mx:columns>
  					</mx:AdvancedDataGrid>
  					<mx:ControlBar paddingBottom="2" horizontalGap="0" paddingTop="3" paddingLeft="0" paddingRight="0">
  								<mx:LinkButton label="New sheet" textDecoration="underline" click="showNewPriceSheetView()" textAlign="left" icon="@Embed(source='../../assets/pricesheet_add.png')">
  								</mx:LinkButton>
  								<mx:LinkButton label="New category" textDecoration="underline" click="showNewPriceCategoryView()" textAlign="left" >
  									<mx:icon>@Embed(source='../../assets/folder_money_add.png')</mx:icon>
  								</mx:LinkButton>
  								<mx:LinkButton label="New price" textDecoration="underline" click="showNewPriceView()"  textAlign="left" icon="@Embed(source='../../assets/money_add.png')"/>
  								<mx:LinkButton  enabled="{this.pricingTree.selectedItem != null}" label="Remove" textDecoration="underline" click="removePricing()" textAlign="left" disabledIcon="@Embed(source='../../assets/delete_disable.png')" icon="@Embed(source='../../assets/delete.png')"/>
  					    	
  					</mx:ControlBar>        		    
        		</mx:VBox>
        	</mx:TabNavigator>
 
			        	
        	<util:PanelBox width="100%" height="100%">
        		<mx:Label id="titleLabel" styleName="titleLabel" text="{'Billing Items' + (this.requestTree.selectedItem != null ? '     ' + this.requestTree.selectedItem.@label + '     ' + this.grandTotalPrice : '')}"/>
        		<mx:AdvancedDataGrid width="100%" height="100%"  
        			displayItemsExpanded="true"  
        			editable="true" 
        			sortExpertMode="true" 
        			sortableColumns="false"   
        			id="billingItemGrid" 
        			dropEnabled="true"  
        			dragDrop="onDragDropBillingItemGrid(event)"
                    allowMultipleSelection="false" 
                    iconFunction="getTreeIcon" 
                    wordWrap="true" 
                    showDataTips="true"
                    headerWordWrap="true" 
                    selectionMode="singleRow" 
                     alternatingItemColors="[0xFFFFFF, 0xEEEEE0]" dropShadowEnabled="true" borderStyle="solid">
        		 	<mx:dataProvider>   
      						<mx:HierarchicalData source="{billingItemsDisplayed}"/> 
    				</mx:dataProvider> 
        			<mx:columns> 
        				<mx:AdvancedDataGridColumn headerText="#" dataField="@requestNumber" editable="false" width="12"  />
        				<mx:AdvancedDataGridColumn headerText="Group" dataField="@billingLabName" editable="false" width="10" wordWrap="true"/>
        				<mx:AdvancedDataGridColumn headerText="Client" dataField="@submitter" editable="false" width="9" wordWrap="false"/>
        				<mx:AdvancedDataGridColumn headerText="Acct" dataField="@billingAccountName" editable="false" width="9" wordWrap="false" dataTipField="@billingAccountName"/>
        				<mx:AdvancedDataGridColumn headerText="Billing period" dataField="@idBillingPeriod" editable="true" width="13"  wordWrap="false"
        			      editorDataField="value"  
        				  rendererIsEditor="false"   
						  itemRenderer="{DropdownLabel.getFactory(parentApplication.dictionaryManager.xml.Dictionary.(@className=='hci.gnomex.model.BillingPeriod').DictionaryEntry,'@display', '@value', '@idBillingPeriod')}"                              
						  itemEditor="{views.renderers.ComboBoxBillingPeriod.create(billingPeriods, '@display', '@value', '@idBillingPeriod', null, true)}"/>                 
        				<mx:AdvancedDataGridColumn headerText="%" dataField="@percentageDisplay" editable="true" textAlign="right" width="6" wordWrap="false"
        				  editorDataField="text"
        				  rendererIsEditor="false"
						  itemRenderer="views.renderers.LabelPercentage" />                              
        				<mx:AdvancedDataGridColumn headerText="Type" dataField="@codeBillingChargeKind" editable="true" width="6" wordWrap="false" 
        			      editorDataField="value" 
        				  rendererIsEditor="false"
						  itemRenderer="{DropdownLabel.getFactory(parentApplication.dictionaryManager.xml.Dictionary.(@className=='hci.gnomex.model.BillingChargeKind').DictionaryEntry,'@display', '@value', '@codeBillingChargeKind')}"                              
						  itemEditor="{hci.flex.controls.ComboBox.getFactory(parentApplication.dictionaryManager.xml.Dictionary.(@className=='hci.gnomex.model.BillingChargeKind').DictionaryEntry,'@display', '@value', '@codeBillingChargeKind', true)}"/>                 
        				<mx:AdvancedDataGridColumn headerText="Price Category" dataField="@category" editable="false" width="12" wordWrap="true"/>
        				<mx:AdvancedDataGridColumn headerText="Description" dataField="@description" editable="true" width="12" wordWrap="true"/>
        				<mx:AdvancedDataGridColumn headerText="Notes" dataField="@notes" editable="true" width="12" wordWrap="true"/>
        				<mx:AdvancedDataGridColumn headerText="Unit price" dataField="@unitPrice"  textAlign="right" editable="true" width="8" wordWrap="false"/> 
        				<mx:AdvancedDataGridColumn headerText="Qty" dataField="@qty" editable="true" textAlign="right" width="5" wordWrap="false"/>
        				<mx:AdvancedDataGridColumn headerText="Total price" dataField="@totalPrice" textAlign="right" width="11" editable="false" wordWrap="false"/>
        				<mx:AdvancedDataGridColumn headerText="Status" dataField="@codeBillingStatus" editable="true" width="18" wordWrap="false"
        			      editorDataField="value"  
        				  rendererIsEditor="false"
						  itemRenderer="{DropdownLabel.getFactory(parentApplication.dictionaryManager.xml.Dictionary.(@className=='hci.gnomex.model.BillingStatus').DictionaryEntry,'@display', '@value', '@codeBillingStatus')}"                              
						  itemEditor="{ComboBoxBillingStatus.create('@codeBillingStatus')}"/>                 
        			</mx:columns>
        			 
        		</mx:AdvancedDataGrid>     
   
				<mx:ControlBar width="100%">
				
				      <mx:HBox width="100%" horizontalAlign="right">
				        <mx:HBox width="100%">
				        	<mx:LinkButton  label="Reassign account" disabledIcon="@Embed(source='../../assets/account_disable.png')" icon="@Embed(source='../../assets/account.png')" toolTip="Reassign charges to another account"  textDecoration="underline" click="showReassignAccountView()" visible="true">
				        		<mx:enabled>{(billingItemGrid.selectedItem != null &amp;&amp; billingItemGrid.selectedItem.name() == 'Request') || (this.requestTree.selectedItem != null &amp;&amp; this.requestTree.selectedItem.name() == 'Request') ? true : false}</mx:enabled>
				        	</mx:LinkButton> 
				        	<mx:LinkButton  label="Split accounts" disabledIcon="@Embed(source='../../assets/account_split_disable.png')" icon="@Embed(source='../../assets/account_split.png')" toolTip="Split charges into multiple accounts"  textDecoration="underline" click="showSplitAccountsView()" visible="true"/>
				        	<mx:LinkButton id="removeBillingItemButton" label="Remove" disabledIcon="@Embed(source='../../assets/delete_disable.png')" icon="@Embed(source='../../assets/delete.png')" toolTip="Remove biling item"  textDecoration="underline" click="removeBillingItem()" enabled="{billingItemGrid.selectedItem != null}"/>
				        	<mx:Spacer width="100%"/>
				        	 
				        </mx:HBox>
				        <util:DirtyNote id="dirty" />
				      	<mx:Button id="saveButton" label="Save" click="validateAndSave()"
				             	icon="@Embed('../../assets/action_save.gif')">
				        </mx:Button>        	
				      </mx:HBox>
				 </mx:ControlBar>

        	</util:PanelBox>
        </mx:HDividedBox>
  </mx:VBox>
  
	

</mx:Canvas>