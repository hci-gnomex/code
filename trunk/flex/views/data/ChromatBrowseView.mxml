<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml"
		 width="100%"
		 height="100%" backgroundAlpha="0"
		 xmlns:util="views.util.*"
		 xmlns:renderers="views.renderers.*"
		 initialize="init()"
		 xmlns:s="library://ns.adobe.com/flex/spark" verticalGap="3">
	
	<mx:HTTPService id="getChromatogramList"
					url="GetChromatogramList.gx"
					resultFormat="e4x"
					showBusyCursor="true"
					result="onGetChromatogramList(event)"
					fault="parentApplication.onFailHttpRequest('Failed to get chromatogram list', event)"
					method="POST"
					useProxy="false">
	</mx:HTTPService>
	
	<mx:HTTPService id="saveChromatogramList"
					url="SaveChromatogramList.gx"
					resultFormat="e4x"
					showBusyCursor="true"
					result="onSaveChromatogramList(event)"
					fault="parentApplication.onFailHttpRequest('Failed to save chromatogram list', event)"
					method="POST"
					useProxy="false">
	</mx:HTTPService>
	
	<mx:HTTPService id="deleteChromatograms"
					url="DeleteChromatograms.gx"
					resultFormat="e4x"
					showBusyCursor="true"
					result="onDeleteChromatograms(event)"
					fault="parentApplication.onFailHttpRequest('Failed to delete chromatogram list', event)"
					method="POST"
					useProxy="false">
	</mx:HTTPService>
	
	<mx:HTTPService  
		id="cacheFileDownloadList" 
		url="CacheFileDownloadList.gx"
		destination="cacheFileDownloadList"
		resultFormat="e4x"
		showBusyCursor="false"
		result="onCacheFileDownloadList(event)"
		fault="parentApplication.onFailHttpRequest('Failed to get cache file download list', event)"
		method="POST"
		useProxy="false">
	</mx:HTTPService>
	
	<mx:HTTPService  
		id="cacheFDTFileDownloadList" 
		url="CacheFileDownloadList.gx"
		destination="cacheFileDownloadList"
		resultFormat="e4x"
		showBusyCursor="true"
		result="onCacheFDTFileDownloadList(event)"
		fault="parentApplication.onFailHttpRequest('Failed to get cache file download list', event)"
		method="POST"
		useProxy="false">
	</mx:HTTPService>
	
	<mx:Script>
		<![CDATA[
			import hci.flex.controls.DropdownLabel;
			
			import mx.collections.ListCollectionView;
			import mx.controls.Alert;
			import mx.controls.List;
			import mx.core.ScrollPolicy;
			import mx.events.CloseEvent;
			import mx.events.CollectionEvent;
			import mx.events.CollectionEventKind;
			import mx.events.ItemClickEvent;
			import mx.events.ListEvent;
			import mx.events.PropertyChangeEvent;
			import mx.managers.PopUpManager;
			import mx.rpc.events.ResultEvent;
			import mx.utils.ObjectUtil;
			
			import views.experiment.DownloadProgressView;
			import views.util.FileUtil;
			
			// Warning Icon
			[Embed( source = '../../assets/error.png' )]
			[Bindable]
			public var iconWarning:Class;
			
			public var parentView:NavDataView;
			
			[Bindable]
			public var selectedFiles:XMLListCollection;
			
			[Bindable]
			public var idRequest:String;
			
			[Bindable]
			private var actionDictionary:XMLListCollection = new XMLListCollection();
			private var dictXML:XML = <actionDictionary>
									  <action value="RELEASE"   display="Release data to user" />
									  <action value="REQUEUE"    display="Release and requeue samples" />
								      </actionDictionary>;
			
			
			
			private var estimatedDownloadSize:Number;
			
			private const DOWNLOAD_FILES_URL:String    = "DownloadFileServlet.gx"; 
			private const SUGGESTED_LOCAL_FILENAME:String = "gnomex-data";  
			
			private var filesExceedingMaxMessage:String = "";
			
			public var downloadProgressWindow:DownloadProgressView;
			
			public static const   ALERT_BUTTON_WIDTH:int = Alert.buttonWidth;
			
			
			
			private function init():void {
				
				if ( currentState == userState.name ) {
					// Don't get the chromatogram list here because the idRequest
					// isn't initialized yet.
					
				} else {
					if ( !initialized ) {
						this.notReleasedCheckbox.selected = true;
					}
					actionDictionary.source = dictXML.action;
					this.getList();
				}
			}
			
			public function getList():void {
				this.getChromatogramList.send( getChromatParameters() );
			}
			
			private function getChromatParameters():Object {
				var params:Object = new Object;
				
				if ( currentState != userState.name ) {
					params.requestNumber = requestNoLookupField.text != null ? requestNoLookupField.text : '' ;
					params.runName = runNameLookupField.text != null ? runNameLookupField.text : '';
					params.plateName = plateNameLookupField.text != null ? plateNameLookupField.text : '';
					params.capSeq = this.capSeqCheckbox.selected ? 'Y' : 'N' ;
					params.mitSeq = this.mitSeqCheckbox.selected ? 'Y' : 'N' ;
					params.lastWeek = this.lastWeekCheckbox.selected ? 'Y' : 'N' ;
					params.lastMonth = this.lastMonthCheckbox.selected ? 'Y' : 'N' ;
					params.lastThreeMonths = this.lastThreeMonthsCheckbox.selected ? 'Y' : 'N' ;
					if ( this.releasedCheckbox.selected ) {
						params.released = 'Y';
					} else if ( this.notReleasedCheckbox.selected ) {
						params.released = 'N';
					}
				}
				
				if ( currentState == userState.name ) {
					params.released = 'Y';
					params.idRequest = idRequest;
				}
				
				return params;
			}
			
			
			private function onGetChromatogramList( event:ResultEvent ):void {
				if ( event.result.name() == "ChromatogramList" ) {
					selectedChromats.refresh();
					comboBox.selectedIndex = -1;
					return;
				}
				Alert.show( event.result..ERROR.@message.toString(), "Error getting chromatogram list" );
				
			}
			
			
			private function gridItemClick( event:ListEvent ):void {
				if ( currentState != userState.name ) {
					if ( chromatogramGrid.selectedItem != null ) {
						parentView.selectedChromat = chromatogramGrid.selectedItem;
						parentView.selectedIdChromatogram = chromatogramGrid.selectedItem.@idChromatogram as int;
					}
					
					if ( event.columnIndex == 2 ) {
						parentView.chromatViews.selectedChild = parentView.chromatDetailView;	
					}
				}
				if ( event.columnIndex == 2 && currentState == userState.name ) {
					var addWindow:ChromatDetailView;
					addWindow = ChromatDetailView(mx.managers.PopUpManager.createPopUp(parentApplication.theBody, ChromatDetailView, true));
					addWindow.selectedChromat = chromatogramGrid.selectedItem;
					addWindow.selectedIdChromatogram = chromatogramGrid.selectedItem.@idChromatogram as int;
					addWindow.currentState = "userState";
					addWindow.init();
					mx.managers.PopUpManager.centerPopUp(addWindow);
					addWindow.setFocus();
				}
				if ( event.columnIndex == 3 ) {
					var rawWindow:RawChromatWindow;
					rawWindow = RawChromatWindow(mx.managers.PopUpManager.createPopUp(parentApplication.theBody, RawChromatWindow, true));
					rawWindow.selectedIdChromatogram = chromatogramGrid.selectedItem.@idChromatogram;
					rawWindow.init();
					mx.managers.PopUpManager.centerPopUp(rawWindow);
					rawWindow.setFocus();
				}
				if ( event.columnIndex == 1 && chromatogramGrid.selectedItem != null ) {
					var url:URLRequest = new URLRequest( chromatogramGrid.selectedItem.@viewURL );
					navigateToURL( url, '_blank' );
				}
				if ( event.columnIndex != 0 && event.columnIndex != 2 ) {
					chromatogramGrid.selectedIndex = -1;
				}
				if ( event.columnIndex == 0 && chromatogramGrid.selectedItems.length > 1 ) {
					for each ( var obj1:Object in chromatogramGrid.selectedItems ) {
						obj1.@isSelected = true;
					}
				}
				
				selectedChromats.refresh();
			}
			

			private function submitAction():void {
				if ( currentState == userState.name ) {
					// download
				}
				
				if ( comboBox.selectedItem == null ) {
					return;
				}
				
				// Release Data to Users
				if ( comboBox.selectedItem.@value == "RELEASE" ) {
					for each ( var ch:Object in selectedChromats ) {
						ch.@released = "Y";
					}
					var params:Object = new Object;
					params.chromatogramXMLString = selectedChromats.toXMLString();
					saveChromatogramList.send( params );
				}
				// Release and Requeue
				if ( comboBox.selectedItem.@value == "REQUEUE" ) {
					for each ( var ch1:Object in selectedChromats ) {
						ch1.@released = "Y";
						ch1.@requeue = "Y";
					}
					var params1:Object = new Object;
					params1.chromatogramXMLString = selectedChromats.toXMLString();
					saveChromatogramList.send( params1 );
				}
			}
			
			
			private function onSaveChromatogramList( event:ResultEvent ):void {
				if ( event.result.name() == "SUCCESS" ) {
					this.getList();
					return;
				}
				Alert.show( event.result..ERROR.@message.toString(), "Error saving chromatogram list" );
				
			}
			
			
			private function promptToDelete():void {
				
				Alert.show( "Are you sure you want to delete these chromatograms?", "Confirm Delete", ( Alert.YES | Alert.NO ), this, onPromptToDelete, iconWarning );
			}
			
			private function onPromptToDelete( event:CloseEvent ):void {
				
				if ( event.detail == Alert.YES ) {
					deleteChromats();
				}
			}
			
			private function deleteChromats():void {
				var params:Object = new Object;
				params.chromatsToDeleteXMLString = "<chromatograms>" +  selectedChromats.toXMLString() + "</chromatograms>";
				deleteChromatograms.send(params);
			}
			
			private function onDeleteChromatograms(event:ResultEvent):void{
				if ( event.result.name() == "SUCCESS" ) {
					this.getList();
					return;
				}
				Alert.show( event.result..ERROR.@message.toString(), "Error deleting chromatogram list" );
				
			}
			
			
			private function selectedFilterFunc( item:XML ):Boolean {
				return item.@isSelected == true;
			}
			
			
			public function toggleDateCheckboxes( checkBoxNumber:int ):void {
				this.clearAllLookupFields();
				if ( currentState != userState.name ) {
					if ( checkBoxNumber == 1 ) {
						this.lastMonthCheckbox.selected = false;
						this.lastThreeMonthsCheckbox.selected = false;
					} else if ( checkBoxNumber == 2 ) {
						this.lastWeekCheckbox.selected = false;
						this.lastThreeMonthsCheckbox.selected = false;
					} else if ( checkBoxNumber == 3 ) {
						this.lastWeekCheckbox.selected = false;
						this.lastMonthCheckbox.selected = false;
					} 
				}
			}
			
			
			public function toggleReactionCheckboxes( checkBoxNumber:int ):void {
				this.clearAllLookupFields();
				if ( checkBoxNumber == 0 ) {
					this.mitSeqCheckbox.selected = false;
				} else if ( checkBoxNumber == 1 ) {
					this.capSeqCheckbox.selected = false;
				}
			}
			
			public function toggleReleaseCheckboxes( checkBoxNumber:int ):void {
				if ( checkBoxNumber == 1 ) {
					this.releasedCheckbox.selected = false;
				} else if ( checkBoxNumber == 2 ) {
					this.notReleasedCheckbox.selected = false;
				}
			}
			
			private function requestNoFieldSearch():void {
				if ( currentState != userState.name ) {
					actionDictionary.source = dictXML.action; 
				}
				if ( this.requestNoLookupField.text != '' ) {
					this.runNameLookupField.text = '';
					this.plateNameLookupField.text = '';
					clearAllCheckBoxes();
					getList();
				}
			}
			
						
			private function runNameFieldSearch():void {
				if ( currentState != userState.name ) {
					actionDictionary.source = dictXML.action; 
				}
				if ( this.runNameLookupField.text != '' ) {
					this.requestNoLookupField.text = '';
					this.plateNameLookupField.text = '';
					clearAllCheckBoxes();
					getList();
				}
			}
			
			private function clearAllLookupFields():void {
				this.requestNoLookupField.text = '';
				this.plateNameLookupField.text = '';
				this.runNameLookupField.text = '';
			}

			private function clearAllCheckBoxes():void {
				this.lastMonthCheckbox.selected = false;
				this.lastThreeMonthsCheckbox.selected = false;
				this.lastWeekCheckbox.selected = false;

				this.mitSeqCheckbox.selected = false;
				this.capSeqCheckbox.selected = false;
			}

			private function plateNameFieldSearch():void {
				if ( currentState != userState.name ) {
					actionDictionary.source = dictXML.action; 
				}
				if ( this.plateNameLookupField.text != '' ) {
					this.requestNoLookupField.text = '';
					this.runNameLookupField.text = '';
					clearAllCheckBoxes();
					getList();
				}
			}
			
			private function fieldNumericSorter( field:String ):Function {
				return function( obj1:Object, obj2:Object ):int {
					var num:Number = (( Number )( obj1.attribute( field )) - ( Number )( obj2.attribute( field )));
					return ( num > 0 ) ? 1 : (( num < 0 ) ? -1 : 0 );
				}
			}
			
			
			private function downloadFiles():void {
				selectedFiles = new XMLListCollection();
				for each (var c:Object in chromats) {
					if (c.@isSelected == 'true') {
						var node:XML = new XML("<FileDescriptor " +
							" fileName ='" + c.@qualifiedFilePath + "/" + c.@displayName + "'" +
							" zipEntryName='" + c.@requestNumber + "/" + c.@displayName + "'" +
							" type='ab1'"  +
							" fileSize='" + c.@fileSize + "'" +
							" requestNumber='" + c.@requestNumber + "'" + "/>");
						selectedFiles.addItem(node);
					}
				}
				
				filesExceedingMaxMessage = "";
				if (selectedFiles.length == 0) {
					Alert.show("Please select the files to download.");
				} else {
					estimatedDownloadSize = 0;
					for each (var item:Object in selectedFiles) {
						var size:Number = FileUtil.getEstimatedCompressedSize(item);
						if (size > parentApplication.MAX_DOWNLOAD_SIZE) {
							if (filesExceedingMaxMessage.length > 0) {
								filesExceedingMaxMessage += ", ";
							}
							filesExceedingMaxMessage += item.@displayName;
						}
						estimatedDownloadSize += size;
					}
					
					var params:Object = new Object();
					params.fileDescriptorXMLString = selectedFiles.toXMLString();
					var call:Object = cacheFileDownloadList.send(params);
					call.marker = estimatedDownloadSize;    		
				}
			}
			private function onCacheFileDownloadList(event:ResultEvent):void {
				var call:Object = event.token;
				var message:String = "";
				var title:String = "";
				var estimatedSize:Number = call.marker;
				if (filesExceedingMaxMessage != "") {
					message = "The following files exceed the " + parentApplication.MAX_DOWNLOAD_SIZE_DISPLAY + " maximum:\n" + this.filesExceedingMaxMessage + ".\n\n";
					if (parentApplication.isFDTSupported) {
						message += "Do you want to be download using FDT instead?";
					} else {
						message += "Please select a smaller number of files to download.";
						return;
					}
					title = "Download file exceeds maximum size"; 
					Alert.show( message,
						title,
						(Alert.OK | Alert.CANCEL), this, 
						onAlternateDownloadFiles,						
						parentApplication.iconWarning);
					
				} else if (estimatedSize >= parentApplication.MAX_DOWNLOAD_SIZE) {
					message =  
						"The total download size (" + FileUtil.getFileSizeText(call.marker) + ") exceeds the " + parentApplication.MAX_DOWNLOAD_SIZE_DISPLAY + " maximum.\n\n"
					if (parentApplication.isFDTSupported) {
						message += "Please choose FDT Download instead";
					} else {
						message += "You must unselect some files or click on the 'Download large files' link to download files individually."; 
					}
					title = "Total download size exceeds maximum size"; 
					Alert.show( message, title);
					
				} else {
					message = "Ready to download approximately " + FileUtil.getFileSizeText(call.marker) + " of data.  Proceed?";
					Alert.show( message,
						title,
						(Alert.YES | Alert.NO), this, 
						onDownloadFiles,						
						parentApplication.iconWarning);
				}
				
				
			}
			private function onDownloadFiles(event:CloseEvent):void {
				if (event.detail==Alert.YES) {
					var parms:URLVariables = new URLVariables();
					parms.mode =  "zip";
					var progressWindow:DownloadProgressView = new DownloadProgressView();
					progressWindow.init(this, this.DOWNLOAD_FILES_URL, parms, this.downloadButton, estimatedDownloadSize, this.SUGGESTED_LOCAL_FILENAME);		
				}
			}    
			
			private function onAlternateDownloadFiles(event:CloseEvent):void {
				if (event.detail==Alert.OK) {
					if (parentApplication.isFDTSupported) {
						downloadFilesUsingFDT();
					} else {
						Alert.show("Please select a smaller number of files to download.");	
					}
				} 		
			}
			public function downloadFilesUsingFDT():void {
				selectedFiles= new XMLListCollection();					
				for each (var c:Object in chromats) {
					if (c.@isSelected == 'true') {
						var node:XML = new XML("<FileDescriptor " +
							" fileName ='" + c.@qualifiedFilePath + "/" + c.@displayName + "'" +
							" zipEntryName='" + c.@requestNumber + "/" + c.@displayName + "'" +
							" type='ab1'"  +
							" fileSize='" + c.@fileSize + "'" +
							" requestNumber='" + c.@requestNumber + "'" + "/>");
						selectedFiles.addItem(node);
					}
				}
				
				if (selectedFiles.length == 0) {
					mx.controls.Alert.show("Please select the files to download.");
				} else {
					var params:Object = new Object();
					params.fileDescriptorXMLString = selectedFiles.toXMLString();
					cacheFDTFileDownloadList.send(params);
				}
			}
			
			private function onCacheFDTFileDownloadList(event:ResultEvent):void {
				// We have cached the selected analysis files
				// Now call the URL to start the FDT app using webstart
				showFastDataTransferDownloadPage();
			}
			
			
			public function showFastDataTransferDownloadPage():void {
				
				var paramStr:String =  selectedFiles.toXMLString();			
				var url:URLRequest = new URLRequest('FastDataTransferDownloadExpServlet.gx');	
				url.contentType = "text/xml";
				url.method = URLRequestMethod.GET;
				navigateToURL(url, '_self');					
				
			}
			
			
			public function sortByQuadrantAndPosition(item1:Object, item2:Object):int {
				var runNumber1:int = item1.@runNumber;
				var runNumber2:int = item2.@runNumber;
				
				var wellPos1:int = item1.@wellPosition;
				var wellPos2:int = item2.@wellPosition;
				
				var quadrant1:int = item1.@quadrant;
				var quadrant2:int = item2.@quadrant;
				
				if (runNumber1 > runNumber2) {
					return 1;
				} else if (runNumber2 > runNumber1) {
					return -1;
				} else {
					if (quadrant1 > quadrant2) {
						return 1;
					} else if (quadrant2 > quadrant1) {
						return -1;
					} else {
						if (wellPos1 > wellPos2) {
							return 1;
						} else if (wellPos2 > wellPos1) {
							return -1;
						} else {
							return 0;
						}
					}
					
				}
			}
			
			private function labelDataTipFunc(item:Object):String {
				return "View chromatogram details";
			}
			
		]]>
	</mx:Script>
	
	<mx:XMLListCollection id="chromats" source="{getChromatogramList.lastResult.Chromatogram}" />
	<mx:XMLListCollection id="selectedChromats" source="{getChromatogramList.lastResult.Chromatogram}" filterFunction="selectedFilterFunc" />
	
	<mx:VBox id="filterBox" width="100%" styleName="panelBox">
		<mx:HBox paddingLeft="2" paddingTop="2" paddingRight="2" horizontalGap="0" verticalAlign="middle" width="100%">
			<mx:Image source="assets/tv_chart_review.png"/>
			<mx:Label id="titleLabel" styleName="titleLabel" text="Results"  />
			<mx:Spacer width="30"/>
			
			
			
			<mx:HBox horizontalGap="0"
					 verticalAlign="middle"
					 verticalGap="0"
					 id="hbox1aa" paddingLeft="10" fontSize="10">
				<mx:Label text="Plate Name" />
				
				<mx:TextInput id="plateNameLookupField"
							  fontSize="9"
							  height="19"
							  width="80"
							  enter="{plateNameFieldSearch()}" />
			</mx:HBox>
			
			
			<mx:HBox horizontalGap="0"
					 height="100%"
					 verticalAlign="middle"
					 verticalGap="0"
					 id="hbox1aaa" paddingLeft="10" fontSize="10">
				<mx:Label text="Run Name" />
				
				<mx:TextInput id="runNameLookupField"
							  fontSize="9"
							  height="19"
							  width="70"
							  enter="{runNameFieldSearch()}" />
				
			</mx:HBox>
			
			
			<mx:HBox horizontalGap="0"
					 verticalAlign="middle"
					 verticalGap="0"
					 id="hbox1" paddingLeft="10">
				<mx:Label text="Order #"  fontSize="10"/>
				
				<mx:TextInput id="requestNoLookupField"
							  fontSize="9"
							  height="19"
							  width="50"
							  enter="{requestNoFieldSearch()}" />
			</mx:HBox>
			<mx:Spacer width="100%"/>
			<mx:LinkButton textDecoration="underline"
						   label="Refresh"
						   icon="@Embed('../../assets/action_refresh.gif')"
						   toolTip="Refresh chromatogram list"
						   click="{this.getList();}"  paddingLeft="8" id="linkbutton3" fontSize="10"/>
			
		</mx:HBox>
		<mx:VBox paddingLeft="2" width="100%" verticalGap="0" id="vbox1" >
			
			
			<mx:HBox id="filterBar"
					 horizontalGap="8"
					 paddingTop="4"
					 paddingBottom="4"
					 paddingLeft="0"
					 horizontalAlign="left"
					 verticalAlign="middle" fontSize="10">
				
				<mx:HBox horizontalGap="0"
						 verticalAlign="middle"
						 verticalGap="0"
						 horizontalAlign="right"
						 paddingLeft="5"
						 paddingRight="5">
					<mx:CheckBox id="capSeqCheckbox"
								 label="Sanger seq"
								 paddingRight="0"
								 paddingLeft="2"
								 horizontalGap="0"
								 selected="true" >
						<mx:click>
							actionDictionary.source = dictXML.action;
							toggleReactionCheckboxes( 0 );
							this.getList();
						</mx:click>
						
					</mx:CheckBox>
					<mx:CheckBox id="mitSeqCheckbox"
								 label="Mitochondrial seq"
								 paddingRight="0"
								 paddingLeft="2"
								 horizontalGap="0">
						<mx:click>
							toggleReactionCheckboxes( 1 );
							this.getList();
						</mx:click>
						
					</mx:CheckBox>
					
				</mx:HBox>
				<mx:VRule height="20" />
				
				<mx:HBox horizontalGap="0"
						 verticalAlign="middle"
						 verticalGap="0"
						 horizontalAlign="right"
						 paddingRight="5"
						 paddingLeft="5">
					<mx:CheckBox id="lastWeekCheckbox"
								 label="Created in last week"
								 paddingRight="0"
								 paddingLeft="0"
								 horizontalGap="0">
						<mx:click>
							toggleDateCheckboxes( 1 );
							this.getList();
						</mx:click>
						
					</mx:CheckBox>
					
					<mx:CheckBox id="lastMonthCheckbox"
								 label="month"
								 paddingRight="0"
								 paddingLeft="2"
								 horizontalGap="0">
						<mx:click>
							toggleDateCheckboxes( 2 );
							this.getList();
						</mx:click>
						
					</mx:CheckBox>
					
					<mx:CheckBox id="lastThreeMonthsCheckbox"
								 label="3 months"
								 paddingRight="0"
								 paddingLeft="2"
								 horizontalGap="0">
						<mx:click>
							toggleDateCheckboxes( 3 );
							this.getList();
						</mx:click>
						
					</mx:CheckBox>
					
				</mx:HBox>
				<mx:VRule height="20" />
				<mx:HBox horizontalGap="0"
						 verticalAlign="middle"
						 verticalGap="0"
						 horizontalAlign="right"
						 paddingLeft="5"
						 paddingRight="5">
					<mx:CheckBox id="notReleasedCheckbox"
								 label="Not released"
								 paddingRight="0"
								 paddingLeft="2"
								 horizontalGap="0" >
						<mx:click>
							toggleReleaseCheckboxes( 1 );
							this.getList();
						</mx:click>
						
					</mx:CheckBox>
					<mx:CheckBox id="releasedCheckbox"
								 label="Released"
								 paddingRight="0"
								 paddingLeft="2"
								 horizontalGap="0" >
						<mx:click>
							toggleReleaseCheckboxes( 2 );
							this.getList();
						</mx:click>
						
					</mx:CheckBox>
					
				</mx:HBox>
				
			</mx:HBox>
		</mx:VBox>
		
		
	</mx:VBox>
	
	
	
	
	
	<mx:VBox width="100%" height="100%" verticalGap="1">
		<util:DataGridAlternatingGroup id="chromatogramGrid" horizontalScrollPolicy="{ScrollPolicy.AUTO}"
									   allowMultipleSelection="true"
									   itemClick="gridItemClick(event)"
									   click="{selectedChromats.refresh();}"
									   width="100%"
									   height="100%"
									   dataProvider="{chromats}"
									   sortArrowSkin="mx.skins.ProgrammaticSkin" borderStyle="solid" fontSize="11">
			<util:columns>
				
				<mx:DataGridColumn id="checkBoxColumn"
								   textAlign="center"
								   width="30"
								   minWidth="30"
								   resizable="false"
								   sortable="false"
								   editable="true"
								   rendererIsEditor="true"
								   dataField="@isSelected"
								   editorDataField="selected"
								   itemRenderer="views.renderers.CheckBoxIsSelectedToggle"
								   headerRenderer="views.renderers.CheckBoxHeader"
								   draggable="false"/>
				
				<mx:DataGridColumn headerText=""
								   textAlign="center"
								   editable="false"
								   resizable="false"
								   sortable="false"
								   width="33" 
								   minWidth="33"
								   draggable="false">
					<mx:itemRenderer>
						<mx:Component>
							<mx:HBox horizontalAlign="center" >
								<mx:Image source="@Embed(source='../../assets/tv_chart.png')" toolTip="Download abi file" />
							</mx:HBox>
						</mx:Component>
					</mx:itemRenderer>						
				</mx:DataGridColumn>
				
				<mx:DataGridColumn dataField="@displayName" sortCompareFunction="sortByQuadrantAndPosition"
								   headerText="File"
								   minWidth="80"
								   textDecoration="underline"
								   color="#2017D8"
								   draggable="false"
								   showDataTips="true"
								   dataTipFunction="labelDataTipFunc">
					<mx:headerRenderer>
						<mx:Component>
							<mx:Label text="Label" textDecoration="none" color="#000000" />
						</mx:Component>
					</mx:headerRenderer>
				</mx:DataGridColumn>
				
				<mx:DataGridColumn headerText=""
								   textAlign="center"
								   editable="false"
								   resizable="false"
								   sortable="false"
								   width="33" 
								   minWidth="30"
								   draggable="false">
					<mx:itemRenderer>
						<mx:Component>
							<mx:HBox horizontalAlign="center" >
								<mx:Image source="@Embed(source='../../assets/chart_curve.png')" toolTip="View raw data" />
							</mx:HBox>
						</mx:Component>
					</mx:itemRenderer>						
				</mx:DataGridColumn>
				
				<mx:DataGridColumn dataField="@idPlateWellShort"
								   headerText="ID"
								   width="44" minWidth="20" />
				<mx:DataGridColumn dataField="@plateLabel"
								   headerText="Plate" 
								   width="90" minWidth="20" visible="{this.parentDocument is NavDataView}" />
				<mx:DataGridColumn dataField="@wellPosition" 
								   headerText="Well"
								   width="35" minWidth="10" visible="{this.parentDocument is NavDataView}"
								   sortCompareFunction="fieldNumericSorter('wellPosition')"/>
				
				<mx:DataGridColumn dataField="@requestNumber" 
								   headerText="Order #"
								   visible="{this.currentState != 'userState'}"
								   width="65" minWidth="15" />
				<mx:DataGridColumn dataField="@sampleName" 
								   headerText="Sample"
								   width="70" minWidth="15" />
				
				<mx:DataGridColumn dataField="@sampleType"
								   headerText="Type" 
								   width="90" minWidth="20" visible="{this.parentDocument is NavDataView}" />
					<mx:DataGridColumn dataField="@releaseDate" headerText="Released" visible="{this.currentState != 'userState' &amp;&amp; !this.releasedCheckbox.selected &amp;&amp; !this.notReleasedCheckbox.selected}"
									   width="70" id="releasedCheckBox">
					<mx:itemRenderer>
						<mx:Component>
							<mx:HBox  horizontalAlign="center" horizontalGap="1" >
								<mx:Image source="{data.@releaseDate != '' ? parentApplication.iconCheck : ''}">
								</mx:Image> 
							</mx:HBox>
						</mx:Component>
					</mx:itemRenderer>
				</mx:DataGridColumn>
				
				
				<mx:DataGridColumn dataField="@releaseDate" visible="{this.releasedCheckbox.selected}"
							   headerText="Released"
							   width="90"  id="datagridcolumn2"/>
			
				<mx:DataGridColumn dataField="@releaser" visible="{this.releasedCheckbox.selected}"
							   headerText="Releaser"
							   width="85"/>
				
				<mx:DataGridColumn width="50" headerText="Redo">
					<mx:itemRenderer>
						<mx:Component >
							<mx:HBox  horizontalAlign="center" horizontalGap="1" >
								<mx:Image source="{data.@redoFlag == 'Y' ? parentApplication.iconCheck : ''}">
								</mx:Image> 
							</mx:HBox>
						</mx:Component>
					</mx:itemRenderer>
				</mx:DataGridColumn>
				
				<mx:DataGridColumn dataField="@submitter"
								   headerText="Submitter" 
								   width="75" />
				
				<mx:DataGridColumn dataField="@readLength"
								   headerText="Len"
								   width="50"
								   sortCompareFunction="fieldNumericSorter('readLength')"  id="datagridcolumn3"/>
				
				<mx:DataGridColumn dataField="@trimmedLength"
								   headerText="Trim"
								   width="50"
								   sortCompareFunction="fieldNumericSorter('trimmedLength')"  id="datagridcolumn4"/>
				
				<mx:DataGridColumn dataField="@q20"
								   headerText="q20"
								   width="50"
								   sortCompareFunction="fieldNumericSorter('q20')"  id="datagridcolumn5"/>
				
				<mx:DataGridColumn dataField="@q40"
								   headerText="q40"
								   width="50"
								   sortCompareFunction="fieldNumericSorter('q40')"  id="datagridcolumn6"/>
				
				<mx:DataGridColumn dataField="@q20_len"
								   headerText="q20/len"
								   width="60"
								   sortCompareFunction="fieldNumericSorter('q20_len')" />
				
				<mx:DataGridColumn dataField="@q40_len"
								   headerText="q40/len"
								   width="60"
								   sortCompareFunction="fieldNumericSorter('q40_len')" />
				
				<mx:DataGridColumn headerText="Lane"
								   dataField="@lane"
								   width="50"/>
				
				<mx:DataGridColumn dataField="@aSignalStrength"
								   headerText="A sig"
								   width="62"
								   sortCompareFunction="fieldNumericSorter('aSignalStrength')" />
				
				<mx:DataGridColumn dataField="@cSignalStrength"
								   headerText="C sig"
								   width="62"
								   sortCompareFunction="fieldNumericSorter('cSignalStrength')" />
				
				<mx:DataGridColumn dataField="@gSignalStrength"
								   headerText="G sig"
								   width="62"
								   sortCompareFunction="fieldNumericSorter('gSignalStrength')" />
				
				<mx:DataGridColumn dataField="@tSignalStrength"
								   headerText="T sig"
								   width="62"
								   sortCompareFunction="fieldNumericSorter('tSignalStrength')" />
				
			</util:columns>
			
			
		</util:DataGridAlternatingGroup>		
		
	</mx:VBox>
	
	
	<mx:ControlBar horizontalAlign="left" width="100%" paddingBottom="2" paddingLeft="2" paddingRight="2" paddingTop="2" id="controlbar1">
		<mx:Label id="nSelectedChromats" text="{selectedChromats.length + ' selected'}" />
		
		<mx:ComboBox id="comboBox"
					 prompt="- Select an action -"
					 dataProvider="{actionDictionary}"
					 labelField="@display" />
		
		<mx:LinkButton textDecoration="underline"
					   label="Go"
					   icon="@Embed('../../assets/bullet_go.png')"
					   disabledIcon="@Embed('../../assets/bullet_go_disable.png')"
					   toolTip="Submit action"
					   click="{submitAction()}" 
					   enabled="{selectedChromats.length>0}"
					   id="linkbutton1"/>
		
		<mx:LinkButton textDecoration="underline"
					   label="Delete"
					   icon="@Embed('../../assets/delete.png')"
					   toolTip="Delete selected chromatograms"
					   click="{promptToDelete()}" 
					   disabledIcon="@Embed('../../assets/delete_disable.png')"
					   enabled="{selectedChromats.length>0}" id="linkbutton2"/>
		<mx:Spacer width="100%" id="spacer1"/>
		<mx:Label text="{'(' + this.chromats.length + ' chromatograms)'}" fontWeight="normal" textAlign="right"  fontStyle="italic"/>
		
	</mx:ControlBar>
	
	<mx:states>
		<mx:State id="userState" name="userState">
			
			<mx:SetProperty target="{datagridcolumn3}" name="headerText" value="Len"/>		
			<mx:AddChild relativeTo="{spacer1}" position="before">
				<mx:Button id="downloadButton" 
						   click="downloadFiles()" 
						   icon="@Embed('../../assets/download.png')" 
						   toolTip="Download selected files"
						   label="Download"/>
			</mx:AddChild>
			<mx:AddChild relativeTo="{spacer1}" position="before">
				<mx:Button visible="{parentApplication.isFDTSupported ? true : false}" 
						   includeInLayout="{parentApplication.isFDTSupported ? true : false}" 
						   id = "linkButtonDownloadFDT"
						   label="FDT Download"  
						   click="{this.downloadFilesUsingFDT()}" icon="@Embed(source='../../assets/download_large.png')"/>
			</mx:AddChild>
			<mx:RemoveChild target="{filterBox}"/>
			<mx:RemoveChild target="{comboBox}"/>
			<mx:RemoveChild target="{linkbutton1}"/>
			<mx:RemoveChild target="{linkbutton2}"/>
			
		</mx:State>
	</mx:states>
	
</mx:VBox>
