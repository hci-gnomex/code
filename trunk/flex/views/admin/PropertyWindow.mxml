<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" 
				width="1005" height="588" 
				 creationComplete="{init()}"
				 xmlns:util="views.util.*" 
				 showCloseButton="true" close="{PopUpManager.removePopUp(this);}"
				title="Configure Annotations">
	
<mx:HTTPService  
		id="saveProperty" 
		url="SaveProperty.gx"
		resultFormat="e4x"
		showBusyCursor="true"
		result="onSaveProperty(event)"
		fault="parentApplication.onFailHttpRequest('Failed to save sample annotation', event)"
		method="POST" 
		useProxy="false">
</mx:HTTPService>
	
<mx:HTTPService  
		id="deleteProperty" 
		url="DeleteProperty.gx"
		resultFormat="e4x"
		showBusyCursor="true"
		result="onDeleteProperty(event)"
		fault="parentApplication.onFailHttpRequest('Failed to delete sample annotation', event)"
		method="POST" 
		useProxy="false">
</mx:HTTPService>
		 
<mx:Script>
	<![CDATA[
		import flash.events.MouseEvent;
		import flash.geom.Point;
		
		import hci.flex.controls.DropdownLabel;
		
		import mx.collections.Sort;
		import mx.controls.Alert;
		import mx.controls.Label;
		import mx.managers.PopUpManager;
		import mx.rpc.events.ResultEvent;
		import mx.utils.StringUtil;
		
		import views.admin.PropertyEvent;
		import views.util.DeleteAnnotation;
		import views.util.DirtyNote;
		
		private var idProperty:Object = null;
		
		[Bindable]
		public var filteredAppList:XMLListCollection;
		[Bindable]
		private var applicationFilterList:XMLListCollection;
		
		private var filteredAppListSorter:Sort;
		
		private var removePropertyClick:Point;
			
		private function setDirty():void {
			if (saveButton.enabled) {
				dirty.setDirty();
			}
		}
		
		private function init():void {
			properties = new XMLListCollection();
			for each(var sc:XML in parentApplication.propertyList) {
				properties.addItem(sc.copy());
			}
			parentApplication.removeEventListener(PropertyEvent.DATA_REFRESHED,
				onPropertyRefreshed);		                                  
			parentApplication.addEventListener(PropertyEvent.DATA_REFRESHED,   
				onPropertyRefreshed);
			
			// We use this technique xml.copy() to prevent duplication of applications 
			// that somehow happens when we add services, then submit request or vice versa
			filteredAppList = new XMLListCollection();
			applicationFilterList = new XMLListCollection();
			for each(var i:Object in parentApplication.dictionaryManager.getEntries('hci.gnomex.model.Application')) {
				filteredAppList.addItem(i.copy());
				applicationFilterList.addItem(i.copy());
			}
			filteredAppListSorter = new Sort();
			filteredAppListSorter.compareFunction = this.sortApplication;
			filteredAppList.sort = filteredAppListSorter;	
			filteredAppList.filterFunction = filterApplication;
			filteredAppList.refresh();
			applicationFilterList.sort = filteredAppListSorter;	
			applicationFilterList.filterFunction = filterApplication1;
			applicationFilterList.refresh();
		}	
				
		public function onPropertyRefreshed(event:Event):void {
			properties = new XMLListCollection();
			for each(var sc:XML in parentApplication.propertyList) {
				if ((this.showSampleAnnotations.selected)
					|| (this.showDataTrackAnnotations.selected)
					|| (this.showAnalysisAnnotations.selected)){
					
					if(filterByAnnotations(sc) && appliesToFilter(sc)){
						properties.addItem(sc.copy());
					}
				}
				else if((organismFilter.selectedIndex > 0 
					|| analysisFilter.selectedIndex > 0
					|| platformFilter.selectedIndex > 0
					|| applicationFilter.selectedIndex > 0)
					&& appliesToFilter(sc)){
					
					properties.addItem(sc.copy());
				}
				else if(!this.showSampleAnnotations.selected && !this.showDataTrackAnnotations.selected
					&& !this.showAnalysisAnnotations.selected && organismFilter.selectedIndex == 0
					&& analysisFilter.selectedIndex == 0 && platformFilter.selectedIndex == 0 && (applicationFilter.selectedIndex == 0 || applicationFilter.selectedIndex == -1)){
					init();
					break;
				}
			}
			
			reselectCharacteristic()
			dirty.resetDirty();
		}
		private function filterByAnnotations(sc:XML):Boolean{
			var forSample:Boolean = false;
			var forDataTrack:Boolean = false;
			var forAnalysis:Boolean = false;
			
			if(showSampleAnnotations.selected && sc.@forSample == "Y"){
				forSample = true;
			}
			else if(!showSampleAnnotations.selected){
				forSample = true;
			}
			
			if(showAnalysisAnnotations.selected && sc.@forAnalysis == "Y"){
				forAnalysis = true;
			}
			else if(!showAnalysisAnnotations.selected){
				forAnalysis = true;
			}
			
			if(showDataTrackAnnotations.selected && sc.@forDataTrack == "Y"){
				forDataTrack = true;
			}
			else if(!showDataTrackAnnotations.selected){
				forDataTrack = true;
			}
			
			if(forSample && forAnalysis && forDataTrack){
				return true;
			}
			else{
				return false;
			}
		}
		
		private function appliesToFilter(sc:XML):Boolean{
			var hasOrganism:Boolean = false;
			var hasAnalysis:Boolean = false;
			var hasPlatform:Boolean = false;
			var hasApplication:Boolean = false;
			
			if(organismFilter.selectedIndex > 0){
				for each(var sc1:XML in sc.organisms.children()){
					if(sc1.@display == organismFilter.selectedLabel){
						hasOrganism = true;
						break;
					}
				}
			}
			else{
				hasOrganism = true;
			}
			
			if(analysisFilter.selectedIndex > 0){
				for each(var sc2:XML in sc.analysisTypes.children()){
					if(sc2.@display == analysisFilter.selectedLabel){
						hasAnalysis = true;
						break;
					}
				}
			}
			else{
				hasAnalysis = true;
			}
			
			if(platformFilter.selectedIndex > 0 && sc.platformApplications.length() > 0){
				for each(var sc3:XML in sc.platformApplications.children()){
					if(sc3.@display == platformFilter.selectedLabel){
						hasPlatform = true;
						break;
					}
				}
			}
			else{
				hasPlatform = true;
			}
			
			if(applicationFilter.selectedIndex > 0 && sc.platformApplications.length() > 0){
				for each(var sc4:XML in sc.platformApplications.children()){
					for each(var sc5:XML in sc4.application.children()){
						if(sc5.@application == applicationFilter.selectedLabel){
							hasApplication = true;
							break;
						}
					}
				}
			}
			else{
				hasApplication = true;
			}

			if(hasOrganism && hasPlatform && hasAnalysis && hasApplication){
				return true;
			}
			else{
				return false;
			}
		}
			
		private function reselectCharacteristic():void {
			var found:Boolean = false;
			if (idProperty != null) {
				for each(var sc:Object in properties) {
					if (sc.@idProperty == idProperty) {
						propertyList.selectedItem = sc;
						propertyList.scrollToIndex(propertyList.selectedIndex);
						found = true;
						break;
					}
				}
			}
			if (found) {
				theViews.selectedIndex = 1;
			} else {
				theViews.selectedIndex = 0;
			}
		}
			
		private function onSelectProperty():void {
			if (propertyList.selectedItem != null) {
				theViews.selectedIndex = 1;
				
				organismCombo.selectedItem = null;
				platformCombo.selectedItem = null;
				analysisTypeCombo.selectedItem = null;
	
				setOwnerCombo();
	
				dirty.resetDirty();
			}
		}
			
		private function setOwnerCombo():void {
			this.ownerCombo.selectedItem = null;
			this.ownerCombo.enabled = true;
			for each(var user:XML in parentApplication.promptedAppUserList) {
				if (user.@idAppUser == propertyList.selectedItem.@idAppUser) {
					this.ownerCombo.selectedItem = user;
					break;
				}
			}
			this.ownerCombo.enabled = parentApplication.hasPermission("canWriteDictionaries") ? true : false;
		}
			
		private function addOption():void {
			var newOption:XML = new XML("<PropertyOption " +
				" idPropertyOption='PropertyOption" + options.length + "'" +
				" option='enter option here..'" +
				" isActive='Y'" +
				" sortOrder='" + (options.length + 1) + "'" +
				" />");
			options.addItem(newOption);
			setDirty();
		}
		
		private function addBlank():void {
			if(options.length == 0) {
				var newOption:XML = new XML("<PropertyOption " +
					" idPropertyOption='PropertyOption0'" +
					" option=''" +
					" isActive='Y'" +
					" sortOrder='1'" +
					" />");
				options.addItem(newOption);				
			}
		}

	
		private function removeOption():void {
			if (this.optionGrid.selectedItem != null) {
				options.removeItemAt(options.getItemIndex(optionGrid.selectedItem));
				setDirty();
			}
		}
			 
		private function addOrganism():void {
			if (organismCombo.selectedItem != null && organismCombo.selectedItem.@value != '') {
				organisms.addItem(organismCombo.selectedItem);
				setDirty();			
			}
		}
		
		private function removeOrganism():void {
			if (this.organismsGrid.selectedItem != null) {
				organisms.removeItemAt(organisms.getItemIndex(organismsGrid.selectedItem));
				setDirty();
			}
		}
	
			
		private function addPlatform():void {
			if (platformCombo.selectedItem != null && platformCombo.selectedIndex > 0) {
				var codeRequestCategory:String = platformCombo.selectedItem.@codeRequestCategory;
				var codeApplication:String = "";
				var applicationDisplay:String = "";
				if (applicationCombo.selectedItem != null && applicationCombo.selectedIndex > 0) {
					codeApplication = applicationCombo.selectedItem.@codeApplication;
					applicationDisplay = applicationCombo.selectedItem.@application;		
				}
				for each(var pa:XML in platformApplications) {
					if(pa.@codeRequestCategory == codeRequestCategory && pa.@codeApplication == codeApplication) {
						Alert.show('This item is already on the list.');
						return;
					}
				}				
				
				var newPlatformApplication:XML = new XML("<PlatformApplication" +
					" idProperty='" + propertyList.selectedItem.@idProperty + "'" +
					" codeRequestCategory='" + codeRequestCategory  + "'" +
					" display='" + platformCombo.selectedItem.@display  + "'" +
					" codeApplication='" + codeApplication  + "'" +
					" applicationDisplay='" + applicationDisplay  + "'" +
					" />");
				platformApplications.addItem(newPlatformApplication);
				setDirty();			
			}
		}
		
		private function removePlatform():void {
			if (this.platformsGrid.selectedItem != null) {
				platformApplications.removeItemAt(platformApplications.getItemIndex(platformsGrid.selectedItem));
				setDirty();
			}
		}
		
		private function addAnalysisType():void {
			if (analysisTypeCombo.selectedItem != null && analysisTypeCombo.selectedItem.@value != '') {
				analysisTypes.addItem(analysisTypeCombo.selectedItem);
				setDirty();			
			}
		}
		
		private function removeAnalysisType():void {
			if (this.analysisTypesGrid.selectedItem != null) {
				analysisTypes.removeItemAt(analysisTypes.getItemIndex(analysisTypesGrid.selectedItem));
				setDirty();
			}
		}
			
		private function addProperty():void {
			var newCharacteristic:XML = new XML("<Property " +
				" idProperty=''" +
				" name='enter name here..'" +
				" isActive='Y'" +
				" canUpdate='Y'"  +
				" canDelete='Y'" + 
				" canRead='Y'" +
				" mageOntologyCode=''" + 
				" mageOntologyDefinition=''" + 
				" description=''" + 
				" idAppUser='" + (parentApplication.hasPermission("canWriteDictionaries") ? '' : parentApplication.getIdAppUser()) + "'" +
				" codePropertyType='TEXT'" +
				" forAnalysis='" + (this.showAnalysisAnnotations.selected ? "Y" : "N") + "'" +
				" forDataTrack='" + (this.showDataTrackAnnotations.selected ? "Y" : "N") + "'" +
				" forSample='" + (this.showSampleAnnotations.selected ? "Y" : "N") + "'" +
				" />");
			properties.addItemAt(newCharacteristic, 0);
			 
			
			propertyList.selectedItem = newCharacteristic;
			propertyList.scrollToIndex(propertyList.selectedIndex);
			idProperty = "";
			
			setOwnerCombo();
			
			setDirty();
	
			this.onSelectProperty()
		}
			
		public function removeProperty(event:MouseEvent, deleteAll:String):void {
			if (propertyList.selectedItem == null) {
				return;
			}
			idProperty = "";
			
			removePropertyClick = new Point(event.localX, event.localY);
			removePropertyClick = event.target.localToGlobal(removePropertyClick);
			
			
			var params:Object = new Object();
			params.idProperty = propertyList.selectedItem.@idProperty;
			params.deleteAll = deleteAll;
			deleteProperty.send(params);
		}
			
		private function onDeleteProperty(event:ResultEvent): void {
			if (deleteProperty.lastResult.name() == "SUCCESS") {
				parentApplication.getPropertyList.send();
				
			} else if (deleteProperty.lastResult.name() == "NONBLANKVALUES") {
				var deleteAnnotation:DeleteAnnotation = DeleteAnnotation(PopUpManager.createPopUp(this, DeleteAnnotation, false));
				deleteAnnotation.x = removePropertyClick.x - (deleteAnnotation.width/2);		
				deleteAnnotation.y = removePropertyClick.y;
				deleteAnnotation.init(this);
			} else {
				Alert.show(deleteProperty.lastResult..ACTMESSAGE.@TEXT);
			}
		}
			
			
		private function save():void {
			if (propertyList.selectedItem == null) {
				return;
			}
			
			scName.text = StringUtil.trim(scName.text);
			var propertyName:String = scName.text.toLowerCase();
			
			if(propertyName == "description") {
				Alert.show("'Description' is a reserved word. Please select a different name for the annotation.");
				return;
			}
			
			if(propertyName == "sample name") {
				Alert.show("'Sample Name' is a reserved term. Please select a different name for the annotation.");
				return;
			}
			
			if(propertyName == "organism") {
				Alert.show("'Organism' is a reserved word. Please select a different name for the annotation.");
				return;
			}
			
			if (!this.sample.selected && !this.dataTrack.selected && !this.analysis.selected) {
				Alert.show("Please choose object the annotation applies to.");
				this.sample.setFocus();
				return;
			}
			
			for each(var sc:Object in properties) {
				var thisPropName:String = StringUtil.trim(sc.@name);
				thisPropName = thisPropName.toLowerCase();
				if(propertyList.selectedItem != sc && propertyName == thisPropName) {
					Alert.show('"' + scName.text + '" has been used as the name for a previously defined annotation. Please choose another name.');
					return;
				}
			}
			
			var params:Object = new Object();
			params.idProperty = propertyList.selectedItem.@idProperty;
			params.name = scName.text;
			params.isActive = this.isActive.selected ? "Y" : "N";
			params.isRequired = this.isRequired.selected ? "Y" : "N";
			params.forSample = this.sample.selected ? "Y" : "N";
			params.forDataTrack = this.dataTrack.selected ? "Y" : "N";
			params.forAnalysis = this.analysis.selected ? "Y" : "N";
			params.mageOntologyCode = mageOntologyCode.text;
			params.mageOntologyDefinition = mageOntologyDefinition.text; 
			params.description = description.text; 
			if (parentApplication.hasPermission("canWriteDictionaries")) {
				params.idAppUser = this.ownerCombo.selectedItem != null ? this.ownerCombo.selectedItem.@idAppUser : "";			
			} else {
				params.idAppUser = parentApplication.getIdAppUser();
			}
			params.codePropertyType = this.characteristicTypeRadioGroup.selectedValue;
			params.optionsXMLString = "<options>" + this.options.toXMLString() + "</options>";
			params.organismsXMLString = "<organisms>" + this.organisms.toXMLString() + "</organisms>";
			params.platformsXMLString = "<platformApplications>" + this.platformApplications.toXMLString() + "</platformApplications>";
			params.analysisTypesXMLString = "<analysisTypes>" + this.analysisTypes.toXMLString() + "</analysisTypes>";
			
			saveProperty.send(params);
			
		}
			
		private function onSaveProperty(event:ResultEvent): void {
			if (saveProperty.lastResult.name() == "SUCCESS") {
				idProperty = saveProperty.lastResult.@idProperty;
				parentApplication.getPropertyList.send();
				dispatchEvent(new Event("updateProperties"));
			} else {
				Alert.show(saveProperty.lastResult..ACTMESSAGE.@TEXT);
			}
		}	
			
		private function filterApplication(item:Object):Boolean {
			var doesMatchRequestCategory:Boolean = false;
			if (platformCombo.selectedItem != null && platformCombo.selectedItem.@value != '') {
				var codeRequestCategory:String = platformCombo.selectedItem.@value;
				if (item.@value == "") {
					// Leave blank item on the list
					return true;
				} 
				
				if (item.@isActive == 'N') {
					return false;
				}
				
				var theApplications:XMLList = parentApplication.dictionaryManager.xml.Dictionary.(@className == 'hci.gnomex.model.RequestCategoryApplication').DictionaryEntry.(@value != '' && @codeApplication == item.@value.toString());
				for each (var xref:Object in theApplications) {
					if (xref.@codeRequestCategory.toString() == codeRequestCategory) {
						doesMatchRequestCategory = true;
						break;
					}
				}
			}
			
			
			return doesMatchRequestCategory;
		}
		
		private function filterApplication1(item:Object):Boolean {
			var doesMatchRequestCategory:Boolean = false;
			if(platformFilter.selectedIndex == 0){
				return true;
			}
			else {
				var codeRequestCategory:String = platformFilter.selectedItem.@value;
				if (item.@value == "") {
					return false;
				} 
				
				if (item.@isActive == 'N') {
					return false;
				}
				
				var theApplications:XMLList = parentApplication.dictionaryManager.xml.Dictionary.(@className == 'hci.gnomex.model.RequestCategoryApplication').DictionaryEntry.(@value != '' && @codeApplication == item.@value.toString());
				for each (var xref:Object in theApplications) {
					if (xref.@codeRequestCategory.toString() == codeRequestCategory) {
						doesMatchRequestCategory = true;
						break;
					}
				}
			}
			
			
			return doesMatchRequestCategory;
		} 
		
		// Sort application by sortOrder field
		private function sortApplication(obj1:Object, obj2:Object, fields:Array=null):int {
			if (obj1 == null && obj2 == null) {
				return 0;
			} else if (obj1 == null) {
				return 1;
			} else if (obj2 == null) {
				return -1;
			} else {
				var app1:String = obj1.@application;
				var app2:String = obj2.@application;
				
				app1 = app1.toLowerCase();
				app2 = app2.toLowerCase();
				
				if (app1 < app2) {
					return -1;
				} else if (app1 > app2) {
					return 1;
				} else {
					return 0;
				}
			}			
		}
		
		public function sortPlatformApplications(obj1:Object, obj2:Object, fields:Array=null):int {
			if (obj1 == null && obj2 == null) {
				return 0;
			} else if (obj1 == null) {
				return 1;
			} else if (obj2 == null) {
				return -1;
			} else {
				var ed1:String = obj1.@display;
				var ed2:String = obj2.@display;			

				if (ed1 < ed2) {
					return -1;
				} else if (ed1 > ed2) {
					return 1;
				} else {
					ed1 = obj1.@applicationDisplay;
					ed2 = obj2.@applicationDisplay;					
					if (ed1 < ed2) {
						return -1;
					} else if (ed1 > ed2) {
						return 1;
					} else {				
						return 0;
					}
				}
			}
		}
		
		private function refreshApplicationFilter():void {
			filteredAppList.filterFunction = filterApplication;
			filteredAppList.refresh();
		}
		
		private function refresh(event:Event):void{
			organismFilter.selectedIndex = 0;
			analysisFilter.selectedIndex = 0;
			platformFilter.selectedIndex = 0;
			showAnalysisAnnotations.selected = false;
			showDataTrackAnnotations.selected = false;
			showSampleAnnotations.selected = false;
			
			init();
		}
		
		private function applicationFilterRefresh():void{
			applicationFilterList.filterFunction = filterApplication1;
			applicationFilterList.refresh();
			onPropertyRefreshed(null);
		}
	]]>
</mx:Script>
	
<mx:XMLListCollection id="properties"/>
<mx:XMLListCollection id="options" source="{propertyList.selectedItem.options.PropertyOption}"/>
<mx:XMLListCollection id="organisms" source="{propertyList.selectedItem.organisms.Organism}"/>
<mx:XMLListCollection id="platformApplications" source="{propertyList.selectedItem.platformApplications.PlatformApplication}" sort="{platformApplicationSorter}"/>
<mx:Sort id="platformApplicationSorter" compareFunction="sortPlatformApplications" unique="true"/>	
<mx:XMLListCollection id="analysisTypes" source="{propertyList.selectedItem.analysisTypes.AnalysisType}"/>
<mx:VBox width="100%" height="100%" verticalGap="0" verticalAlign="middle" backgroundSize="100%" backgroundImage="@Embed(source='../../assets/filterBarGrey.png')" horizontalGap="0">
	<mx:HBox width="100%" verticalAlign="middle" horizontalGap="0">
		<mx:LinkButton label="Add annotation" click="addProperty()"  disabledIcon="@Embed(source='../../assets/page_add_disable.png')" icon="@Embed(source='../../assets/page_add.png')"  fontSize="12"/>
		<mx:LinkButton label="Remove annotation" styleName="linkMenuButton" mouseUp="removeProperty(event, 'N')" enabled="{propertyList.selectedItem != null &amp;&amp; propertyList.selectedItem.@canDelete == 'Y'}"   disabledIcon="@Embed(source='../../assets/page_delete_disable.png')" icon="@Embed(source='../../assets/page_delete.png')" fontSize="12"/>
		<mx:Spacer width="100%"/>
		<mx:LinkButton label="Refresh" icon="@Embed(source='../../assets/refresh.png')" click="refresh(event)"/>
	</mx:HBox>
	<mx:HBox width="100%" verticalAlign="middle" horizontalGap="5">
		<mx:Label text="Show Annotations For:" fontSize="12"/>
		<mx:CheckBox label="Sample" id="showSampleAnnotations" click="onPropertyRefreshed(null)" selected="false" fontSize="11"/>
		<mx:VRule height = "20" />
		<mx:CheckBox label="Analysis" id="showAnalysisAnnotations" click="onPropertyRefreshed(null)" selected="false" fontSize="11"/>
		<mx:VRule height = "20" />
		<mx:CheckBox label="DataTrack" id="showDataTrackAnnotations" click="onPropertyRefreshed(null)" selected="false" fontSize="11"/>
		<mx:VRule height = "20" />
		<mx:Label text="Organism:"/>
		<mx:ComboBox id="organismFilter" width="211" itemRenderer="mx.controls.Label" dataProvider="{parentApplication.dictionaryManager.xml.Dictionary.(@className == 'hci.gnomex.model.OrganismLite').DictionaryEntry}" change="onPropertyRefreshed(null)" labelField="@display" fontSize="10"/>
		<mx:VRule height = "20" />
		<mx:Label text="Analysis:" paddingRight="15"/>
		<mx:ComboBox id="analysisFilter" width="211" itemRenderer="mx.controls.Label" dataProvider="{parentApplication.dictionaryManager.xml.Dictionary.(@className == 'hci.gnomex.model.AnalysisType').DictionaryEntry}" change="onPropertyRefreshed(null)" labelField="@display" fontSize="10"/>
	</mx:HBox>
	<mx:HBox width="100%" verticalAlign="middle" horizontalGap="5">
		<mx:Spacer width="409" height="2"/>
		<mx:Label text="Platform:" paddingRight="7"/>
		<mx:ComboBox id="platformFilter" width="211" itemRenderer="mx.controls.Label" dataProvider="{parentApplication.dictionaryManager.xml.Dictionary.(@className == 'hci.gnomex.model.RequestCategory').DictionaryEntry}" change="applicationFilterRefresh()" labelField="@display" fontSize="10"/>
		<mx:VRule height = "20" />
		<mx:Label text="Application:"/>
		<mx:ComboBox id="applicationFilter" width="211" itemRenderer="mx.controls.Label" dataProvider="{applicationFilterList}" change="onPropertyRefreshed(null)" labelField="@application" fontSize="10"/>
	</mx:HBox>
	
<mx:HDividedBox height="100%" width="100%" >
  <mx:VBox height="100%" width="200" styleName="panelBox">
		<mx:List id="propertyList" width="100%" height="100%" labelField="@name" dataProvider="{properties}" 
				 itemClick="{onSelectProperty()}">
		</mx:List>
  </mx:VBox>	
  <mx:ViewStack id="theViews"  width="100%" height="100%" creationPolicy="all">
	<mx:VBox width="100%" height="100%" verticalGap="0" styleName="panelBox" paddingLeft="4" paddingTop="4" paddingRight="4" paddingBottom="0"/>		
		<mx:VBox width="100%" height="100%" verticalGap="1" styleName="panelBox" paddingLeft="4" paddingTop="4" paddingRight="4" paddingBottom="0">		
			<mx:TabNavigator tabWidth="140" width="100%" height="100%">
				<mx:VBox  label="Annotation" width="100%" height="100%" styleName="form" paddingTop="4" paddingBottom="4" paddingRight="4" paddingLeft="4" verticalGap="10" borderStyle="none">			 
					<mx:HBox width="100%" verticalGap="0">
						<mx:Label text="Name:" width="140"/>
						<mx:TextInput text="{propertyList.selectedItem.@name}" change="{setDirty()}" width="100%" id="scName"  maxChars="50"/>
						<mx:HBox >
							<mx:CheckBox label="Active" id="isActive"  click="setDirty()">
								<mx:selected>{propertyList.selectedItem.@isActive == 'Y' ? true : false}</mx:selected>
							</mx:CheckBox>
							<mx:Spacer width="80"/>
						</mx:HBox>
					</mx:HBox>
					<mx:HBox width="100%" verticalGap="0" height="65">
						<mx:Text text="Description:" width="140"/>
						<mx:TextArea text="{propertyList.selectedItem.@description}" change="{setDirty()}" width="100%" height="65" id="description" />
					</mx:HBox>
					<mx:HBox width="100%">
						<mx:Label text="Owner:" width="140"/>
						<mx:ComboBox width="200" id="ownerCombo" change="{setDirty()}"  labelField="@displayName" dataProvider="{parentApplication.promptedAppUserList}" ></mx:ComboBox>
					</mx:HBox>
					<mx:HBox width="100%"  verticalAlign="middle">
						<mx:Label text="Required:" width="140"/>
						<mx:CheckBox label="(Annotation must be filled in)" id="isRequired"  click="setDirty()">
							<mx:selected>{propertyList.selectedItem.@isRequired == 'Y' ? true : false}</mx:selected>
						</mx:CheckBox>
					</mx:HBox>
					<mx:HBox height="20" width="100%"  verticalAlign="middle">
						<mx:HBox verticalAlign="middle">
							<mx:Text text="Annotation Applies To:" width="140"/>
							<mx:HBox>
								<mx:CheckBox label="Sample" id="sample" click="setDirty()">
									<mx:selected>{propertyList.selectedItem.@forSample == 'Y' ? true : false}</mx:selected>
								</mx:CheckBox>
								<mx:CheckBox label="Data Track" id="dataTrack" click="setDirty()">
									<mx:selected>{propertyList.selectedItem.@forDataTrack == 'Y' ? true : false}</mx:selected>
								</mx:CheckBox>
								<mx:CheckBox label="Analysis" id="analysis" click="setDirty()">
									<mx:selected>{propertyList.selectedItem.@forAnalysis == 'Y' ? true : false}</mx:selected>
								</mx:CheckBox>
							</mx:HBox>
						</mx:HBox>
					</mx:HBox>
					<mx:HBox width="100%" verticalGap="0" height="100%">
						<mx:Label text="Type:" width="140"/>
						<mx:VBox verticalGap="0">
							<mx:RadioButtonGroup id="characteristicTypeRadioGroup"  selectedValue="{propertyList.selectedItem.@codePropertyType}">
							</mx:RadioButtonGroup>
							<mx:RadioButton label="Text"  groupName="characteristicTypeRadioGroup" value="TEXT" paddingTop="0" paddingBottom="0"  click="setDirty()" />
							<mx:RadioButton label="URL"  groupName="characteristicTypeRadioGroup" value="URL" paddingBottom="0" paddingTop="0"  click="setDirty()" />
							<mx:RadioButton label="Checkbox"  groupName="characteristicTypeRadioGroup" value="CHECK" paddingTop="0" paddingBottom="0"  click="setDirty()" />
							<mx:RadioButton label="Dropdown (single selection)"  groupName="characteristicTypeRadioGroup" value="OPTION" paddingBottom="0" paddingTop="0"  click="addBlank();setDirty()" />
							<mx:RadioButton label="Dropdown (multiple selection)"  groupName="characteristicTypeRadioGroup" value="MOPTION" paddingBottom="0" paddingTop="0" click="setDirty()" />
						</mx:VBox>
						<mx:HBox width="100%"  visible="{characteristicTypeRadioGroup.selectedValue == 'OPTION' || characteristicTypeRadioGroup.selectedValue == 'MOPTION'}" verticalGap="0" height="100%">
							<mx:VBox verticalGap="0" height="100%" width="100%">
								<mx:HBox>
									<mx:LinkButton label="Add option" styleName="linkMenuButton" click="addOption()"   enabled="{propertyList.selectedItem.@canUpdate == 'Y' ? true : false}" icon="@Embed(source='../../assets/add.png')" disabledIcon="@Embed(source='../../assets/add_disable.png')"/>
									<mx:LinkButton label="Remove option" styleName="linkMenuButton" click="removeOption()" enabled="{propertyList.selectedItem.@canUpdate == 'Y' &amp;&amp; optionGrid.selectedItem != null}"   icon="@Embed(source='../../assets/delete.png')" disabledIcon="@Embed(source='../../assets/delete_disable.png')"/>
								</mx:HBox>
								<mx:DataGrid id="optionGrid" dataProvider="{options}" editable="{propertyList.selectedItem.@canUpdate == 'Y' ? true : false}" width="100%" showHeaders="true"  height="100%">
									<mx:columns>
										<mx:DataGridColumn headerText="Option" width="120" dataField="@option"/>
										<mx:DataGridColumn headerText="Order"  width="25" dataField="@sortOrder"/>
										<mx:DataGridColumn headerText="Active"  width="25" dataField="@isActive"/>
									</mx:columns>
								</mx:DataGrid>
							</mx:VBox>
						</mx:HBox>
					</mx:HBox> 
				</mx:VBox>
				<mx:VBox label="Applies To" width="100%" height="100%" verticalGap="15" paddingLeft="4" paddingRight="4" paddingBottom="4">
					<mx:VBox   verticalGap="1" height="100%"  width="100%">
						<mx:Text text="Only show for these platforms/applications:" width="100%"/>
						<mx:HBox horizontalAlign="left" horizontalGap="2">
							<mx:ComboBox width="225" dataProvider="{parentApplication.dictionaryManager.xml.Dictionary.(@className == 'hci.gnomex.model.RequestCategory').DictionaryEntry}" id="platformCombo" labelField="@display" prompt="Select platform" change="refreshApplicationFilter();" >			       	
							</mx:ComboBox>
							<mx:ComboBox width="170" dataProvider="{filteredAppList}" id="applicationCombo" labelField="@application" prompt="Application (optional)" >			       	
							</mx:ComboBox>
							<mx:Button click="addPlatform()" icon="@Embed(source='../../assets/add.png')" disabledIcon="@Embed(source='../../assets/add_disable.png')" width="25">
								<mx:enabled>{propertyList.selectedItem.@canUpdate == 'Y' &amp;&amp; platformCombo.selectedItem != null &amp;&amp; platformCombo.selectedIndex > 0 ? true : false}</mx:enabled>
							</mx:Button>
							<mx:Button click="removePlatform()" icon="@Embed(source='../../assets/delete.png')" 
									   disabledIcon="@Embed(source='../../assets/delete_disable.png')" 
									   enabled="{propertyList.selectedItem.@canUpdate == 'Y' &amp;&amp; platformsGrid.selectedItem != null}" width="25"/>
						</mx:HBox>
						<mx:DataGrid id="platformsGrid" showHeaders="false" dataProvider="{platformApplications}" width="100%" height="100%">
							<mx:columns>
								<mx:DataGridColumn dataField="@display"/>
								<mx:DataGridColumn dataField="@applicationDisplay"/>
								<!--mx:DataGridColumn headerText="Application"  
								id="applicationCol"
								dataField="@codeApplication"  
								editorDataField="value" 
								sortable="true" 
								editable="true" 
								rendererIsEditor="false"  
								itemRenderer="{DropdownLabel.getFactory(parentApplication.dictionaryManager.xml.Dictionary.(@className=='hci.gnomex.model.Application').DictionaryEntry, '@display', '@value', '@codeApplication')}"                
								itemEditor="{views.renderers.ComboBox.create(parentApplication.dictionaryManager.xml.Dictionary.(@className=='hci.gnomex.model.Application').DictionaryEntry, '@display', '@value', '@codeApplication', null, true)}"/-->															 
							</mx:columns>
						</mx:DataGrid>			   	
					</mx:VBox>
					<mx:VBox width="100%" height="100%" verticalGap="0">
						<mx:VBox   verticalGap="1" height="100%"  width="100%">
							<mx:Text text="Only show for these organisms:" width="100%"/>
							<mx:HBox horizontalAlign="left" horizontalGap="2">
								<mx:ComboBox width="265" dataProvider="{parentApplication.dictionaryManager.xml.Dictionary.(@className == 'hci.gnomex.model.OrganismLite').DictionaryEntry}" id="organismCombo" labelField="@display" >			       	
								</mx:ComboBox>
								<mx:Button click="addOrganism()" icon="@Embed(source='../../assets/add.png')" disabledIcon="@Embed(source='../../assets/add_disable.png')" width="25">
									<mx:enabled>{propertyList.selectedItem.@canUpdate == 'Y' &amp;&amp; organismCombo.selectedItem != null &amp;&amp; organismCombo.selectedIndex > 0 ? true : false}</mx:enabled>
								</mx:Button>
								<mx:Button click="removeOrganism()" icon="@Embed(source='../../assets/delete.png')" 
										   disabledIcon="@Embed(source='../../assets/delete_disable.png')" 
										   enabled="{propertyList.selectedItem.@canUpdate == 'Y' &amp;&amp; organismsGrid.selectedItem != null}" width="25"/>
							</mx:HBox>
							<mx:DataGrid id="organismsGrid" showHeaders="false" dataProvider="{organisms}" width="100%" height="100%">
								<mx:columns>
									<mx:DataGridColumn dataField="@display" headerText="Name"/>
								</mx:columns>
							</mx:DataGrid>			   	
						</mx:VBox>
					</mx:VBox>
						<mx:VBox   verticalGap="1" height="100%"  width="100%">
							<mx:Label text="Only show for these analysis types:" width="100%"/>
								<mx:HBox horizontalAlign="left" horizontalGap="2">
									<mx:ComboBox width="265" dataProvider="{parentApplication.dictionaryManager.xml.Dictionary.(@className == 'hci.gnomex.model.AnalysisType').DictionaryEntry}" id="analysisTypeCombo" labelField="@display" >			       	
									</mx:ComboBox>
									<mx:Button click="addAnalysisType()" icon="@Embed(source='../../assets/add.png')" disabledIcon="@Embed(source='../../assets/add_disable.png')" width="25">
										<mx:enabled>{propertyList.selectedItem.@canUpdate == 'Y' &amp;&amp; analysisTypeCombo.selectedItem != null &amp;&amp; analysisTypeCombo.selectedIndex > 0 ? true : false}</mx:enabled>
									</mx:Button>
									<mx:Button click="removeAnalysisType()" icon="@Embed(source='../../assets/delete.png')" 
											   disabledIcon="@Embed(source='../../assets/delete_disable.png')" 
											   enabled="{propertyList.selectedItem.@canUpdate == 'Y' &amp;&amp; analysisTypesGrid.selectedItem != null}" width="25"/>
								</mx:HBox>
								<mx:DataGrid id="analysisTypesGrid" showHeaders="false" dataProvider="{analysisTypes}" width="100%" height="100%">
									<mx:columns>
										<mx:DataGridColumn dataField="@display" headerText="Name"/>
									</mx:columns>
								</mx:DataGrid>			   	
						</mx:VBox>				 
					
				</mx:VBox>
				<mx:VBox width="100%" height="100%" label="Mage Ontology" paddingLeft="4" paddingRight="4" paddingBottom="4">
					<mx:HBox width="100%" verticalGap="0">
						<mx:Text text="Mage Ontology Code:" width="80"/>
						<mx:TextInput text="{propertyList.selectedItem.@mageOntologyCode}" change="{setDirty()}" width="192" id="mageOntologyCode"  maxChars="100"/>
						<mx:Text text="Mage Ontology Definition:" width="65"/>
						<mx:TextArea text="{propertyList.selectedItem.@mageOntologyDefinition}" change="{setDirty()}" width="100%" id="mageOntologyDefinition"  maxChars="100"/>
					</mx:HBox>
					
				</mx:VBox>
				</mx:TabNavigator>
			<mx:ControlBar width="100%" horizontalAlign="right"  paddingTop="0" paddingRight="0" paddingLeft="0" paddingBottom="0">
				<util:DirtyNote id="dirty" />
				<mx:Button id="saveButton" label="Save"  enabled="{propertyList.selectedItem.@canUpdate == 'Y' ? true : false}"
						   icon="@Embed('../../assets/action_save.gif')" click="save()" disabledIcon="@Embed(source='../../assets/save_disable.png')">
				</mx:Button>
				<mx:Button label="Close"  click="{PopUpManager.removePopUp(this);}"/>
				
			</mx:ControlBar>
		</mx:VBox>
  </mx:ViewStack>

	
</mx:HDividedBox>
</mx:VBox>
	
</mx:TitleWindow>
